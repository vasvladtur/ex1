## Демонстрация некоторых случаев с которыми я столкнулся.
 Сделано на 
 1С:Предприятие 8.3 (8.3.23.1782) 1С:ERP Управление предприятием 2 (2.5.12.53)
 
 Изменения где необходимо помечаются комментарием 
 ```
 //1729
 //-1729
 ```
 Реализовать можно и в расширении, но 
### Я решил:  меняем конфигурацию. (оригинальную сохраняем в безопасном месте)
 
 Задачи сгенерированы обычными пользователями
 
 Все идеи взяты из открытых источников. (Спасибо авторам)
 
 Новые сущности имеют префикс др_
 
 Изменения носят ДЕМО характер
 
1. Снимаем с поддержки

2. Создать форму списка справочника серии независящее от параметров открытия

2.1. Создать форму списка справочника серии (просто без кода, добавляем реквизиты справочника)
 делаем ее основной

2.2. Добавить в подсистему НСИ и администрирование -> НСИ -> Серии номенклатуры

3. Создать подсистему Расширение (Тестовое решение)

4. _
 

5. Создать возможность для (CRM и маркетинг)->(Скидки (наценки))->
(Создать элемент (например, 
- Тип скидки (скидка количеством),
- предоставляется на: Номенклатуру из списка, 
- нажать гиперссылка справа,
- кнопка Подобрать товары)
В этой форме релизовать: Добавить все характеристики номенклатуры при выборе номенклатуры


5.1. Решение

5.2. Добавить в форму обработки ПодборТоваровВДокументПродажи кнопку и команду

<details>

<summary>Просмотр кода</summary>

### Код формы

```
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

...
 	
//1729 Задача 5.1. Программно Добавить Команду и кнопку
// для добавления характеристик при выборе номенклатуры
	Команда = Команды.Добавить(
		"ДобавитьНоменклатуруСХарактеристиками"); //Имя команды
	Команда.Заголовок = "Добавить все Характеристики номенклатуры";
	Команда.Действие  = "др_ДобавитьНоменклатуруСХарактеристикамиПосле"; //Имя связанной процедуры
	 	
	//Добавление кнопки формы
	КнопкаФормы = Элементы.Добавить(
		"др_КнопкаДобавитьНоменклатуруСХарактеристиками", //Имя кнопки
		Тип("КнопкаФормы"),             //Тип, всегда КнопкаФормы
		КоманднаяПанель);                      //Контейнер для кнопки 
		
	КнопкаФормы.ИмяКоманды = "ДобавитьНоменклатуруСХарактеристиками"; //Связь с командой по имени
	
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка; 
//1729- Программно Добавить Команду и кнопку
	

	
КонецПроцедуры

```

</details>


5.3. Открыть форму Выбора номенклатуры

<details>

<summary>Просмотр кода</summary>

### Код формы

```

 //1729 Задача 5.1. Добавить все характеристики номенклатуры при выборе номенклатуры
&НаКлиенте
Процедура др_ДобавитьНоменклатуруСХарактеристикамиПосле(Команда)
	ОповещениеОзакрытии = Новый ОписаниеОповещения("др_ДобавитьНоменклатуруСХарактеристикамиЗавершение",ЭтотОбъект);
	параметрыОткрытия = Новый Структура("РежимВыбора,МножественныйВыбор,ЗакрыватьПриВыборе",Истина,Истина,Истина);
//	открыть форму выбора справочника номенклатура
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора",параметрыОткрытия,ЭтотОбъект,,,,ОповещениеОзакрытии );
	  
КонецПроцедуры

&НаКлиенте
Процедура др_ДобавитьНоменклатуруСХарактеристикамиЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт 
    Если ЗначениеЗаполнено(РезультатЗакрытия) Тогда
		др_ДобавитьНоменклатуруСХарактеристикамиЗавершениеСервер(РезультатЗакрытия);
	КонецЕсли;
КонецПроцедуры
 
&НаСервере
Процедура др_ДобавитьНоменклатуруСХарактеристикамиЗавершениеСервер(СписокТоваров)   
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Номенклатура,
	|	Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
	|	Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры
	|ПОМЕСТИТЬ втНоменклатураСВидомНоменклатуры
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка В(&СписокТоваров)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втНоменклатураСВидомНоменклатуры.Номенклатура КАК Номенклатура,
	|	ХарактеристикиНоменклатуры.Ссылка КАК Характеристика,
	|	1 КАК Количество,
	|	1 КАК КоличествоУпаковок,
	|	ХарактеристикиНоменклатуры.Ссылка КАК Артикул,
	|	Ложь КАК Обособленно,
	|	Истина КАК ХарактеристикиИспользуются,
	|	втНоменклатураСВидомНоменклатуры.ТипНоменклатуры КАК ТипНоменклатуры,
	|	Ложь КАК ПроизводитсяВПроцессе,
	|	Истина КАК ЗаказатьНаСклад
	|ИЗ
	|	втНоменклатураСВидомНоменклатуры КАК втНоменклатураСВидомНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО (втНоменклатураСВидомНоменклатуры.ВидНоменклатуры = (ВЫРАЗИТЬ(ХарактеристикиНоменклатуры.Владелец КАК Справочник.ВидыНоменклатуры)))
	|ГДЕ
	|	НЕ ХарактеристикиНоменклатуры.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура";
	
	Запрос.УстановитьПараметр("СписокТоваров", СписокТоваров);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл                               
		НоваяСтрока = Объект.Корзина.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,ВыборкаДетальныеЗаписи);
		Элементы.Корзина.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
	КонецЦикла;

КонецПроцедуры
//-1729 

```


</details>

5.4. Обработать выбор

### Учет ярлыков. Ярлыки не номенклатура. 

- Компания приобретает Ярлыки у стороних Организаций.
- Ярлыки учитываются по определенной Организации и бывают платными и бесплатными и поступают на выбранный складу.
- Автоматически списываются оператором .
- Платные ярлыки выгружаются в бухгалтерию.
- Учет количественный


6. Справочник 	др_Ярлыки, Перечисление 	др_ТипыЯрлыков (Платный, бесплатный															

7. Создать документ др_ПриобретениеЯрлыков 

<details>

<summary>Структура метаданных документа др_ПриобретениеЯрлыков</summary>

### Структура метаданных документа др_ПриобретениеЯрлыков

```
//1729 Задача 7.6. 
др_ПриобретениеЯрлыков

реквизит
-Организация

таб часть
	Ярлыки
    - др_Ярлык
    - Склад
    - Количество

```

</details>



7.1. Настроить интерактивно проверку заполнения для др_Ярлык и Склад

7.2. Настроить программно проверку заполнения для Склад (если Тип ярлыка бесплатный не проверять)

<details>

<summary>Просмотр кода</summary>

### Код общего модуля

```
//1729 Задача 7.2. 
        
#Область ПрограммныйИнтерфейс



#Область Оформление

// Создает элемент условного ооформления одного параметра одного поля по одному отбору.
// Параметры:
//  Форма                        - ФормаКлиентскогоПриложения       - На которую необходимо добавить условное оформление
//  ОформляемоеПоле              - Строка, Неопределено             - Имя офомляемого элемента формы. Например, "Товары". Если не передавать, то оформляемое поле не будет добавлено
//  ИмяПараметраОформления       - Строка, ПараметрКомпоновкиДанных - Указывает что будет изменено в офомлении поля. Например, "ЦветФона" / "Текст"
//  ЗначениеПараметраОформления  - Произвольный                     - Значение оформления. Например, <Новый Цвет(222, 255, 222)>
//  ПутьКЛевомуЗначениюОтбора    - Строка, Неопределено             - Путь к полю по которому выполняется отбор условного оформления. Например, "Товары.Номенклатура" / "Контрагент". Если не передавать, то отбор не будет установлен
//  ВидСравненияОтбора           - ВидСравненияКомпоновкиДанных     - Вид сравнения. По умолчанию "ВидСравненияКомпоновкиДанных.Равно"
//  ПравоеЗначениеОтбора         - Произвольный                     - С чем сравнивать левое значение
//
// ВозвращаемоеЗначение:
//  ЭлементУсловногоОформления - Добавленный элемент оформления
Функция ДобавитьПростойЭлементУсловногоОформления(Форма, ОформляемоеПоле = Неопределено, 
	ИмяПараметраОформления = Неопределено, ЗначениеПараметраОформления = Неопределено, 
	ПутьКЛевомуЗначениюОтбора = Неопределено, ВидСравненияОтбора = Неопределено, ПравоеЗначениеОтбора = Неопределено) Экспорт
	
	//Добавляем элемент:
	ЭлементУсловногоОформления = Форма.УсловноеОформление.Элементы.Добавить();
	
	Если ЗначениеЗаполнено(ИмяПараметраОформления) Тогда
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра(ИмяПараметраОформления, ЗначениеПараметраОформления);
	КонецЕсли;
	
	Если НЕ ПутьКЛевомуЗначениюОтбора = Неопределено Тогда
		ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(ПутьКЛевомуЗначениюОтбора);
		ОтборЭлемента.ВидСравнения   = ?(ВидСравненияОтбора = Неопределено, ВидСравненияКомпоновкиДанных.Равно, ВидСравненияОтбора);
		ОтборЭлемента.ПравоеЗначение = ПравоеЗначениеОтбора;
	КонецЕсли;
	
	Если НЕ ОформляемоеПоле = Неопределено Тогда
		ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ОформляемоеПоле);
	КонецЕсли;

	Возврат ЭлементУсловногоОформления;
	
КонецФункции

#КонецОбласти

#Область ДинамическийСписок

////////////////////////////////////////////////////////////////////////////////
// Функции для работы с отборами и параметрами динамических списков.
//

// Найти элемент или группу отбора по заданному имени поля или представлению.
//
// Параметры:
//  ОбластьПоиска - ОтборКомпоновкиДанных
//                - КоллекцияЭлементовОтбораКомпоновкиДанных
//                - ГруппаЭлементовОтбораКомпоновкиДанных    - контейнер с элементами и группами отбора,
//                                                             например Список.Отбор или группа в отборе.
//  ИмяПоля       - Строка - имя поля компоновки (не используется для групп).
//  Представление - Строка - представление поля компоновки.
//
// Возвращаемое значение:
//  Массив - коллекция отборов.
//
Функция НайтиЭлементыИГруппыОтбора(Знач ОбластьПоиска,
									Знач ИмяПоля = Неопределено,
									Знач Представление = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ИмяПоля) Тогда
		ЗначениеПоиска = Новый ПолеКомпоновкиДанных(ИмяПоля);
		СпособПоиска = 1;
	Иначе
		СпособПоиска = 2;
		ЗначениеПоиска = Представление;
	КонецЕсли;
	
	МассивЭлементов = Новый Массив;
	
	НайтиРекурсивно(ОбластьПоиска.Элементы, МассивЭлементов, СпособПоиска, ЗначениеПоиска);
	
	Возврат МассивЭлементов;
	
КонецФункции

// Добавить группу отбора в коллекцию КоллекцияЭлементов.
//
// Параметры:
//  КоллекцияЭлементов - ОтборКомпоновкиДанных
//                     - КоллекцияЭлементовОтбораКомпоновкиДанных
//                     - ГруппаЭлементовОтбораКомпоновкиДанных    - контейнер с элементами и группами отбора,
//                                                                  например Список.Отбор или группа в отборе.
//  Представление      - Строка - представление группы.
//  ТипГруппы          - ТипГруппыЭлементовОтбораКомпоновкиДанных - тип группы.
//
// Возвращаемое значение:
//  ГруппаЭлементовОтбораКомпоновкиДанных - группа отбора.
//
Функция СоздатьГруппуЭлементовОтбора(Знач КоллекцияЭлементов, Представление, ТипГруппы) Экспорт
	
	Если ТипЗнч(КоллекцияЭлементов) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных")
		Или ТипЗнч(КоллекцияЭлементов) = Тип("ОтборКомпоновкиДанных") Тогда
		
		КоллекцияЭлементов = КоллекцияЭлементов.Элементы;
	КонецЕсли;
	
	ГруппаЭлементовОтбора = НайтиЭлементОтбораПоПредставлению(КоллекцияЭлементов, Представление);
	Если ГруппаЭлементовОтбора = Неопределено Тогда
		ГруппаЭлементовОтбора = КоллекцияЭлементов.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	Иначе
		ГруппаЭлементовОтбора.Элементы.Очистить();
	КонецЕсли;
	
	ГруппаЭлементовОтбора.Представление    = Представление;
	ГруппаЭлементовОтбора.Применение       = ТипПримененияОтбораКомпоновкиДанных.Элементы;
	ГруппаЭлементовОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ГруппаЭлементовОтбора.ТипГруппы        = ТипГруппы;
	ГруппаЭлементовОтбора.Использование    = Истина;
	
	Возврат ГруппаЭлементовОтбора;
	
КонецФункции

// Добавить элемент компоновки в контейнер элементов компоновки.
//
// Параметры:
//  ОбластьДобавления - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                                                 например, Список.Отбор или группа в отборе.
//  ИмяПоля                 - Строка - имя поля компоновки данных (заполняется всегда).
//  ВидСравнения            - ВидСравненияКомпоновкиДанных - вид сравнения.
//  ПравоеЗначение          - Произвольный - сравниваемое значение.
//  Представление           - Строка - представление элемента компоновки данных.
//  Использование           - Булево - использование элемента.
//  РежимОтображения        - РежимОтображенияЭлементаНастройкиКомпоновкиДанных - режим отображения.
//  ИдентификаторПользовательскойНастройки - Строка - см. ОтборКомпоновкиДанных.ИдентификаторПользовательскойНастройки
//                                                    в синтакс-помощнике.
// Возвращаемое значение:
//  ЭлементОтбораКомпоновкиДанных - элемент компоновки.
//
Функция ДобавитьЭлементКомпоновки(ОбластьДобавления,
									Знач ИмяПоля,
									Знач ВидСравнения,
									Знач ПравоеЗначение = Неопределено,
									Знач Представление  = Неопределено,
									Знач Использование  = Неопределено,
									знач РежимОтображения = Неопределено,
									знач ИдентификаторПользовательскойНастройки = Неопределено) Экспорт
	
	Элемент = ОбластьДобавления.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Элемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоля);
	Элемент.ВидСравнения = ВидСравнения;
	
	Если РежимОтображения = Неопределено Тогда
		Элемент.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	Иначе
		Элемент.РежимОтображения = РежимОтображения;
	КонецЕсли;
	
	Если ПравоеЗначение <> Неопределено Тогда
		Элемент.ПравоеЗначение = ПравоеЗначение;
	КонецЕсли;
	
	Если Представление <> Неопределено Тогда
		Элемент.Представление = Представление;
	КонецЕсли;
	
	Если Использование <> Неопределено Тогда
		Элемент.Использование = Использование;
	КонецЕсли;
	
	// Важно: установка идентификатора должна выполняться
	// в конце настройки элемента, иначе он будет скопирован
	// в пользовательские настройки частично заполненным.
	Если ИдентификаторПользовательскойНастройки <> Неопределено Тогда
		Элемент.ИдентификаторПользовательскойНастройки = ИдентификаторПользовательскойНастройки;
	ИначеЕсли Элемент.РежимОтображения <> РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
		Элемент.ИдентификаторПользовательскойНастройки = ИмяПоля;
	КонецЕсли;
	
	Возврат Элемент;
	
КонецФункции

// Изменить элемент отбора с заданным именем поля или представлением.
//
// Параметры:
//  ОбластьПоиска - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                                             например Список.Отбор или группа в отборе.
//  ИмяПоля                 - Строка - имя поля компоновки данных (заполняется всегда).
//  Представление           - Строка - представление элемента компоновки данных.
//  ПравоеЗначение          - Произвольный - сравниваемое значение.
//  ВидСравнения            - ВидСравненияКомпоновкиДанных - вид сравнения.
//  Использование           - Булево - использование элемента.
//  РежимОтображения        - РежимОтображенияЭлементаНастройкиКомпоновкиДанных - режим отображения.
//  ИдентификаторПользовательскойНастройки - Строка - см. ОтборКомпоновкиДанных.ИдентификаторПользовательскойНастройки
//                                                    в синтакс-помощнике.
//
// Возвращаемое значение:
//  Число - количество измененных элементов.
//
Функция ИзменитьЭлементыОтбора(ОбластьПоиска,
								Знач ИмяПоля = Неопределено,
								Знач Представление = Неопределено,
								Знач ПравоеЗначение = Неопределено,
								Знач ВидСравнения = Неопределено,
								Знач Использование = Неопределено,
								Знач РежимОтображения = Неопределено,
								Знач ИдентификаторПользовательскойНастройки = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ИмяПоля) Тогда
		ЗначениеПоиска = Новый ПолеКомпоновкиДанных(ИмяПоля);
		СпособПоиска = 1;
	Иначе
		СпособПоиска = 2;
		ЗначениеПоиска = Представление;
	КонецЕсли;
	
	МассивЭлементов = Новый Массив;
	
	НайтиРекурсивно(ОбластьПоиска.Элементы, МассивЭлементов, СпособПоиска, ЗначениеПоиска);
	
	Для Каждого Элемент Из МассивЭлементов Цикл
		Если ИмяПоля <> Неопределено Тогда
			Элемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоля);
		КонецЕсли;
		Если Представление <> Неопределено Тогда
			Элемент.Представление = Представление;
		КонецЕсли;
		Если Использование <> Неопределено Тогда
			Элемент.Использование = Использование;
		КонецЕсли;
		Если ВидСравнения <> Неопределено Тогда
			Элемент.ВидСравнения = ВидСравнения;
		КонецЕсли;
		Если ПравоеЗначение <> Неопределено Тогда
			Элемент.ПравоеЗначение = ПравоеЗначение;
		КонецЕсли;
		Если РежимОтображения <> Неопределено Тогда
			Элемент.РежимОтображения = РежимОтображения;
		КонецЕсли;
		Если ИдентификаторПользовательскойНастройки <> Неопределено Тогда
			Элемент.ИдентификаторПользовательскойНастройки = ИдентификаторПользовательскойНастройки;
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивЭлементов.Количество();
	
КонецФункции

// Удалить элементы отбора с заданным именем поля или представлением.
//
// Параметры:
//  ОбластьУдаления - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                                               например, Список.Отбор или группа в отборе..
//  ИмяПоля         - Строка - имя поля компоновки (не используется для групп).
//  Представление   - Строка - представление поля компоновки.
//
Процедура УдалитьЭлементыГруппыОтбора(Знач ОбластьУдаления, Знач ИмяПоля = Неопределено, Знач Представление = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ИмяПоля) Тогда
		ЗначениеПоиска = Новый ПолеКомпоновкиДанных(ИмяПоля);
		СпособПоиска = 1;
	Иначе
		СпособПоиска = 2;
		ЗначениеПоиска = Представление;
	КонецЕсли;
	
	МассивЭлементов = Новый Массив; // Массив из ЭлементОтбораКомпоновкиДанных, ГруппаЭлементовОтбораКомпоновкиДанных
	
	НайтиРекурсивно(ОбластьУдаления.Элементы, МассивЭлементов, СпособПоиска, ЗначениеПоиска);
	
	Для Каждого Элемент Из МассивЭлементов Цикл
		Если Элемент.Родитель = Неопределено Тогда
			ОбластьУдаления.Элементы.Удалить(Элемент);
		Иначе
			Элемент.Родитель.Элементы.Удалить(Элемент);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Добавить или заменить существующий элемент отбора.
//
// Параметры:
//  ОбластьПоискаДобавления - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                     например, Список.Отбор или группа в отборе.
//  ИмяПоля                 - Строка - имя поля компоновки данных (заполняется всегда).
//  ПравоеЗначение          - Произвольный - сравниваемое значение.
//  ВидСравнения            - ВидСравненияКомпоновкиДанных - вид сравнения.
//  Представление           - Строка - представление элемента компоновки данных.
//  Использование           - Булево - использование элемента.
//  РежимОтображения        - РежимОтображенияЭлементаНастройкиКомпоновкиДанных - режим отображения.
//  ИдентификаторПользовательскойНастройки - Строка - см. ОтборКомпоновкиДанных.ИдентификаторПользовательскойНастройки
//                                                    в синтакс-помощнике.
//
Процедура УстановитьЭлементОтбора(ОбластьПоискаДобавления,
								Знач ИмяПоля,
								Знач ПравоеЗначение = Неопределено,
								Знач ВидСравнения = Неопределено,
								Знач Представление = Неопределено,
								Знач Использование = Неопределено,
								Знач РежимОтображения = Неопределено,
								Знач ИдентификаторПользовательскойНастройки = Неопределено) Экспорт
	
	ЧислоИзмененных = ИзменитьЭлементыОтбора(ОбластьПоискаДобавления, ИмяПоля, Представление,
							ПравоеЗначение, ВидСравнения, Использование, РежимОтображения, ИдентификаторПользовательскойНастройки);
	
	Если ЧислоИзмененных = 0 Тогда
		Если ВидСравнения = Неопределено Тогда
			Если ТипЗнч(ПравоеЗначение) = Тип("Массив")
				Или ТипЗнч(ПравоеЗначение) = Тип("ФиксированныйМассив")
				Или ТипЗнч(ПравоеЗначение) = Тип("СписокЗначений") Тогда
				ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
			Иначе
				ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			КонецЕсли;
		КонецЕсли;
		Если РежимОтображения = Неопределено Тогда
			РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		КонецЕсли;
		ДобавитьЭлементКомпоновки(ОбластьПоискаДобавления, ИмяПоля, ВидСравнения,
								ПравоеЗначение, Представление, Использование, РежимОтображения, ИдентификаторПользовательскойНастройки);
	КонецЕсли;
	
КонецПроцедуры

// Добавить или заменить существующий элемент отбора динамического списка.
//
// Параметры:
//   ДинамическийСписок - ДинамическийСписок - список, в котором требуется установить отбор.
//   ИмяПоля            - Строка - поле, по которому необходимо установить отбор.
//   ПравоеЗначение     - Произвольный - значение отбора.
//       Необязательный. Значение по умолчанию Неопределено.
//       Внимание! Если передать Неопределено, то значение не будет изменено.
//   ВидСравнения  - ВидСравненияКомпоновкиДанных - условие отбора.
//   Представление - Строка - представление элемента компоновки данных.
//       Необязательный. Значение по умолчанию Неопределено.
//       Если указано, то выводится только флажок использования с указанным представлением (значение не выводится).
//       Для очистки (чтобы значение снова выводилось) следует передать пустую строку.
//   Использование - Булево - флажок использования этого отбора.
//       Необязательный. Значение по умолчанию: Неопределено.
//   РежимОтображения - РежимОтображенияЭлементаНастройкиКомпоновкиДанных - способ отображения этого отбора
//                                                                          пользователю:
//        РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ - в группе быстрых настроек над списком.
//        РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный       - в настройка списка (в подменю Еще).
//        РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный   - запретить пользователю менять этот отбор.
//   ИдентификаторПользовательскойНастройки - Строка - уникальный идентификатор этого отбора.
//       Используется для связи с пользовательскими настройками.
//
Процедура УстановитьЭлементОтбораДинамическогоСписка(ДинамическийСписок, ИмяПоля,
	ПравоеЗначение = Неопределено,
	ВидСравнения = Неопределено,
	Представление = Неопределено,
	Использование = Неопределено,
	РежимОтображения = Неопределено,
	ИдентификаторПользовательскойНастройки = Неопределено) Экспорт
	
	Если РежимОтображения = Неопределено Тогда
		РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	КонецЕсли;
	
	Если РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
		ОтборДинамическогоСписка = ДинамическийСписок.КомпоновщикНастроек.ФиксированныеНастройки.Отбор;
	Иначе
		ОтборДинамическогоСписка = ДинамическийСписок.КомпоновщикНастроек.Настройки.Отбор;
	КонецЕсли;
	
	УстановитьЭлементОтбора(
		ОтборДинамическогоСписка,
		ИмяПоля,
		ПравоеЗначение,
		ВидСравнения,
		Представление,
		Использование,
		РежимОтображения,
		ИдентификаторПользовательскойНастройки);
	
КонецПроцедуры

// Удалить элемент группы отбора динамического списка.
//
// Параметры:
//  ДинамическийСписок - ДинамическийСписок - реквизит формы, для которого требуется установить отбор.
//  ИмяПоля         - Строка - имя поля компоновки (не используется для групп).
//  Представление   - Строка - представление поля компоновки.
//
Процедура УдалитьЭлементыГруппыОтбораДинамическогоСписка(ДинамическийСписок, ИмяПоля = Неопределено, Представление = Неопределено) Экспорт
	
	УдалитьЭлементыГруппыОтбора(
		ДинамическийСписок.КомпоновщикНастроек.ФиксированныеНастройки.Отбор,
		ИмяПоля,
		Представление);
	
	УдалитьЭлементыГруппыОтбора(
		ДинамическийСписок.КомпоновщикНастроек.Настройки.Отбор,
		ИмяПоля,
		Представление);
	
КонецПроцедуры

// Установить или обновить значение параметра ИмяПараметра динамического списка Список.
//
// Параметры:
//  Список          - ДинамическийСписок - реквизит формы, для которого требуется установить параметр.
//  ИмяПараметра    - Строка             - имя параметра динамического списка.
//  Значение        - Произвольный        - новое значение параметра.
//  Использование   - Булево             - признак использования параметра.
//
Процедура УстановитьПараметрДинамическогоСписка(Список, ИмяПараметра, Значение, Использование = Истина) Экспорт
	
	ЗначениеПараметраКомпоновкиДанных = Список.Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
	Если ЗначениеПараметраКомпоновкиДанных <> Неопределено Тогда
		Если Использование И ЗначениеПараметраКомпоновкиДанных.Значение <> Значение Тогда
			ЗначениеПараметраКомпоновкиДанных.Значение = Значение;
		КонецЕсли;
		Если ЗначениеПараметраКомпоновкиДанных.Использование <> Использование Тогда
			ЗначениеПараметраКомпоновкиДанных.Использование = Использование;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ДинамическийСписок

Процедура НайтиРекурсивно(КоллекцияЭлементов, МассивЭлементов, СпособПоиска, ЗначениеПоиска)
	
	Для каждого ЭлементОтбора Из КоллекцияЭлементов Цикл
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			
			Если СпособПоиска = 1 Тогда
				Если ЭлементОтбора.ЛевоеЗначение = ЗначениеПоиска Тогда
					МассивЭлементов.Добавить(ЭлементОтбора);
				КонецЕсли;
			ИначеЕсли СпособПоиска = 2 Тогда
				Если ЭлементОтбора.Представление = ЗначениеПоиска Тогда
					МассивЭлементов.Добавить(ЭлементОтбора);
				КонецЕсли;
			КонецЕсли;
		Иначе
			
			НайтиРекурсивно(ЭлементОтбора.Элементы, МассивЭлементов, СпособПоиска, ЗначениеПоиска);
			
			Если СпособПоиска = 2 И ЭлементОтбора.Представление = ЗначениеПоиска Тогда
				МассивЭлементов.Добавить(ЭлементОтбора);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Выполняет поиск элемента отбора в коллекции по заданному представлению.
//
// Параметры:
//  КоллекцияЭлементов - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                                                  например, Список.Отбор.Элементы или группа в отборе.
//  Представление - Строка - представление группы.
// 
// Возвращаемое значение:
//  ЭлементОтбораКомпоновкиДанных - элемент отбора.
//
Функция НайтиЭлементОтбораПоПредставлению(КоллекцияЭлементов, Представление) Экспорт
	
	ВозвращаемоеЗначение = Неопределено;
	
	Для каждого ЭлементОтбора Из КоллекцияЭлементов Цикл
		Если ЭлементОтбора.Представление = Представление Тогда
			ВозвращаемоеЗначение = ЭлементОтбора;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение
	
КонецФункции

#КонецОбласти

#КонецОбласти
```

</details>

7.3. Настроить Связи параметров выбора реквизита таб части др_Ярлык отбор по Организации)

7.4. Создать обший модуль 	др_ОбщегоНазначенияКлиентСервер							  
(Функции для работы с отборами и параметрами динамических списков)
<details>

<summary>Просмотр кода</summary>

### Код обший модуль

```
//1729 Задача 7.4.         
#Область ПрограммныйИнтерфейс



#Область Оформление

// Создает элемент условного ооформления одного параметра одного поля по одному отбору.
// Параметры:
//  Форма                        - ФормаКлиентскогоПриложения       - На которую необходимо добавить условное оформление
//  ОформляемоеПоле              - Строка, Неопределено             - Имя офомляемого элемента формы. Например, "Товары". Если не передавать, то оформляемое поле не будет добавлено
//  ИмяПараметраОформления       - Строка, ПараметрКомпоновкиДанных - Указывает что будет изменено в офомлении поля. Например, "ЦветФона" / "Текст"
//  ЗначениеПараметраОформления  - Произвольный                     - Значение оформления. Например, <Новый Цвет(222, 255, 222)>
//  ПутьКЛевомуЗначениюОтбора    - Строка, Неопределено             - Путь к полю по которому выполняется отбор условного оформления. Например, "Товары.Номенклатура" / "Контрагент". Если не передавать, то отбор не будет установлен
//  ВидСравненияОтбора           - ВидСравненияКомпоновкиДанных     - Вид сравнения. По умолчанию "ВидСравненияКомпоновкиДанных.Равно"
//  ПравоеЗначениеОтбора         - Произвольный                     - С чем сравнивать левое значение
//
// ВозвращаемоеЗначение:
//  ЭлементУсловногоОформления - Добавленный элемент оформления
Функция ДобавитьПростойЭлементУсловногоОформления(Форма, ОформляемоеПоле = Неопределено, 
	ИмяПараметраОформления = Неопределено, ЗначениеПараметраОформления = Неопределено, 
	ПутьКЛевомуЗначениюОтбора = Неопределено, ВидСравненияОтбора = Неопределено, ПравоеЗначениеОтбора = Неопределено) Экспорт
	
	//Добавляем элемент:
	ЭлементУсловногоОформления = Форма.УсловноеОформление.Элементы.Добавить();
	
	Если ЗначениеЗаполнено(ИмяПараметраОформления) Тогда
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра(ИмяПараметраОформления, ЗначениеПараметраОформления);
	КонецЕсли;
	
	Если НЕ ПутьКЛевомуЗначениюОтбора = Неопределено Тогда
		ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(ПутьКЛевомуЗначениюОтбора);
		ОтборЭлемента.ВидСравнения   = ?(ВидСравненияОтбора = Неопределено, ВидСравненияКомпоновкиДанных.Равно, ВидСравненияОтбора);
		ОтборЭлемента.ПравоеЗначение = ПравоеЗначениеОтбора;
	КонецЕсли;
	
	Если НЕ ОформляемоеПоле = Неопределено Тогда
		ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ОформляемоеПоле);
	КонецЕсли;

	Возврат ЭлементУсловногоОформления;
	
КонецФункции

#КонецОбласти

#Область ДинамическийСписок

////////////////////////////////////////////////////////////////////////////////
// Функции для работы с отборами и параметрами динамических списков.
//

// Найти элемент или группу отбора по заданному имени поля или представлению.
//
// Параметры:
//  ОбластьПоиска - ОтборКомпоновкиДанных
//                - КоллекцияЭлементовОтбораКомпоновкиДанных
//                - ГруппаЭлементовОтбораКомпоновкиДанных    - контейнер с элементами и группами отбора,
//                                                             например Список.Отбор или группа в отборе.
//  ИмяПоля       - Строка - имя поля компоновки (не используется для групп).
//  Представление - Строка - представление поля компоновки.
//
// Возвращаемое значение:
//  Массив - коллекция отборов.
//
Функция НайтиЭлементыИГруппыОтбора(Знач ОбластьПоиска,
									Знач ИмяПоля = Неопределено,
									Знач Представление = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ИмяПоля) Тогда
		ЗначениеПоиска = Новый ПолеКомпоновкиДанных(ИмяПоля);
		СпособПоиска = 1;
	Иначе
		СпособПоиска = 2;
		ЗначениеПоиска = Представление;
	КонецЕсли;
	
	МассивЭлементов = Новый Массив;
	
	НайтиРекурсивно(ОбластьПоиска.Элементы, МассивЭлементов, СпособПоиска, ЗначениеПоиска);
	
	Возврат МассивЭлементов;
	
КонецФункции

// Добавить группу отбора в коллекцию КоллекцияЭлементов.
//
// Параметры:
//  КоллекцияЭлементов - ОтборКомпоновкиДанных
//                     - КоллекцияЭлементовОтбораКомпоновкиДанных
//                     - ГруппаЭлементовОтбораКомпоновкиДанных    - контейнер с элементами и группами отбора,
//                                                                  например Список.Отбор или группа в отборе.
//  Представление      - Строка - представление группы.
//  ТипГруппы          - ТипГруппыЭлементовОтбораКомпоновкиДанных - тип группы.
//
// Возвращаемое значение:
//  ГруппаЭлементовОтбораКомпоновкиДанных - группа отбора.
//
Функция СоздатьГруппуЭлементовОтбора(Знач КоллекцияЭлементов, Представление, ТипГруппы) Экспорт
	
	Если ТипЗнч(КоллекцияЭлементов) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных")
		Или ТипЗнч(КоллекцияЭлементов) = Тип("ОтборКомпоновкиДанных") Тогда
		
		КоллекцияЭлементов = КоллекцияЭлементов.Элементы;
	КонецЕсли;
	
	ГруппаЭлементовОтбора = НайтиЭлементОтбораПоПредставлению(КоллекцияЭлементов, Представление);
	Если ГруппаЭлементовОтбора = Неопределено Тогда
		ГруппаЭлементовОтбора = КоллекцияЭлементов.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	Иначе
		ГруппаЭлементовОтбора.Элементы.Очистить();
	КонецЕсли;
	
	ГруппаЭлементовОтбора.Представление    = Представление;
	ГруппаЭлементовОтбора.Применение       = ТипПримененияОтбораКомпоновкиДанных.Элементы;
	ГруппаЭлементовОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ГруппаЭлементовОтбора.ТипГруппы        = ТипГруппы;
	ГруппаЭлементовОтбора.Использование    = Истина;
	
	Возврат ГруппаЭлементовОтбора;
	
КонецФункции

// Добавить элемент компоновки в контейнер элементов компоновки.
//
// Параметры:
//  ОбластьДобавления - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                                                 например, Список.Отбор или группа в отборе.
//  ИмяПоля                 - Строка - имя поля компоновки данных (заполняется всегда).
//  ВидСравнения            - ВидСравненияКомпоновкиДанных - вид сравнения.
//  ПравоеЗначение          - Произвольный - сравниваемое значение.
//  Представление           - Строка - представление элемента компоновки данных.
//  Использование           - Булево - использование элемента.
//  РежимОтображения        - РежимОтображенияЭлементаНастройкиКомпоновкиДанных - режим отображения.
//  ИдентификаторПользовательскойНастройки - Строка - см. ОтборКомпоновкиДанных.ИдентификаторПользовательскойНастройки
//                                                    в синтакс-помощнике.
// Возвращаемое значение:
//  ЭлементОтбораКомпоновкиДанных - элемент компоновки.
//
Функция ДобавитьЭлементКомпоновки(ОбластьДобавления,
									Знач ИмяПоля,
									Знач ВидСравнения,
									Знач ПравоеЗначение = Неопределено,
									Знач Представление  = Неопределено,
									Знач Использование  = Неопределено,
									знач РежимОтображения = Неопределено,
									знач ИдентификаторПользовательскойНастройки = Неопределено) Экспорт
	
	Элемент = ОбластьДобавления.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Элемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоля);
	Элемент.ВидСравнения = ВидСравнения;
	
	Если РежимОтображения = Неопределено Тогда
		Элемент.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	Иначе
		Элемент.РежимОтображения = РежимОтображения;
	КонецЕсли;
	
	Если ПравоеЗначение <> Неопределено Тогда
		Элемент.ПравоеЗначение = ПравоеЗначение;
	КонецЕсли;
	
	Если Представление <> Неопределено Тогда
		Элемент.Представление = Представление;
	КонецЕсли;
	
	Если Использование <> Неопределено Тогда
		Элемент.Использование = Использование;
	КонецЕсли;
	
	// Важно: установка идентификатора должна выполняться
	// в конце настройки элемента, иначе он будет скопирован
	// в пользовательские настройки частично заполненным.
	Если ИдентификаторПользовательскойНастройки <> Неопределено Тогда
		Элемент.ИдентификаторПользовательскойНастройки = ИдентификаторПользовательскойНастройки;
	ИначеЕсли Элемент.РежимОтображения <> РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
		Элемент.ИдентификаторПользовательскойНастройки = ИмяПоля;
	КонецЕсли;
	
	Возврат Элемент;
	
КонецФункции

// Изменить элемент отбора с заданным именем поля или представлением.
//
// Параметры:
//  ОбластьПоиска - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                                             например Список.Отбор или группа в отборе.
//  ИмяПоля                 - Строка - имя поля компоновки данных (заполняется всегда).
//  Представление           - Строка - представление элемента компоновки данных.
//  ПравоеЗначение          - Произвольный - сравниваемое значение.
//  ВидСравнения            - ВидСравненияКомпоновкиДанных - вид сравнения.
//  Использование           - Булево - использование элемента.
//  РежимОтображения        - РежимОтображенияЭлементаНастройкиКомпоновкиДанных - режим отображения.
//  ИдентификаторПользовательскойНастройки - Строка - см. ОтборКомпоновкиДанных.ИдентификаторПользовательскойНастройки
//                                                    в синтакс-помощнике.
//
// Возвращаемое значение:
//  Число - количество измененных элементов.
//
Функция ИзменитьЭлементыОтбора(ОбластьПоиска,
								Знач ИмяПоля = Неопределено,
								Знач Представление = Неопределено,
								Знач ПравоеЗначение = Неопределено,
								Знач ВидСравнения = Неопределено,
								Знач Использование = Неопределено,
								Знач РежимОтображения = Неопределено,
								Знач ИдентификаторПользовательскойНастройки = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ИмяПоля) Тогда
		ЗначениеПоиска = Новый ПолеКомпоновкиДанных(ИмяПоля);
		СпособПоиска = 1;
	Иначе
		СпособПоиска = 2;
		ЗначениеПоиска = Представление;
	КонецЕсли;
	
	МассивЭлементов = Новый Массив;
	
	НайтиРекурсивно(ОбластьПоиска.Элементы, МассивЭлементов, СпособПоиска, ЗначениеПоиска);
	
	Для Каждого Элемент Из МассивЭлементов Цикл
		Если ИмяПоля <> Неопределено Тогда
			Элемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоля);
		КонецЕсли;
		Если Представление <> Неопределено Тогда
			Элемент.Представление = Представление;
		КонецЕсли;
		Если Использование <> Неопределено Тогда
			Элемент.Использование = Использование;
		КонецЕсли;
		Если ВидСравнения <> Неопределено Тогда
			Элемент.ВидСравнения = ВидСравнения;
		КонецЕсли;
		Если ПравоеЗначение <> Неопределено Тогда
			Элемент.ПравоеЗначение = ПравоеЗначение;
		КонецЕсли;
		Если РежимОтображения <> Неопределено Тогда
			Элемент.РежимОтображения = РежимОтображения;
		КонецЕсли;
		Если ИдентификаторПользовательскойНастройки <> Неопределено Тогда
			Элемент.ИдентификаторПользовательскойНастройки = ИдентификаторПользовательскойНастройки;
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивЭлементов.Количество();
	
КонецФункции

// Удалить элементы отбора с заданным именем поля или представлением.
//
// Параметры:
//  ОбластьУдаления - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                                               например, Список.Отбор или группа в отборе..
//  ИмяПоля         - Строка - имя поля компоновки (не используется для групп).
//  Представление   - Строка - представление поля компоновки.
//
Процедура УдалитьЭлементыГруппыОтбора(Знач ОбластьУдаления, Знач ИмяПоля = Неопределено, Знач Представление = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ИмяПоля) Тогда
		ЗначениеПоиска = Новый ПолеКомпоновкиДанных(ИмяПоля);
		СпособПоиска = 1;
	Иначе
		СпособПоиска = 2;
		ЗначениеПоиска = Представление;
	КонецЕсли;
	
	МассивЭлементов = Новый Массив; // Массив из ЭлементОтбораКомпоновкиДанных, ГруппаЭлементовОтбораКомпоновкиДанных
	
	НайтиРекурсивно(ОбластьУдаления.Элементы, МассивЭлементов, СпособПоиска, ЗначениеПоиска);
	
	Для Каждого Элемент Из МассивЭлементов Цикл
		Если Элемент.Родитель = Неопределено Тогда
			ОбластьУдаления.Элементы.Удалить(Элемент);
		Иначе
			Элемент.Родитель.Элементы.Удалить(Элемент);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Добавить или заменить существующий элемент отбора.
//
// Параметры:
//  ОбластьПоискаДобавления - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                     например, Список.Отбор или группа в отборе.
//  ИмяПоля                 - Строка - имя поля компоновки данных (заполняется всегда).
//  ПравоеЗначение          - Произвольный - сравниваемое значение.
//  ВидСравнения            - ВидСравненияКомпоновкиДанных - вид сравнения.
//  Представление           - Строка - представление элемента компоновки данных.
//  Использование           - Булево - использование элемента.
//  РежимОтображения        - РежимОтображенияЭлементаНастройкиКомпоновкиДанных - режим отображения.
//  ИдентификаторПользовательскойНастройки - Строка - см. ОтборКомпоновкиДанных.ИдентификаторПользовательскойНастройки
//                                                    в синтакс-помощнике.
//
Процедура УстановитьЭлементОтбора(ОбластьПоискаДобавления,
								Знач ИмяПоля,
								Знач ПравоеЗначение = Неопределено,
								Знач ВидСравнения = Неопределено,
								Знач Представление = Неопределено,
								Знач Использование = Неопределено,
								Знач РежимОтображения = Неопределено,
								Знач ИдентификаторПользовательскойНастройки = Неопределено) Экспорт
	
	ЧислоИзмененных = ИзменитьЭлементыОтбора(ОбластьПоискаДобавления, ИмяПоля, Представление,
							ПравоеЗначение, ВидСравнения, Использование, РежимОтображения, ИдентификаторПользовательскойНастройки);
	
	Если ЧислоИзмененных = 0 Тогда
		Если ВидСравнения = Неопределено Тогда
			Если ТипЗнч(ПравоеЗначение) = Тип("Массив")
				Или ТипЗнч(ПравоеЗначение) = Тип("ФиксированныйМассив")
				Или ТипЗнч(ПравоеЗначение) = Тип("СписокЗначений") Тогда
				ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
			Иначе
				ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			КонецЕсли;
		КонецЕсли;
		Если РежимОтображения = Неопределено Тогда
			РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		КонецЕсли;
		ДобавитьЭлементКомпоновки(ОбластьПоискаДобавления, ИмяПоля, ВидСравнения,
								ПравоеЗначение, Представление, Использование, РежимОтображения, ИдентификаторПользовательскойНастройки);
	КонецЕсли;
	
КонецПроцедуры

// Добавить или заменить существующий элемент отбора динамического списка.
//
// Параметры:
//   ДинамическийСписок - ДинамическийСписок - список, в котором требуется установить отбор.
//   ИмяПоля            - Строка - поле, по которому необходимо установить отбор.
//   ПравоеЗначение     - Произвольный - значение отбора.
//       Необязательный. Значение по умолчанию Неопределено.
//       Внимание! Если передать Неопределено, то значение не будет изменено.
//   ВидСравнения  - ВидСравненияКомпоновкиДанных - условие отбора.
//   Представление - Строка - представление элемента компоновки данных.
//       Необязательный. Значение по умолчанию Неопределено.
//       Если указано, то выводится только флажок использования с указанным представлением (значение не выводится).
//       Для очистки (чтобы значение снова выводилось) следует передать пустую строку.
//   Использование - Булево - флажок использования этого отбора.
//       Необязательный. Значение по умолчанию: Неопределено.
//   РежимОтображения - РежимОтображенияЭлементаНастройкиКомпоновкиДанных - способ отображения этого отбора
//                                                                          пользователю:
//        РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ - в группе быстрых настроек над списком.
//        РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный       - в настройка списка (в подменю Еще).
//        РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный   - запретить пользователю менять этот отбор.
//   ИдентификаторПользовательскойНастройки - Строка - уникальный идентификатор этого отбора.
//       Используется для связи с пользовательскими настройками.
//
Процедура УстановитьЭлементОтбораДинамическогоСписка(ДинамическийСписок, ИмяПоля,
	ПравоеЗначение = Неопределено,
	ВидСравнения = Неопределено,
	Представление = Неопределено,
	Использование = Неопределено,
	РежимОтображения = Неопределено,
	ИдентификаторПользовательскойНастройки = Неопределено) Экспорт
	
	Если РежимОтображения = Неопределено Тогда
		РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	КонецЕсли;
	
	Если РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
		ОтборДинамическогоСписка = ДинамическийСписок.КомпоновщикНастроек.ФиксированныеНастройки.Отбор;
	Иначе
		ОтборДинамическогоСписка = ДинамическийСписок.КомпоновщикНастроек.Настройки.Отбор;
	КонецЕсли;
	
	УстановитьЭлементОтбора(
		ОтборДинамическогоСписка,
		ИмяПоля,
		ПравоеЗначение,
		ВидСравнения,
		Представление,
		Использование,
		РежимОтображения,
		ИдентификаторПользовательскойНастройки);
	
КонецПроцедуры

// Удалить элемент группы отбора динамического списка.
//
// Параметры:
//  ДинамическийСписок - ДинамическийСписок - реквизит формы, для которого требуется установить отбор.
//  ИмяПоля         - Строка - имя поля компоновки (не используется для групп).
//  Представление   - Строка - представление поля компоновки.
//
Процедура УдалитьЭлементыГруппыОтбораДинамическогоСписка(ДинамическийСписок, ИмяПоля = Неопределено, Представление = Неопределено) Экспорт
	
	УдалитьЭлементыГруппыОтбора(
		ДинамическийСписок.КомпоновщикНастроек.ФиксированныеНастройки.Отбор,
		ИмяПоля,
		Представление);
	
	УдалитьЭлементыГруппыОтбора(
		ДинамическийСписок.КомпоновщикНастроек.Настройки.Отбор,
		ИмяПоля,
		Представление);
	
КонецПроцедуры

// Установить или обновить значение параметра ИмяПараметра динамического списка Список.
//
// Параметры:
//  Список          - ДинамическийСписок - реквизит формы, для которого требуется установить параметр.
//  ИмяПараметра    - Строка             - имя параметра динамического списка.
//  Значение        - Произвольный        - новое значение параметра.
//  Использование   - Булево             - признак использования параметра.
//
Процедура УстановитьПараметрДинамическогоСписка(Список, ИмяПараметра, Значение, Использование = Истина) Экспорт
	
	ЗначениеПараметраКомпоновкиДанных = Список.Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
	Если ЗначениеПараметраКомпоновкиДанных <> Неопределено Тогда
		Если Использование И ЗначениеПараметраКомпоновкиДанных.Значение <> Значение Тогда
			ЗначениеПараметраКомпоновкиДанных.Значение = Значение;
		КонецЕсли;
		Если ЗначениеПараметраКомпоновкиДанных.Использование <> Использование Тогда
			ЗначениеПараметраКомпоновкиДанных.Использование = Использование;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ДинамическийСписок

Процедура НайтиРекурсивно(КоллекцияЭлементов, МассивЭлементов, СпособПоиска, ЗначениеПоиска)
	
	Для каждого ЭлементОтбора Из КоллекцияЭлементов Цикл
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			
			Если СпособПоиска = 1 Тогда
				Если ЭлементОтбора.ЛевоеЗначение = ЗначениеПоиска Тогда
					МассивЭлементов.Добавить(ЭлементОтбора);
				КонецЕсли;
			ИначеЕсли СпособПоиска = 2 Тогда
				Если ЭлементОтбора.Представление = ЗначениеПоиска Тогда
					МассивЭлементов.Добавить(ЭлементОтбора);
				КонецЕсли;
			КонецЕсли;
		Иначе
			
			НайтиРекурсивно(ЭлементОтбора.Элементы, МассивЭлементов, СпособПоиска, ЗначениеПоиска);
			
			Если СпособПоиска = 2 И ЭлементОтбора.Представление = ЗначениеПоиска Тогда
				МассивЭлементов.Добавить(ЭлементОтбора);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Выполняет поиск элемента отбора в коллекции по заданному представлению.
//
// Параметры:
//  КоллекцияЭлементов - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                                                  например, Список.Отбор.Элементы или группа в отборе.
//  Представление - Строка - представление группы.
// 
// Возвращаемое значение:
//  ЭлементОтбораКомпоновкиДанных - элемент отбора.
//
Функция НайтиЭлементОтбораПоПредставлению(КоллекцияЭлементов, Представление) Экспорт
	
	ВозвращаемоеЗначение = Неопределено;
	
	Для каждого ЭлементОтбора Из КоллекцияЭлементов Цикл
		Если ЭлементОтбора.Представление = Представление Тогда
			ВозвращаемоеЗначение = ЭлементОтбора;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение
	
КонецФункции

#КонецОбласти

#КонецОбласти
```

</details>


7.5. Создать обший модуль	др_ПроверкиКорректностиДанных	(ПроцедурыФормированияЗапросовПроверкиКорректностиЗаполненияДокументов)							

<details>

<summary>Просмотр кода</summary>

### Код обшего модуля

```
//1729 Задача 7.5. 

#Область ПрограммныйИнтерфейс

Процедура ПроверитьКорректностьЗаполненияДокумента(ДокументОбъект, Отказ = Ложь) Экспорт
	
	// Заполним массив необходимых проверок в зависимости от типа документа
	МассивПроверок = Новый Массив();
	ИмяТаблицы     = ДокументОбъект.Метаданные().ПолноеИмя();
	
	Если ИмяТаблицы = "Документ.др_РеализацияЯрлыков" Тогда
		
		МассивПроверок.Добавить("ТаблицаЯрлыки");
		МассивПроверок.Добавить("КорректностьЗаполненияСкладов");
		МассивПроверок.Добавить("ДублиЯрлыков");
		
	ИначеЕсли ИмяТаблицы = "Документ.др_ПриобретениеЯрлыков" Тогда
		
		МассивПроверок.Добавить("ТаблицаЯрлыки");
		МассивПроверок.Добавить("КорректностьЗаполненияСкладов");
		
	КонецЕсли;
	
	// Сформируем текст запроса необходимых проверок в соответствие с массивом проверок
	ТекстЗапроса     = "";
	ПараметрыЗапроса = Новый Структура;
	
	ПараметрыЗапроса.Вставить("Дата",   ДокументОбъект.Дата);
	ПараметрыЗапроса.Вставить("Ссылка", ДокументОбъект.Ссылка);
	
	Для Каждого ИмяПроверки Из МассивПроверок Цикл
		
		Если ИмяПроверки = "ТаблицаЯрлыки" Тогда
			
			СформироватьЗапросТаблицаЯрлыки(ТекстЗапроса, ПараметрыЗапроса, ДокументОбъект, ИмяТаблицы);
			
		ИначеЕсли ИмяПроверки = "КорректностьЗаполненияСкладов" Тогда
			
			СформироватьЗапросКорректностьЗаполненияСкладов(ТекстЗапроса);
			
		ИначеЕсли ИмяПроверки = "ДублиЯрлыков" Тогда
			
			СформироватьЗапросДублиТоваров(ТекстЗапроса);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ТекстЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Для Каждого ПараметрЗапроса Из ПараметрыЗапроса Цикл
		Запрос.УстановитьПараметр(ПараметрЗапроса.Ключ, ПараметрЗапроса.Значение);
	КонецЦикла;
	
	РезультатПакета = Запрос.ВыполнитьПакет();
	
	// Сообщим пользователю о результатах проверки для каждого результата запроса
	Для ИндексПроверки = 0 По РезультатПакета.Количество()-1 Цикл
		
		Выборка = РезультатПакета[ИндексПроверки].Выбрать();
		ИмяПроверки = МассивПроверок[ИндексПроверки];
		
		Если ИмяПроверки = "КорректностьЗаполненияСкладов" Тогда
			
			СообщитьОбОшибкахКорректностьЗаполненияСкладов(Выборка, ДокументОбъект, Отказ);
			
		ИначеЕсли ИмяПроверки = "ДублиЯрлыков" Тогда
			
			СообщитьОбОшибкахДублиЯрлыков(Выборка, ДокументОбъект, Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроцедурыФормированияЗапросовПроверкиКорректностиЗаполненияДокументов

Процедура СформироватьЗапросТаблицаЯрлыки(ТекстЗапроса, ПараметрыЗапроса, ДокументОбъект, ИмяТаблицы)
	
	Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовВПакете();
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + 
	"ВЫБРАТЬ
	|	Ярлыки.НомерСтроки КАК НомерСтроки,
	|	Ярлыки.Склад КАК Склад,
	|	Ярлыки.др_Ярлык КАК др_Ярлык
	|ПОМЕСТИТЬ Ярлыки
	|ИЗ
	|	&Ярлыки КАК Ярлыки";
	
	ПараметрыЗапроса.Вставить("Ярлыки", ДокументОбъект.Ярлыки.Выгрузить(, "НомерСтроки, Склад, др_Ярлык"));
	
КонецПроцедуры

Процедура СформироватьЗапросКорректностьЗаполненияСкладов(ТекстЗапроса)
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовВПакете() + 
	"ВЫБРАТЬ
	|	Ярлыки.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Ярлыки КАК Ярлыки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.др_Ярлыки КАК СправочникДр_Ярлыки
	|		ПО Ярлыки.др_Ярлык = СправочникДр_Ярлыки.Ссылка
	|ГДЕ
	|	СправочникДр_Ярлыки.Тип = ЗНАЧЕНИЕ(Перечисление.др_ТипыЯрлыков.Платный)
	|	И Ярлыки.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)";
	
КонецПроцедуры

Процедура СформироватьЗапросДублиТоваров(ТекстЗапроса)
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовВПакете() + 
	"ВЫБРАТЬ
	|	МАКСИМУМ(Ярлыки.НомерСтроки) КАК НомерСтроки,
	|	ПРЕДСТАВЛЕНИЕ(Ярлыки.др_Ярлык) КАК ЯрлыкПредставление,
	|	Ярлыки.Склад КАК Склад,
	|	ПРЕДСТАВЛЕНИЕ(Ярлыки.Склад) КАК СкладПредставление
	|ИЗ
	|	Ярлыки КАК Ярлыки
	|
	|СГРУППИРОВАТЬ ПО
	|	Ярлыки.Склад,
	|	Ярлыки.др_Ярлык
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(*) > 1";
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыДляВыводаСообщенийОбОшибкахЗаполненияДокументов

Процедура СообщитьОбОшибкахКорректностьЗаполненияСкладов(Выборка, ДокументОбъект, Отказ)
	
	Пока Выборка.Следующий() Цикл
		
		ТекстОшибки = СтрШаблон("Не заполнена колонка ""Склад"" в строке %1 списка ""Ярлыки""",
			Выборка.НомерСтроки);
		
		ОбщегоНазначения.СообщитьПользователю(
			ТекстОшибки,
			ДокументОбъект,
			ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Ярлыки", Выборка.НомерСтроки, "Склад"),,
			Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СообщитьОбОшибкахДублиЯрлыков(Выборка, ДокументОбъект, Отказ)
	
	Пока Выборка.Следующий() Цикл
		
		
		Если ЗначениеЗаполнено(Выборка.Склад) Тогда
			ТекстОшибки = СтрШаблон("Ярлык ""%1"" со складом ""%2"" повторяется",
				Выборка.ЯрлыкПредставление,
				Выборка.СкладПредставление);
		Иначе
			ТекстОшибки = СтрШаблон("Ярлык ""%1"" повторяется",
				Выборка.ЯрлыкПредставление);
		КонецЕсли;
		
		ОбщегоНазначения.СообщитьПользователю(
		ТекстОшибки,
		ДокументОбъект,
		ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Ярлыки", Выборка.НомерСтроки, "Номенклатура"),
		,
		Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
```

</details>

7.6. Создать регистр накопления др_ЯрлыкиНаСкладах								

<details>

<summary>Структура метаданных регистра др_ЯрлыкиНаСкладах</summary>

### Структура метаданных регистра др_ЯрлыкиНаСкладах

```
//1729 Задача 7.6. 
др_ЯрлыкиНаСкладах
    Измерения
    - Ярлык
    - Склад
    Ресурсы
    - Количество

```

</details>


8. Создать документ др_РеализацияЯрлыков (реквизит Организация, таб часть Ярлыки (реквизиты таб части 
др_Ярлык, Склад, Количество, Подразделение)

8.1. Настроить интерактивно проверку заполнения для др_Ярлык и Склад

8.2. Настроить программно проверку заполнения для Склад (если Тип ярлыка бесплатный не проверять)

<details>

<summary>Просмотр кода</summary>

### Код формы

```

//1729 Задача 8.2. 

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриЧтенииСозданииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	ЗаполнитьДанныеФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаполнитьДанныеФормы();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если НеВыполнятьПроверкуОбъекта Тогда
		НепроверяемыеРеквизиты.Добавить("Объект");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте             
Процедура ЯрлыкиДр_ЯрлыкПриИзменении(Элемент)
	
	СтрокаЯрлыков = Элементы.Ярлыки.ТекущиеДанные;
	
	ЯрлыкДокумента = Новый Массив;
	ЯрлыкДокумента.Добавить(СтрокаЯрлыков.др_Ярлык);
	
	ТипыЯрлыка = ПолучитьТипыЯрлыка(ЯрлыкДокумента);
	СтрокаЯрлыков.др_ТипЯрлыка = ТипыЯрлыка[СтрокаЯрлыков.др_Ярлык];
	
КонецПроцедуры


&НаКлиенте
Процедура ЯрлыкиСкладПриИзменении(Элемент)
	СтрокаТоваров = Элементы.Ярлыки.ТекущиеДанные;
	СтрокаТоваров.Подразделение = ПодразделениеСклада(СтрокаТоваров.Склад);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДанныеФормы()
	ЯрлыкДокумента = ПолучитьЯрлыкДокумента();
	ТипыЯрлыка = ПолучитьТипыЯрлыка(ЯрлыкДокумента);
	
	Для Каждого СтрокаЯрлыков Из Объект.Ярлыки Цикл
		СтрокаЯрлыков.др_ТипЯрлыка = ТипыЯрлыка[СтрокаЯрлыков.др_Ярлык];
	КонецЦикла;

	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	др_ОбщегоНазначенияКлиентСервер.ДобавитьПростойЭлементУсловногоОформления(ЭтаФорма,
		"ЯрлыкиСклад", "Видимость", Ложь, 
		"Объект.Ярлыки.др_ТипЯрлыка", ВидСравненияКомпоновкиДанных.Равно, Перечисления.др_ТипыЯрлыков.БесПлатный);

	// На неравенство, чтобы не показывать при пустом ярлыке
	др_ОбщегоНазначенияКлиентСервер.ДобавитьПростойЭлементУсловногоОформления(ЭтаФорма,
		"ЯрлыкиПодразделение", "Видимость", Ложь, 
		"Объект.Ярлыки.др_ТипЯрлыка", ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.др_ТипыЯрлыков.БесПлатный);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьЯрлыкДокумента()
	
	ВозвращаемоеЗначение = Новый Массив;
	
	Для Каждого СтрокаЯрлыков Из Объект.Ярлыки Цикл
		ВозвращаемоеЗначение.Добавить(СтрокаЯрлыков.др_Ярлык);
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции 


&НаСервереБезКонтекста
Функция ПолучитьТипыЯрлыка(МассивЯрлыка)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивЯрлыка, "Тип");
КонецФункции

&НаСервереБезКонтекста
Функция ПодразделениеСклада(Склад)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Склад, "Подразделение");
КонецФункции

#КонецОбласти


```

</details>


8.3. Настроить Связи параметров выбора реквизита таб части др_Ярлык отбор по Организации)

8.4. Создать регистр накопления др_Продажи

<details>

<summary>Структура метаданных регистра др_Продажи</summary>

### Структура метаданных регистра др_Продажи

```
//1729 Задача 8.4. 
др_Продажи
    Измерения
    - Ярлык
    - Подразделение
    Ресурсы
    - Количество

```

</details>

9. Создать обработку др_ФормированиеРеализаций для автоматического создания др_РеализацияЯрлыков

9.1. Создать форму

<details>

<summary>Просмотр кода</summary>

### Код формы  Имя команды СоздатьДокументы

```
 	
//1729 Задача 9.1. Программно Создать форму


#Область ОбработчикиСобытий

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	МассивНепроверяемыхРеквизитов.Добавить("ОсновнойРеквизитФормы");
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Остатки, "Организация",
		Организация, ВидСравненияКомпоновкиДанных.Равно, "ОтборОрганизация", ЗначениеЗаполнено(Организация));
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Остатки, "Подразделение",
		Подразделение, ВидСравненияКомпоновкиДанных.Равно, "ОтборПодразделение", ЗначениеЗаполнено(Подразделение));
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьДокументы(Команда)
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьДокументыНаСервере(Элементы.Остатки.ВыделенныеСтроки);
	
КонецПроцедуры

&НаСервере
Функция СоздатьДокументыНаСервере(Знач ДанныеСтрок)
	
	Результат = Новый Массив;
	
	// Проверяем возможность запуска:
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат Результат;
	КонецЕсли;
	
	// Готовим данные:
	ТаблицаОтборов = Обработки.др_ФормированиеРеализаций.ТаблицаОтборов();
	Для Каждого ДанныеСтроки Из ДанныеСтрок Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаОтборов.Добавить(), ДанныеСтроки);
	КонецЦикла;
	
	// Вызываем основной интерфейс:
	Результат = Обработки.др_ФормированиеРеализаций.СоздатьДокументы(ТаблицаОтборов);
	
	// Собираем информацию о результате:
	ПроведенныеДокументы = Новый Массив;
	Для Каждого ДокументОбъект Из Результат Цикл
		Если ДокументОбъект.Проведен Тогда
			ПроведенныеДокументы.Добавить(ДокументОбъект);
		КонецЕсли;
	КонецЦикла;

	Элементы.Остатки.Обновить(); // Перечитываем остатки

КонецФункции

#КонецОбласти

```

</details>

9.2. Создать Модуль менеджера

<details>

<summary>Модуль менеджера</summary>

### Код Создать Модуль менеджера

```
 	
//1729 Задача 9.2. Создать Модуль менеджера



#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ТаблицаОтборов() Экспорт
	
	ТаблицаОтборов = Новый ТаблицаЗначений;
	ТаблицаОтборов.Колонки.Добавить("Ярлык", 		Новый ОписаниеТипов("СправочникСсылка.др_Ярлыки"));
	ТаблицаОтборов.Колонки.Добавить("Склад",        Новый ОписаниеТипов("СправочникСсылка.Склады"));
	
	Возврат ТаблицаОтборов;
	
КонецФункции

Функция СоздатьДокументы(ТаблицаОтборов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаОтборов.Ярлык КАК Ярлык,
	|	ТаблицаОтборов.Склад КАК Склад
	|ПОМЕСТИТЬ ТаблицаОтборов
	|ИЗ
	|	&ТаблицаОтборов КАК ТаблицаОтборов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	др_ЯрлыкиНаСкладахОстатки.Ярлык.Организация КАК Организация,
	|	др_ЯрлыкиНаСкладахОстатки.Ярлык КАК др_Ярлык,
	|	др_ЯрлыкиНаСкладахОстатки.Склад КАК Склад,
	|	др_ЯрлыкиНаСкладахОстатки.КоличествоОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.др_ЯрлыкиНаСкладах.Остатки(
	|			,
	|			(Ярлык, Склад) В
	|				(ВЫБРАТЬ
	|					ТаблицаОтборов.Ярлык,
	|					ТаблицаОтборов.Склад
	|				ИЗ
	|					ТаблицаОтборов)) КАК др_ЯрлыкиНаСкладахОстатки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация";
	
	Запрос.УстановитьПараметр("ТаблицаОтборов", ТаблицаОтборов);
	ТаблицаОстатков = Запрос.Выполнить().Выгрузить();
	МассивТаблиц = ОбеспечениеСервер.РазбитьТаблицуПоЗначениюКлюча(ТаблицаОстатков, "Организация");
	
	Результат = Новый Массив;
	
	Для Каждого Элемент Из МассивТаблиц Цикл
		ДанныеЗаполнения = Элемент.Ключ;
		ДанныеЗаполнения.Вставить("Ярлыки", Элемент.Таблица); 
		
		Документ = ЗаполнитьИПровестиДокумент(ДанныеЗаполнения);
		Результат.Добавить(Документ);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет и проводит документ созданный обработкой.
//
Функция ЗаполнитьИПровестиДокумент(ДанныеЗаполнения)
	
	ДокументОбъект = Документы.др_РеализацияЯрлыков.СоздатьДокумент();
	ДанныеЗаполнения.Вставить("Дата", ТекущаяДатаСеанса());
	
	ДокументОбъект.Заполнить(ДанныеЗаполнения);
	
	ЗаписатьДокумент(ДокументОбъект);
	
	Возврат ДокументОбъект;
	
КонецФункции

Процедура ЗаписатьДокумент(ДокументОбъект)
	
	ОшибокНеОбнаружено = ДокументОбъект.ПроверитьЗаполнение();
	
	Если Не ОшибокНеОбнаружено Тогда
		
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		
	Иначе //нет ошибок, проводим документ
		
		Попытка
			
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			
		Исключение
			
			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
			ЗаписьЖурналаРегистрации("Создание реализаций", УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
```

</details>

9.3. Добавить код в модуль др_РеализацияЯрлыков

<details>

<summary>Просмотр кода</summary>

### Код модуля

```
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий


Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьДокументПоОтбору(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры



#КонецОбласти

#Область СлужебныеПроцедурыИФункции


#Область ИнициализацияИЗаполнение

Процедура ЗаполнитьДокументПоОтбору(Знач ДанныеЗаполнения)
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	
	Если ДанныеЗаполнения.Свойство("Ярлыки") Тогда
		ЭтотОбъект.Ярлыки.Загрузить(ДанныеЗаполнения.Ярлыки);
		ЗаполнитьПодразделенияСкладов();
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПодразделенияСкладов()
	
	ПодразделенияСкладов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Ярлыки.ВыгрузитьКолонку("Склад"), "Подразделение");
	Для Каждого СтрокаЯрлыков Из Ярлыки Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаЯрлыков.Склад) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаЯрлыков.Подразделение = ПодразделенияСкладов[СтрокаЯрлыков.Склад];
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти




#КонецЕсли





```

</details>



10. Синхронизация с бухгалтерией.

10.1. Изменить состав плана обмена

10.1.1. Добавить Справочник др_Ярлыки

10.1.2. Добавить документ др_ПриобретениеЯрлыков

10.2. Изменить Подписки на события

10.2.1. Справочник др_Ярлыки для СинхронизацияДанныхЧерезУниверсальныйФорматРегистрация

10.2.2. Документ др_ПриобретениеЯрлыков для	СинхронизацияДанныхЧерезУниверсальныйФорматРегистрацияДокумента

10.3. Добавить 	XDTO-пакет (YR3)

10.4. Расширить пакет

10.5. Изменить 	МенеджерОбменаЧерезУниверсальныйФормат	МенеджерОбменаЧерезУниверсальныйФормат		

