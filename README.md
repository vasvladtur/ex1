## Демонстрация некоторых случаев с которыми я столкнулся.
 Сделано на 
 1С:Предприятие 8.3 (8.3.23.1782) 1С:ERP Управление предприятием 2 (2.5.12.53)
 
 Изменения где необходимо помечаются комментарием 
 ```
 //1729
 //-1729
 ```
 Реализовать можно и в расширении, но 
### Я решил:  меняем конфигурацию. (оригинальную сохраняем в безопасном месте)
 
 Задачи сгенерированы обычными пользователями
 
 Все идеи взяты из открытых источников. (Спасибо авторам)
 
 Новые сущности имеют префикс др_
 
 Изменения носят ДЕМО характер
 
1. Снимаем с поддержки

2. Создать форму списка справочника серии независящее от параметров открытия

2.1. Создать форму списка справочника серии (просто без кода, добавляем реквизиты справочника)
 делаем ее основной

2.2. Добавить в подсистему НСИ и администрирование -> НСИ -> Серии номенклатуры

3. Создать подсистему Расширение (Тестовое решение)

4. _
 

5. Создать возможность для (CRM и маркетинг)->(Скидки (наценки))->
(Создать элемент (например, 
- Тип скидки (скидка количеством),
- предоставляется на: Номенклатуру из списка, 
- нажать гиперссылка справа,
- кнопка Подобрать товары)
В этой форме реализовать: Добавить все характеристики номенклатуры при выборе номенклатуры


5.1. Решение

5.2. Добавить в форму обработки ПодборТоваровВДокументПродажи кнопку и команду

<details>

<summary>Просмотр кода</summary>

### Код формы

```
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

...
 	
//1729 Задача 5.1. Программно Добавить Команду и кнопку
// для добавления характеристик при выборе номенклатуры
	Команда = Команды.Добавить(
		"ДобавитьНоменклатуруСХарактеристиками"); //Имя команды
	Команда.Заголовок = "Добавить все Характеристики номенклатуры";
	Команда.Действие  = "др_ДобавитьНоменклатуруСХарактеристикамиПосле"; //Имя связанной процедуры
	 	
	//Добавление кнопки формы
	КнопкаФормы = Элементы.Добавить(
		"др_КнопкаДобавитьНоменклатуруСХарактеристиками", //Имя кнопки
		Тип("КнопкаФормы"),             //Тип, всегда КнопкаФормы
		КоманднаяПанель);                      //Контейнер для кнопки 
		
	КнопкаФормы.ИмяКоманды = "ДобавитьНоменклатуруСХарактеристиками"; //Связь с командой по имени
	
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка; 
//1729- Программно Добавить Команду и кнопку
	

	
КонецПроцедуры

```

</details>


5.3. Открыть форму Выбора номенклатуры

<details>

<summary>Просмотр кода</summary>

### Код формы

```

 //1729 Задача 5.3. Добавить все характеристики номенклатуры при выборе номенклатуры
&НаКлиенте
Процедура др_ДобавитьНоменклатуруСХарактеристикамиПосле(Команда)
	ОповещениеОзакрытии = Новый ОписаниеОповещения("др_ДобавитьНоменклатуруСХарактеристикамиЗавершение",ЭтотОбъект);
	параметрыОткрытия = Новый Структура("РежимВыбора,МножественныйВыбор,ЗакрыватьПриВыборе",Истина,Истина,Истина);
//	открыть форму выбора справочника номенклатура
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора",параметрыОткрытия,ЭтотОбъект,,,,ОповещениеОзакрытии );
	  
КонецПроцедуры

&НаКлиенте
Процедура др_ДобавитьНоменклатуруСХарактеристикамиЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт 
    Если ЗначениеЗаполнено(РезультатЗакрытия) Тогда
		др_ДобавитьНоменклатуруСХарактеристикамиЗавершениеСервер(РезультатЗакрытия);
	КонецЕсли;
КонецПроцедуры
 
&НаСервере
Процедура др_ДобавитьНоменклатуруСХарактеристикамиЗавершениеСервер(СписокТоваров)   
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Номенклатура,
	|	Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
	|	Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры
	|ПОМЕСТИТЬ втНоменклатураСВидомНоменклатуры
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка В(&СписокТоваров)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втНоменклатураСВидомНоменклатуры.Номенклатура КАК Номенклатура,
	|	ХарактеристикиНоменклатуры.Ссылка КАК Характеристика,
	|	1 КАК Количество,
	|	1 КАК КоличествоУпаковок,
	|	ХарактеристикиНоменклатуры.Ссылка КАК Артикул,
	|	Ложь КАК Обособленно,
	|	Истина КАК ХарактеристикиИспользуются,
	|	втНоменклатураСВидомНоменклатуры.ТипНоменклатуры КАК ТипНоменклатуры,
	|	Ложь КАК ПроизводитсяВПроцессе,
	|	Истина КАК ЗаказатьНаСклад
	|ИЗ
	|	втНоменклатураСВидомНоменклатуры КАК втНоменклатураСВидомНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО (втНоменклатураСВидомНоменклатуры.ВидНоменклатуры = (ВЫРАЗИТЬ(ХарактеристикиНоменклатуры.Владелец КАК Справочник.ВидыНоменклатуры)))
	|ГДЕ
	|	НЕ ХарактеристикиНоменклатуры.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура";
	
	Запрос.УстановитьПараметр("СписокТоваров", СписокТоваров);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл                               
		НоваяСтрока = Объект.Корзина.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,ВыборкаДетальныеЗаписи);
		Элементы.Корзина.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
	КонецЦикла;

КонецПроцедуры
//-1729 

```


</details>

5.4. Обработать выбор

### Учет ярлыков. Ярлыки не номенклатура. 

- Компания приобретает Ярлыки у стороних Организаций.
- Ярлыки учитываются по определенной Организации и бывают платными и бесплатными и поступают на выбранный складу.
- Автоматически списываются оператором .
- Платные ярлыки выгружаются в бухгалтерию.
- Учет количественный


6. Справочник 	др_Ярлыки, Перечисление 	др_ТипыЯрлыков (Платный, бесплатный															

7. Создать документ др_ПриобретениеЯрлыков 

<details>

<summary>Структура метаданных документа др_ПриобретениеЯрлыков</summary>

### Структура метаданных документа др_ПриобретениеЯрлыков

```
//1729 Задача 7.6. 
др_ПриобретениеЯрлыков

реквизит
-Организация

таб часть
	Ярлыки
    - др_Ярлык
    - Склад
    - Количество

```

</details>



7.1. Настроить интерактивно проверку заполнения для др_Ярлык и Склад

7.2. Настроить программно проверку заполнения для Склад (если Тип ярлыка бесплатный не проверять)

<details>

<summary>Просмотр кода</summary>

### Код общего модуля

```
//1729 Задача 7.2. 
        
#Область ПрограммныйИнтерфейс



#Область Оформление

// Создает элемент условного ооформления одного параметра одного поля по одному отбору.
// Параметры:
//  Форма                        - ФормаКлиентскогоПриложения       - На которую необходимо добавить условное оформление
//  ОформляемоеПоле              - Строка, Неопределено             - Имя офомляемого элемента формы. Например, "Товары". Если не передавать, то оформляемое поле не будет добавлено
//  ИмяПараметраОформления       - Строка, ПараметрКомпоновкиДанных - Указывает что будет изменено в офомлении поля. Например, "ЦветФона" / "Текст"
//  ЗначениеПараметраОформления  - Произвольный                     - Значение оформления. Например, <Новый Цвет(222, 255, 222)>
//  ПутьКЛевомуЗначениюОтбора    - Строка, Неопределено             - Путь к полю по которому выполняется отбор условного оформления. Например, "Товары.Номенклатура" / "Контрагент". Если не передавать, то отбор не будет установлен
//  ВидСравненияОтбора           - ВидСравненияКомпоновкиДанных     - Вид сравнения. По умолчанию "ВидСравненияКомпоновкиДанных.Равно"
//  ПравоеЗначениеОтбора         - Произвольный                     - С чем сравнивать левое значение
//
// ВозвращаемоеЗначение:
//  ЭлементУсловногоОформления - Добавленный элемент оформления
Функция ДобавитьПростойЭлементУсловногоОформления(Форма, ОформляемоеПоле = Неопределено, 
	ИмяПараметраОформления = Неопределено, ЗначениеПараметраОформления = Неопределено, 
	ПутьКЛевомуЗначениюОтбора = Неопределено, ВидСравненияОтбора = Неопределено, ПравоеЗначениеОтбора = Неопределено) Экспорт
	
	//Добавляем элемент:
	ЭлементУсловногоОформления = Форма.УсловноеОформление.Элементы.Добавить();
	
	Если ЗначениеЗаполнено(ИмяПараметраОформления) Тогда
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра(ИмяПараметраОформления, ЗначениеПараметраОформления);
	КонецЕсли;
	
	Если НЕ ПутьКЛевомуЗначениюОтбора = Неопределено Тогда
		ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(ПутьКЛевомуЗначениюОтбора);
		ОтборЭлемента.ВидСравнения   = ?(ВидСравненияОтбора = Неопределено, ВидСравненияКомпоновкиДанных.Равно, ВидСравненияОтбора);
		ОтборЭлемента.ПравоеЗначение = ПравоеЗначениеОтбора;
	КонецЕсли;
	
	Если НЕ ОформляемоеПоле = Неопределено Тогда
		ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ОформляемоеПоле);
	КонецЕсли;

	Возврат ЭлементУсловногоОформления;
	
КонецФункции

#КонецОбласти

#Область ДинамическийСписок

////////////////////////////////////////////////////////////////////////////////
// Функции для работы с отборами и параметрами динамических списков.
//

// Найти элемент или группу отбора по заданному имени поля или представлению.
//
// Параметры:
//  ОбластьПоиска - ОтборКомпоновкиДанных
//                - КоллекцияЭлементовОтбораКомпоновкиДанных
//                - ГруппаЭлементовОтбораКомпоновкиДанных    - контейнер с элементами и группами отбора,
//                                                             например Список.Отбор или группа в отборе.
//  ИмяПоля       - Строка - имя поля компоновки (не используется для групп).
//  Представление - Строка - представление поля компоновки.
//
// Возвращаемое значение:
//  Массив - коллекция отборов.
//
Функция НайтиЭлементыИГруппыОтбора(Знач ОбластьПоиска,
									Знач ИмяПоля = Неопределено,
									Знач Представление = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ИмяПоля) Тогда
		ЗначениеПоиска = Новый ПолеКомпоновкиДанных(ИмяПоля);
		СпособПоиска = 1;
	Иначе
		СпособПоиска = 2;
		ЗначениеПоиска = Представление;
	КонецЕсли;
	
	МассивЭлементов = Новый Массив;
	
	НайтиРекурсивно(ОбластьПоиска.Элементы, МассивЭлементов, СпособПоиска, ЗначениеПоиска);
	
	Возврат МассивЭлементов;
	
КонецФункции

// Добавить группу отбора в коллекцию КоллекцияЭлементов.
//
// Параметры:
//  КоллекцияЭлементов - ОтборКомпоновкиДанных
//                     - КоллекцияЭлементовОтбораКомпоновкиДанных
//                     - ГруппаЭлементовОтбораКомпоновкиДанных    - контейнер с элементами и группами отбора,
//                                                                  например Список.Отбор или группа в отборе.
//  Представление      - Строка - представление группы.
//  ТипГруппы          - ТипГруппыЭлементовОтбораКомпоновкиДанных - тип группы.
//
// Возвращаемое значение:
//  ГруппаЭлементовОтбораКомпоновкиДанных - группа отбора.
//
Функция СоздатьГруппуЭлементовОтбора(Знач КоллекцияЭлементов, Представление, ТипГруппы) Экспорт
	
	Если ТипЗнч(КоллекцияЭлементов) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных")
		Или ТипЗнч(КоллекцияЭлементов) = Тип("ОтборКомпоновкиДанных") Тогда
		
		КоллекцияЭлементов = КоллекцияЭлементов.Элементы;
	КонецЕсли;
	
	ГруппаЭлементовОтбора = НайтиЭлементОтбораПоПредставлению(КоллекцияЭлементов, Представление);
	Если ГруппаЭлементовОтбора = Неопределено Тогда
		ГруппаЭлементовОтбора = КоллекцияЭлементов.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	Иначе
		ГруппаЭлементовОтбора.Элементы.Очистить();
	КонецЕсли;
	
	ГруппаЭлементовОтбора.Представление    = Представление;
	ГруппаЭлементовОтбора.Применение       = ТипПримененияОтбораКомпоновкиДанных.Элементы;
	ГруппаЭлементовОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ГруппаЭлементовОтбора.ТипГруппы        = ТипГруппы;
	ГруппаЭлементовОтбора.Использование    = Истина;
	
	Возврат ГруппаЭлементовОтбора;
	
КонецФункции

// Добавить элемент компоновки в контейнер элементов компоновки.
//
// Параметры:
//  ОбластьДобавления - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                                                 например, Список.Отбор или группа в отборе.
//  ИмяПоля                 - Строка - имя поля компоновки данных (заполняется всегда).
//  ВидСравнения            - ВидСравненияКомпоновкиДанных - вид сравнения.
//  ПравоеЗначение          - Произвольный - сравниваемое значение.
//  Представление           - Строка - представление элемента компоновки данных.
//  Использование           - Булево - использование элемента.
//  РежимОтображения        - РежимОтображенияЭлементаНастройкиКомпоновкиДанных - режим отображения.
//  ИдентификаторПользовательскойНастройки - Строка - см. ОтборКомпоновкиДанных.ИдентификаторПользовательскойНастройки
//                                                    в синтакс-помощнике.
// Возвращаемое значение:
//  ЭлементОтбораКомпоновкиДанных - элемент компоновки.
//
Функция ДобавитьЭлементКомпоновки(ОбластьДобавления,
									Знач ИмяПоля,
									Знач ВидСравнения,
									Знач ПравоеЗначение = Неопределено,
									Знач Представление  = Неопределено,
									Знач Использование  = Неопределено,
									знач РежимОтображения = Неопределено,
									знач ИдентификаторПользовательскойНастройки = Неопределено) Экспорт
	
	Элемент = ОбластьДобавления.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Элемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоля);
	Элемент.ВидСравнения = ВидСравнения;
	
	Если РежимОтображения = Неопределено Тогда
		Элемент.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	Иначе
		Элемент.РежимОтображения = РежимОтображения;
	КонецЕсли;
	
	Если ПравоеЗначение <> Неопределено Тогда
		Элемент.ПравоеЗначение = ПравоеЗначение;
	КонецЕсли;
	
	Если Представление <> Неопределено Тогда
		Элемент.Представление = Представление;
	КонецЕсли;
	
	Если Использование <> Неопределено Тогда
		Элемент.Использование = Использование;
	КонецЕсли;
	
	// Важно: установка идентификатора должна выполняться
	// в конце настройки элемента, иначе он будет скопирован
	// в пользовательские настройки частично заполненным.
	Если ИдентификаторПользовательскойНастройки <> Неопределено Тогда
		Элемент.ИдентификаторПользовательскойНастройки = ИдентификаторПользовательскойНастройки;
	ИначеЕсли Элемент.РежимОтображения <> РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
		Элемент.ИдентификаторПользовательскойНастройки = ИмяПоля;
	КонецЕсли;
	
	Возврат Элемент;
	
КонецФункции

// Изменить элемент отбора с заданным именем поля или представлением.
//
// Параметры:
//  ОбластьПоиска - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                                             например Список.Отбор или группа в отборе.
//  ИмяПоля                 - Строка - имя поля компоновки данных (заполняется всегда).
//  Представление           - Строка - представление элемента компоновки данных.
//  ПравоеЗначение          - Произвольный - сравниваемое значение.
//  ВидСравнения            - ВидСравненияКомпоновкиДанных - вид сравнения.
//  Использование           - Булево - использование элемента.
//  РежимОтображения        - РежимОтображенияЭлементаНастройкиКомпоновкиДанных - режим отображения.
//  ИдентификаторПользовательскойНастройки - Строка - см. ОтборКомпоновкиДанных.ИдентификаторПользовательскойНастройки
//                                                    в синтакс-помощнике.
//
// Возвращаемое значение:
//  Число - количество измененных элементов.
//
Функция ИзменитьЭлементыОтбора(ОбластьПоиска,
								Знач ИмяПоля = Неопределено,
								Знач Представление = Неопределено,
								Знач ПравоеЗначение = Неопределено,
								Знач ВидСравнения = Неопределено,
								Знач Использование = Неопределено,
								Знач РежимОтображения = Неопределено,
								Знач ИдентификаторПользовательскойНастройки = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ИмяПоля) Тогда
		ЗначениеПоиска = Новый ПолеКомпоновкиДанных(ИмяПоля);
		СпособПоиска = 1;
	Иначе
		СпособПоиска = 2;
		ЗначениеПоиска = Представление;
	КонецЕсли;
	
	МассивЭлементов = Новый Массив;
	
	НайтиРекурсивно(ОбластьПоиска.Элементы, МассивЭлементов, СпособПоиска, ЗначениеПоиска);
	
	Для Каждого Элемент Из МассивЭлементов Цикл
		Если ИмяПоля <> Неопределено Тогда
			Элемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоля);
		КонецЕсли;
		Если Представление <> Неопределено Тогда
			Элемент.Представление = Представление;
		КонецЕсли;
		Если Использование <> Неопределено Тогда
			Элемент.Использование = Использование;
		КонецЕсли;
		Если ВидСравнения <> Неопределено Тогда
			Элемент.ВидСравнения = ВидСравнения;
		КонецЕсли;
		Если ПравоеЗначение <> Неопределено Тогда
			Элемент.ПравоеЗначение = ПравоеЗначение;
		КонецЕсли;
		Если РежимОтображения <> Неопределено Тогда
			Элемент.РежимОтображения = РежимОтображения;
		КонецЕсли;
		Если ИдентификаторПользовательскойНастройки <> Неопределено Тогда
			Элемент.ИдентификаторПользовательскойНастройки = ИдентификаторПользовательскойНастройки;
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивЭлементов.Количество();
	
КонецФункции

// Удалить элементы отбора с заданным именем поля или представлением.
//
// Параметры:
//  ОбластьУдаления - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                                               например, Список.Отбор или группа в отборе..
//  ИмяПоля         - Строка - имя поля компоновки (не используется для групп).
//  Представление   - Строка - представление поля компоновки.
//
Процедура УдалитьЭлементыГруппыОтбора(Знач ОбластьУдаления, Знач ИмяПоля = Неопределено, Знач Представление = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ИмяПоля) Тогда
		ЗначениеПоиска = Новый ПолеКомпоновкиДанных(ИмяПоля);
		СпособПоиска = 1;
	Иначе
		СпособПоиска = 2;
		ЗначениеПоиска = Представление;
	КонецЕсли;
	
	МассивЭлементов = Новый Массив; // Массив из ЭлементОтбораКомпоновкиДанных, ГруппаЭлементовОтбораКомпоновкиДанных
	
	НайтиРекурсивно(ОбластьУдаления.Элементы, МассивЭлементов, СпособПоиска, ЗначениеПоиска);
	
	Для Каждого Элемент Из МассивЭлементов Цикл
		Если Элемент.Родитель = Неопределено Тогда
			ОбластьУдаления.Элементы.Удалить(Элемент);
		Иначе
			Элемент.Родитель.Элементы.Удалить(Элемент);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Добавить или заменить существующий элемент отбора.
//
// Параметры:
//  ОбластьПоискаДобавления - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                     например, Список.Отбор или группа в отборе.
//  ИмяПоля                 - Строка - имя поля компоновки данных (заполняется всегда).
//  ПравоеЗначение          - Произвольный - сравниваемое значение.
//  ВидСравнения            - ВидСравненияКомпоновкиДанных - вид сравнения.
//  Представление           - Строка - представление элемента компоновки данных.
//  Использование           - Булево - использование элемента.
//  РежимОтображения        - РежимОтображенияЭлементаНастройкиКомпоновкиДанных - режим отображения.
//  ИдентификаторПользовательскойНастройки - Строка - см. ОтборКомпоновкиДанных.ИдентификаторПользовательскойНастройки
//                                                    в синтакс-помощнике.
//
Процедура УстановитьЭлементОтбора(ОбластьПоискаДобавления,
								Знач ИмяПоля,
								Знач ПравоеЗначение = Неопределено,
								Знач ВидСравнения = Неопределено,
								Знач Представление = Неопределено,
								Знач Использование = Неопределено,
								Знач РежимОтображения = Неопределено,
								Знач ИдентификаторПользовательскойНастройки = Неопределено) Экспорт
	
	ЧислоИзмененных = ИзменитьЭлементыОтбора(ОбластьПоискаДобавления, ИмяПоля, Представление,
							ПравоеЗначение, ВидСравнения, Использование, РежимОтображения, ИдентификаторПользовательскойНастройки);
	
	Если ЧислоИзмененных = 0 Тогда
		Если ВидСравнения = Неопределено Тогда
			Если ТипЗнч(ПравоеЗначение) = Тип("Массив")
				Или ТипЗнч(ПравоеЗначение) = Тип("ФиксированныйМассив")
				Или ТипЗнч(ПравоеЗначение) = Тип("СписокЗначений") Тогда
				ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
			Иначе
				ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			КонецЕсли;
		КонецЕсли;
		Если РежимОтображения = Неопределено Тогда
			РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		КонецЕсли;
		ДобавитьЭлементКомпоновки(ОбластьПоискаДобавления, ИмяПоля, ВидСравнения,
								ПравоеЗначение, Представление, Использование, РежимОтображения, ИдентификаторПользовательскойНастройки);
	КонецЕсли;
	
КонецПроцедуры

// Добавить или заменить существующий элемент отбора динамического списка.
//
// Параметры:
//   ДинамическийСписок - ДинамическийСписок - список, в котором требуется установить отбор.
//   ИмяПоля            - Строка - поле, по которому необходимо установить отбор.
//   ПравоеЗначение     - Произвольный - значение отбора.
//       Необязательный. Значение по умолчанию Неопределено.
//       Внимание! Если передать Неопределено, то значение не будет изменено.
//   ВидСравнения  - ВидСравненияКомпоновкиДанных - условие отбора.
//   Представление - Строка - представление элемента компоновки данных.
//       Необязательный. Значение по умолчанию Неопределено.
//       Если указано, то выводится только флажок использования с указанным представлением (значение не выводится).
//       Для очистки (чтобы значение снова выводилось) следует передать пустую строку.
//   Использование - Булево - флажок использования этого отбора.
//       Необязательный. Значение по умолчанию: Неопределено.
//   РежимОтображения - РежимОтображенияЭлементаНастройкиКомпоновкиДанных - способ отображения этого отбора
//                                                                          пользователю:
//        РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ - в группе быстрых настроек над списком.
//        РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный       - в настройка списка (в подменю Еще).
//        РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный   - запретить пользователю менять этот отбор.
//   ИдентификаторПользовательскойНастройки - Строка - уникальный идентификатор этого отбора.
//       Используется для связи с пользовательскими настройками.
//
Процедура УстановитьЭлементОтбораДинамическогоСписка(ДинамическийСписок, ИмяПоля,
	ПравоеЗначение = Неопределено,
	ВидСравнения = Неопределено,
	Представление = Неопределено,
	Использование = Неопределено,
	РежимОтображения = Неопределено,
	ИдентификаторПользовательскойНастройки = Неопределено) Экспорт
	
	Если РежимОтображения = Неопределено Тогда
		РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	КонецЕсли;
	
	Если РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
		ОтборДинамическогоСписка = ДинамическийСписок.КомпоновщикНастроек.ФиксированныеНастройки.Отбор;
	Иначе
		ОтборДинамическогоСписка = ДинамическийСписок.КомпоновщикНастроек.Настройки.Отбор;
	КонецЕсли;
	
	УстановитьЭлементОтбора(
		ОтборДинамическогоСписка,
		ИмяПоля,
		ПравоеЗначение,
		ВидСравнения,
		Представление,
		Использование,
		РежимОтображения,
		ИдентификаторПользовательскойНастройки);
	
КонецПроцедуры

// Удалить элемент группы отбора динамического списка.
//
// Параметры:
//  ДинамическийСписок - ДинамическийСписок - реквизит формы, для которого требуется установить отбор.
//  ИмяПоля         - Строка - имя поля компоновки (не используется для групп).
//  Представление   - Строка - представление поля компоновки.
//
Процедура УдалитьЭлементыГруппыОтбораДинамическогоСписка(ДинамическийСписок, ИмяПоля = Неопределено, Представление = Неопределено) Экспорт
	
	УдалитьЭлементыГруппыОтбора(
		ДинамическийСписок.КомпоновщикНастроек.ФиксированныеНастройки.Отбор,
		ИмяПоля,
		Представление);
	
	УдалитьЭлементыГруппыОтбора(
		ДинамическийСписок.КомпоновщикНастроек.Настройки.Отбор,
		ИмяПоля,
		Представление);
	
КонецПроцедуры

// Установить или обновить значение параметра ИмяПараметра динамического списка Список.
//
// Параметры:
//  Список          - ДинамическийСписок - реквизит формы, для которого требуется установить параметр.
//  ИмяПараметра    - Строка             - имя параметра динамического списка.
//  Значение        - Произвольный        - новое значение параметра.
//  Использование   - Булево             - признак использования параметра.
//
Процедура УстановитьПараметрДинамическогоСписка(Список, ИмяПараметра, Значение, Использование = Истина) Экспорт
	
	ЗначениеПараметраКомпоновкиДанных = Список.Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
	Если ЗначениеПараметраКомпоновкиДанных <> Неопределено Тогда
		Если Использование И ЗначениеПараметраКомпоновкиДанных.Значение <> Значение Тогда
			ЗначениеПараметраКомпоновкиДанных.Значение = Значение;
		КонецЕсли;
		Если ЗначениеПараметраКомпоновкиДанных.Использование <> Использование Тогда
			ЗначениеПараметраКомпоновкиДанных.Использование = Использование;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ДинамическийСписок

Процедура НайтиРекурсивно(КоллекцияЭлементов, МассивЭлементов, СпособПоиска, ЗначениеПоиска)
	
	Для каждого ЭлементОтбора Из КоллекцияЭлементов Цикл
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			
			Если СпособПоиска = 1 Тогда
				Если ЭлементОтбора.ЛевоеЗначение = ЗначениеПоиска Тогда
					МассивЭлементов.Добавить(ЭлементОтбора);
				КонецЕсли;
			ИначеЕсли СпособПоиска = 2 Тогда
				Если ЭлементОтбора.Представление = ЗначениеПоиска Тогда
					МассивЭлементов.Добавить(ЭлементОтбора);
				КонецЕсли;
			КонецЕсли;
		Иначе
			
			НайтиРекурсивно(ЭлементОтбора.Элементы, МассивЭлементов, СпособПоиска, ЗначениеПоиска);
			
			Если СпособПоиска = 2 И ЭлементОтбора.Представление = ЗначениеПоиска Тогда
				МассивЭлементов.Добавить(ЭлементОтбора);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Выполняет поиск элемента отбора в коллекции по заданному представлению.
//
// Параметры:
//  КоллекцияЭлементов - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                                                  например, Список.Отбор.Элементы или группа в отборе.
//  Представление - Строка - представление группы.
// 
// Возвращаемое значение:
//  ЭлементОтбораКомпоновкиДанных - элемент отбора.
//
Функция НайтиЭлементОтбораПоПредставлению(КоллекцияЭлементов, Представление) Экспорт
	
	ВозвращаемоеЗначение = Неопределено;
	
	Для каждого ЭлементОтбора Из КоллекцияЭлементов Цикл
		Если ЭлементОтбора.Представление = Представление Тогда
			ВозвращаемоеЗначение = ЭлементОтбора;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение
	
КонецФункции

#КонецОбласти

#КонецОбласти
```

</details>

7.3. Настроить Связи параметров выбора реквизита таб части др_Ярлык отбор по Организации)

7.4. Создать обший модуль 	др_ОбщегоНазначенияКлиентСервер							  
(Функции для работы с отборами и параметрами динамических списков)
<details>

<summary>Просмотр кода</summary>

### Код обший модуль

```
//1729 Задача 7.4.         
#Область ПрограммныйИнтерфейс



#Область Оформление

// Создает элемент условного ооформления одного параметра одного поля по одному отбору.
// Параметры:
//  Форма                        - ФормаКлиентскогоПриложения       - На которую необходимо добавить условное оформление
//  ОформляемоеПоле              - Строка, Неопределено             - Имя офомляемого элемента формы. Например, "Товары". Если не передавать, то оформляемое поле не будет добавлено
//  ИмяПараметраОформления       - Строка, ПараметрКомпоновкиДанных - Указывает что будет изменено в офомлении поля. Например, "ЦветФона" / "Текст"
//  ЗначениеПараметраОформления  - Произвольный                     - Значение оформления. Например, <Новый Цвет(222, 255, 222)>
//  ПутьКЛевомуЗначениюОтбора    - Строка, Неопределено             - Путь к полю по которому выполняется отбор условного оформления. Например, "Товары.Номенклатура" / "Контрагент". Если не передавать, то отбор не будет установлен
//  ВидСравненияОтбора           - ВидСравненияКомпоновкиДанных     - Вид сравнения. По умолчанию "ВидСравненияКомпоновкиДанных.Равно"
//  ПравоеЗначениеОтбора         - Произвольный                     - С чем сравнивать левое значение
//
// ВозвращаемоеЗначение:
//  ЭлементУсловногоОформления - Добавленный элемент оформления
Функция ДобавитьПростойЭлементУсловногоОформления(Форма, ОформляемоеПоле = Неопределено, 
	ИмяПараметраОформления = Неопределено, ЗначениеПараметраОформления = Неопределено, 
	ПутьКЛевомуЗначениюОтбора = Неопределено, ВидСравненияОтбора = Неопределено, ПравоеЗначениеОтбора = Неопределено) Экспорт
	
	//Добавляем элемент:
	ЭлементУсловногоОформления = Форма.УсловноеОформление.Элементы.Добавить();
	
	Если ЗначениеЗаполнено(ИмяПараметраОформления) Тогда
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра(ИмяПараметраОформления, ЗначениеПараметраОформления);
	КонецЕсли;
	
	Если НЕ ПутьКЛевомуЗначениюОтбора = Неопределено Тогда
		ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(ПутьКЛевомуЗначениюОтбора);
		ОтборЭлемента.ВидСравнения   = ?(ВидСравненияОтбора = Неопределено, ВидСравненияКомпоновкиДанных.Равно, ВидСравненияОтбора);
		ОтборЭлемента.ПравоеЗначение = ПравоеЗначениеОтбора;
	КонецЕсли;
	
	Если НЕ ОформляемоеПоле = Неопределено Тогда
		ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ОформляемоеПоле);
	КонецЕсли;

	Возврат ЭлементУсловногоОформления;
	
КонецФункции

#КонецОбласти

#Область ДинамическийСписок

////////////////////////////////////////////////////////////////////////////////
// Функции для работы с отборами и параметрами динамических списков.
//

// Найти элемент или группу отбора по заданному имени поля или представлению.
//
// Параметры:
//  ОбластьПоиска - ОтборКомпоновкиДанных
//                - КоллекцияЭлементовОтбораКомпоновкиДанных
//                - ГруппаЭлементовОтбораКомпоновкиДанных    - контейнер с элементами и группами отбора,
//                                                             например Список.Отбор или группа в отборе.
//  ИмяПоля       - Строка - имя поля компоновки (не используется для групп).
//  Представление - Строка - представление поля компоновки.
//
// Возвращаемое значение:
//  Массив - коллекция отборов.
//
Функция НайтиЭлементыИГруппыОтбора(Знач ОбластьПоиска,
									Знач ИмяПоля = Неопределено,
									Знач Представление = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ИмяПоля) Тогда
		ЗначениеПоиска = Новый ПолеКомпоновкиДанных(ИмяПоля);
		СпособПоиска = 1;
	Иначе
		СпособПоиска = 2;
		ЗначениеПоиска = Представление;
	КонецЕсли;
	
	МассивЭлементов = Новый Массив;
	
	НайтиРекурсивно(ОбластьПоиска.Элементы, МассивЭлементов, СпособПоиска, ЗначениеПоиска);
	
	Возврат МассивЭлементов;
	
КонецФункции

// Добавить группу отбора в коллекцию КоллекцияЭлементов.
//
// Параметры:
//  КоллекцияЭлементов - ОтборКомпоновкиДанных
//                     - КоллекцияЭлементовОтбораКомпоновкиДанных
//                     - ГруппаЭлементовОтбораКомпоновкиДанных    - контейнер с элементами и группами отбора,
//                                                                  например Список.Отбор или группа в отборе.
//  Представление      - Строка - представление группы.
//  ТипГруппы          - ТипГруппыЭлементовОтбораКомпоновкиДанных - тип группы.
//
// Возвращаемое значение:
//  ГруппаЭлементовОтбораКомпоновкиДанных - группа отбора.
//
Функция СоздатьГруппуЭлементовОтбора(Знач КоллекцияЭлементов, Представление, ТипГруппы) Экспорт
	
	Если ТипЗнч(КоллекцияЭлементов) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных")
		Или ТипЗнч(КоллекцияЭлементов) = Тип("ОтборКомпоновкиДанных") Тогда
		
		КоллекцияЭлементов = КоллекцияЭлементов.Элементы;
	КонецЕсли;
	
	ГруппаЭлементовОтбора = НайтиЭлементОтбораПоПредставлению(КоллекцияЭлементов, Представление);
	Если ГруппаЭлементовОтбора = Неопределено Тогда
		ГруппаЭлементовОтбора = КоллекцияЭлементов.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	Иначе
		ГруппаЭлементовОтбора.Элементы.Очистить();
	КонецЕсли;
	
	ГруппаЭлементовОтбора.Представление    = Представление;
	ГруппаЭлементовОтбора.Применение       = ТипПримененияОтбораКомпоновкиДанных.Элементы;
	ГруппаЭлементовОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ГруппаЭлементовОтбора.ТипГруппы        = ТипГруппы;
	ГруппаЭлементовОтбора.Использование    = Истина;
	
	Возврат ГруппаЭлементовОтбора;
	
КонецФункции

// Добавить элемент компоновки в контейнер элементов компоновки.
//
// Параметры:
//  ОбластьДобавления - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                                                 например, Список.Отбор или группа в отборе.
//  ИмяПоля                 - Строка - имя поля компоновки данных (заполняется всегда).
//  ВидСравнения            - ВидСравненияКомпоновкиДанных - вид сравнения.
//  ПравоеЗначение          - Произвольный - сравниваемое значение.
//  Представление           - Строка - представление элемента компоновки данных.
//  Использование           - Булево - использование элемента.
//  РежимОтображения        - РежимОтображенияЭлементаНастройкиКомпоновкиДанных - режим отображения.
//  ИдентификаторПользовательскойНастройки - Строка - см. ОтборКомпоновкиДанных.ИдентификаторПользовательскойНастройки
//                                                    в синтакс-помощнике.
// Возвращаемое значение:
//  ЭлементОтбораКомпоновкиДанных - элемент компоновки.
//
Функция ДобавитьЭлементКомпоновки(ОбластьДобавления,
									Знач ИмяПоля,
									Знач ВидСравнения,
									Знач ПравоеЗначение = Неопределено,
									Знач Представление  = Неопределено,
									Знач Использование  = Неопределено,
									знач РежимОтображения = Неопределено,
									знач ИдентификаторПользовательскойНастройки = Неопределено) Экспорт
	
	Элемент = ОбластьДобавления.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Элемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоля);
	Элемент.ВидСравнения = ВидСравнения;
	
	Если РежимОтображения = Неопределено Тогда
		Элемент.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	Иначе
		Элемент.РежимОтображения = РежимОтображения;
	КонецЕсли;
	
	Если ПравоеЗначение <> Неопределено Тогда
		Элемент.ПравоеЗначение = ПравоеЗначение;
	КонецЕсли;
	
	Если Представление <> Неопределено Тогда
		Элемент.Представление = Представление;
	КонецЕсли;
	
	Если Использование <> Неопределено Тогда
		Элемент.Использование = Использование;
	КонецЕсли;
	
	// Важно: установка идентификатора должна выполняться
	// в конце настройки элемента, иначе он будет скопирован
	// в пользовательские настройки частично заполненным.
	Если ИдентификаторПользовательскойНастройки <> Неопределено Тогда
		Элемент.ИдентификаторПользовательскойНастройки = ИдентификаторПользовательскойНастройки;
	ИначеЕсли Элемент.РежимОтображения <> РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
		Элемент.ИдентификаторПользовательскойНастройки = ИмяПоля;
	КонецЕсли;
	
	Возврат Элемент;
	
КонецФункции

// Изменить элемент отбора с заданным именем поля или представлением.
//
// Параметры:
//  ОбластьПоиска - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                                             например Список.Отбор или группа в отборе.
//  ИмяПоля                 - Строка - имя поля компоновки данных (заполняется всегда).
//  Представление           - Строка - представление элемента компоновки данных.
//  ПравоеЗначение          - Произвольный - сравниваемое значение.
//  ВидСравнения            - ВидСравненияКомпоновкиДанных - вид сравнения.
//  Использование           - Булево - использование элемента.
//  РежимОтображения        - РежимОтображенияЭлементаНастройкиКомпоновкиДанных - режим отображения.
//  ИдентификаторПользовательскойНастройки - Строка - см. ОтборКомпоновкиДанных.ИдентификаторПользовательскойНастройки
//                                                    в синтакс-помощнике.
//
// Возвращаемое значение:
//  Число - количество измененных элементов.
//
Функция ИзменитьЭлементыОтбора(ОбластьПоиска,
								Знач ИмяПоля = Неопределено,
								Знач Представление = Неопределено,
								Знач ПравоеЗначение = Неопределено,
								Знач ВидСравнения = Неопределено,
								Знач Использование = Неопределено,
								Знач РежимОтображения = Неопределено,
								Знач ИдентификаторПользовательскойНастройки = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ИмяПоля) Тогда
		ЗначениеПоиска = Новый ПолеКомпоновкиДанных(ИмяПоля);
		СпособПоиска = 1;
	Иначе
		СпособПоиска = 2;
		ЗначениеПоиска = Представление;
	КонецЕсли;
	
	МассивЭлементов = Новый Массив;
	
	НайтиРекурсивно(ОбластьПоиска.Элементы, МассивЭлементов, СпособПоиска, ЗначениеПоиска);
	
	Для Каждого Элемент Из МассивЭлементов Цикл
		Если ИмяПоля <> Неопределено Тогда
			Элемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоля);
		КонецЕсли;
		Если Представление <> Неопределено Тогда
			Элемент.Представление = Представление;
		КонецЕсли;
		Если Использование <> Неопределено Тогда
			Элемент.Использование = Использование;
		КонецЕсли;
		Если ВидСравнения <> Неопределено Тогда
			Элемент.ВидСравнения = ВидСравнения;
		КонецЕсли;
		Если ПравоеЗначение <> Неопределено Тогда
			Элемент.ПравоеЗначение = ПравоеЗначение;
		КонецЕсли;
		Если РежимОтображения <> Неопределено Тогда
			Элемент.РежимОтображения = РежимОтображения;
		КонецЕсли;
		Если ИдентификаторПользовательскойНастройки <> Неопределено Тогда
			Элемент.ИдентификаторПользовательскойНастройки = ИдентификаторПользовательскойНастройки;
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивЭлементов.Количество();
	
КонецФункции

// Удалить элементы отбора с заданным именем поля или представлением.
//
// Параметры:
//  ОбластьУдаления - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                                               например, Список.Отбор или группа в отборе..
//  ИмяПоля         - Строка - имя поля компоновки (не используется для групп).
//  Представление   - Строка - представление поля компоновки.
//
Процедура УдалитьЭлементыГруппыОтбора(Знач ОбластьУдаления, Знач ИмяПоля = Неопределено, Знач Представление = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ИмяПоля) Тогда
		ЗначениеПоиска = Новый ПолеКомпоновкиДанных(ИмяПоля);
		СпособПоиска = 1;
	Иначе
		СпособПоиска = 2;
		ЗначениеПоиска = Представление;
	КонецЕсли;
	
	МассивЭлементов = Новый Массив; // Массив из ЭлементОтбораКомпоновкиДанных, ГруппаЭлементовОтбораКомпоновкиДанных
	
	НайтиРекурсивно(ОбластьУдаления.Элементы, МассивЭлементов, СпособПоиска, ЗначениеПоиска);
	
	Для Каждого Элемент Из МассивЭлементов Цикл
		Если Элемент.Родитель = Неопределено Тогда
			ОбластьУдаления.Элементы.Удалить(Элемент);
		Иначе
			Элемент.Родитель.Элементы.Удалить(Элемент);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Добавить или заменить существующий элемент отбора.
//
// Параметры:
//  ОбластьПоискаДобавления - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                     например, Список.Отбор или группа в отборе.
//  ИмяПоля                 - Строка - имя поля компоновки данных (заполняется всегда).
//  ПравоеЗначение          - Произвольный - сравниваемое значение.
//  ВидСравнения            - ВидСравненияКомпоновкиДанных - вид сравнения.
//  Представление           - Строка - представление элемента компоновки данных.
//  Использование           - Булево - использование элемента.
//  РежимОтображения        - РежимОтображенияЭлементаНастройкиКомпоновкиДанных - режим отображения.
//  ИдентификаторПользовательскойНастройки - Строка - см. ОтборКомпоновкиДанных.ИдентификаторПользовательскойНастройки
//                                                    в синтакс-помощнике.
//
Процедура УстановитьЭлементОтбора(ОбластьПоискаДобавления,
								Знач ИмяПоля,
								Знач ПравоеЗначение = Неопределено,
								Знач ВидСравнения = Неопределено,
								Знач Представление = Неопределено,
								Знач Использование = Неопределено,
								Знач РежимОтображения = Неопределено,
								Знач ИдентификаторПользовательскойНастройки = Неопределено) Экспорт
	
	ЧислоИзмененных = ИзменитьЭлементыОтбора(ОбластьПоискаДобавления, ИмяПоля, Представление,
							ПравоеЗначение, ВидСравнения, Использование, РежимОтображения, ИдентификаторПользовательскойНастройки);
	
	Если ЧислоИзмененных = 0 Тогда
		Если ВидСравнения = Неопределено Тогда
			Если ТипЗнч(ПравоеЗначение) = Тип("Массив")
				Или ТипЗнч(ПравоеЗначение) = Тип("ФиксированныйМассив")
				Или ТипЗнч(ПравоеЗначение) = Тип("СписокЗначений") Тогда
				ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
			Иначе
				ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			КонецЕсли;
		КонецЕсли;
		Если РежимОтображения = Неопределено Тогда
			РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		КонецЕсли;
		ДобавитьЭлементКомпоновки(ОбластьПоискаДобавления, ИмяПоля, ВидСравнения,
								ПравоеЗначение, Представление, Использование, РежимОтображения, ИдентификаторПользовательскойНастройки);
	КонецЕсли;
	
КонецПроцедуры

// Добавить или заменить существующий элемент отбора динамического списка.
//
// Параметры:
//   ДинамическийСписок - ДинамическийСписок - список, в котором требуется установить отбор.
//   ИмяПоля            - Строка - поле, по которому необходимо установить отбор.
//   ПравоеЗначение     - Произвольный - значение отбора.
//       Необязательный. Значение по умолчанию Неопределено.
//       Внимание! Если передать Неопределено, то значение не будет изменено.
//   ВидСравнения  - ВидСравненияКомпоновкиДанных - условие отбора.
//   Представление - Строка - представление элемента компоновки данных.
//       Необязательный. Значение по умолчанию Неопределено.
//       Если указано, то выводится только флажок использования с указанным представлением (значение не выводится).
//       Для очистки (чтобы значение снова выводилось) следует передать пустую строку.
//   Использование - Булево - флажок использования этого отбора.
//       Необязательный. Значение по умолчанию: Неопределено.
//   РежимОтображения - РежимОтображенияЭлементаНастройкиКомпоновкиДанных - способ отображения этого отбора
//                                                                          пользователю:
//        РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ - в группе быстрых настроек над списком.
//        РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный       - в настройка списка (в подменю Еще).
//        РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный   - запретить пользователю менять этот отбор.
//   ИдентификаторПользовательскойНастройки - Строка - уникальный идентификатор этого отбора.
//       Используется для связи с пользовательскими настройками.
//
Процедура УстановитьЭлементОтбораДинамическогоСписка(ДинамическийСписок, ИмяПоля,
	ПравоеЗначение = Неопределено,
	ВидСравнения = Неопределено,
	Представление = Неопределено,
	Использование = Неопределено,
	РежимОтображения = Неопределено,
	ИдентификаторПользовательскойНастройки = Неопределено) Экспорт
	
	Если РежимОтображения = Неопределено Тогда
		РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	КонецЕсли;
	
	Если РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
		ОтборДинамическогоСписка = ДинамическийСписок.КомпоновщикНастроек.ФиксированныеНастройки.Отбор;
	Иначе
		ОтборДинамическогоСписка = ДинамическийСписок.КомпоновщикНастроек.Настройки.Отбор;
	КонецЕсли;
	
	УстановитьЭлементОтбора(
		ОтборДинамическогоСписка,
		ИмяПоля,
		ПравоеЗначение,
		ВидСравнения,
		Представление,
		Использование,
		РежимОтображения,
		ИдентификаторПользовательскойНастройки);
	
КонецПроцедуры

// Удалить элемент группы отбора динамического списка.
//
// Параметры:
//  ДинамическийСписок - ДинамическийСписок - реквизит формы, для которого требуется установить отбор.
//  ИмяПоля         - Строка - имя поля компоновки (не используется для групп).
//  Представление   - Строка - представление поля компоновки.
//
Процедура УдалитьЭлементыГруппыОтбораДинамическогоСписка(ДинамическийСписок, ИмяПоля = Неопределено, Представление = Неопределено) Экспорт
	
	УдалитьЭлементыГруппыОтбора(
		ДинамическийСписок.КомпоновщикНастроек.ФиксированныеНастройки.Отбор,
		ИмяПоля,
		Представление);
	
	УдалитьЭлементыГруппыОтбора(
		ДинамическийСписок.КомпоновщикНастроек.Настройки.Отбор,
		ИмяПоля,
		Представление);
	
КонецПроцедуры

// Установить или обновить значение параметра ИмяПараметра динамического списка Список.
//
// Параметры:
//  Список          - ДинамическийСписок - реквизит формы, для которого требуется установить параметр.
//  ИмяПараметра    - Строка             - имя параметра динамического списка.
//  Значение        - Произвольный        - новое значение параметра.
//  Использование   - Булево             - признак использования параметра.
//
Процедура УстановитьПараметрДинамическогоСписка(Список, ИмяПараметра, Значение, Использование = Истина) Экспорт
	
	ЗначениеПараметраКомпоновкиДанных = Список.Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
	Если ЗначениеПараметраКомпоновкиДанных <> Неопределено Тогда
		Если Использование И ЗначениеПараметраКомпоновкиДанных.Значение <> Значение Тогда
			ЗначениеПараметраКомпоновкиДанных.Значение = Значение;
		КонецЕсли;
		Если ЗначениеПараметраКомпоновкиДанных.Использование <> Использование Тогда
			ЗначениеПараметраКомпоновкиДанных.Использование = Использование;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ДинамическийСписок

Процедура НайтиРекурсивно(КоллекцияЭлементов, МассивЭлементов, СпособПоиска, ЗначениеПоиска)
	
	Для каждого ЭлементОтбора Из КоллекцияЭлементов Цикл
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			
			Если СпособПоиска = 1 Тогда
				Если ЭлементОтбора.ЛевоеЗначение = ЗначениеПоиска Тогда
					МассивЭлементов.Добавить(ЭлементОтбора);
				КонецЕсли;
			ИначеЕсли СпособПоиска = 2 Тогда
				Если ЭлементОтбора.Представление = ЗначениеПоиска Тогда
					МассивЭлементов.Добавить(ЭлементОтбора);
				КонецЕсли;
			КонецЕсли;
		Иначе
			
			НайтиРекурсивно(ЭлементОтбора.Элементы, МассивЭлементов, СпособПоиска, ЗначениеПоиска);
			
			Если СпособПоиска = 2 И ЭлементОтбора.Представление = ЗначениеПоиска Тогда
				МассивЭлементов.Добавить(ЭлементОтбора);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Выполняет поиск элемента отбора в коллекции по заданному представлению.
//
// Параметры:
//  КоллекцияЭлементов - КоллекцияЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора,
//                                                                  например, Список.Отбор.Элементы или группа в отборе.
//  Представление - Строка - представление группы.
// 
// Возвращаемое значение:
//  ЭлементОтбораКомпоновкиДанных - элемент отбора.
//
Функция НайтиЭлементОтбораПоПредставлению(КоллекцияЭлементов, Представление) Экспорт
	
	ВозвращаемоеЗначение = Неопределено;
	
	Для каждого ЭлементОтбора Из КоллекцияЭлементов Цикл
		Если ЭлементОтбора.Представление = Представление Тогда
			ВозвращаемоеЗначение = ЭлементОтбора;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение
	
КонецФункции

#КонецОбласти

#КонецОбласти
```

</details>


7.5. Создать обший модуль	др_ПроверкиКорректностиДанных	(ПроцедурыФормированияЗапросовПроверкиКорректностиЗаполненияДокументов)							

<details>

<summary>Просмотр кода</summary>

### Код обшего модуля

```
//1729 Задача 7.5. 

#Область ПрограммныйИнтерфейс

Процедура ПроверитьКорректностьЗаполненияДокумента(ДокументОбъект, Отказ = Ложь) Экспорт
	
	// Заполним массив необходимых проверок в зависимости от типа документа
	МассивПроверок = Новый Массив();
	ИмяТаблицы     = ДокументОбъект.Метаданные().ПолноеИмя();
	
	Если ИмяТаблицы = "Документ.др_РеализацияЯрлыков" Тогда
		
		МассивПроверок.Добавить("ТаблицаЯрлыки");
		МассивПроверок.Добавить("КорректностьЗаполненияСкладов");
		МассивПроверок.Добавить("ДублиЯрлыков");
		
	ИначеЕсли ИмяТаблицы = "Документ.др_ПриобретениеЯрлыков" Тогда
		
		МассивПроверок.Добавить("ТаблицаЯрлыки");
		МассивПроверок.Добавить("КорректностьЗаполненияСкладов");
		
	КонецЕсли;
	
	// Сформируем текст запроса необходимых проверок в соответствие с массивом проверок
	ТекстЗапроса     = "";
	ПараметрыЗапроса = Новый Структура;
	
	ПараметрыЗапроса.Вставить("Дата",   ДокументОбъект.Дата);
	ПараметрыЗапроса.Вставить("Ссылка", ДокументОбъект.Ссылка);
	
	Для Каждого ИмяПроверки Из МассивПроверок Цикл
		
		Если ИмяПроверки = "ТаблицаЯрлыки" Тогда
			
			СформироватьЗапросТаблицаЯрлыки(ТекстЗапроса, ПараметрыЗапроса, ДокументОбъект, ИмяТаблицы);
			
		ИначеЕсли ИмяПроверки = "КорректностьЗаполненияСкладов" Тогда
			
			СформироватьЗапросКорректностьЗаполненияСкладов(ТекстЗапроса);
			
		ИначеЕсли ИмяПроверки = "ДублиЯрлыков" Тогда
			
			СформироватьЗапросДублиТоваров(ТекстЗапроса);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ТекстЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Для Каждого ПараметрЗапроса Из ПараметрыЗапроса Цикл
		Запрос.УстановитьПараметр(ПараметрЗапроса.Ключ, ПараметрЗапроса.Значение);
	КонецЦикла;
	
	РезультатПакета = Запрос.ВыполнитьПакет();
	
	// Сообщим пользователю о результатах проверки для каждого результата запроса
	Для ИндексПроверки = 0 По РезультатПакета.Количество()-1 Цикл
		
		Выборка = РезультатПакета[ИндексПроверки].Выбрать();
		ИмяПроверки = МассивПроверок[ИндексПроверки];
		
		Если ИмяПроверки = "КорректностьЗаполненияСкладов" Тогда
			
			СообщитьОбОшибкахКорректностьЗаполненияСкладов(Выборка, ДокументОбъект, Отказ);
			
		ИначеЕсли ИмяПроверки = "ДублиЯрлыков" Тогда
			
			СообщитьОбОшибкахДублиЯрлыков(Выборка, ДокументОбъект, Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроцедурыФормированияЗапросовПроверкиКорректностиЗаполненияДокументов

Процедура СформироватьЗапросТаблицаЯрлыки(ТекстЗапроса, ПараметрыЗапроса, ДокументОбъект, ИмяТаблицы)
	
	Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовВПакете();
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + 
	"ВЫБРАТЬ
	|	Ярлыки.НомерСтроки КАК НомерСтроки,
	|	Ярлыки.Склад КАК Склад,
	|	Ярлыки.др_Ярлык КАК др_Ярлык
	|ПОМЕСТИТЬ Ярлыки
	|ИЗ
	|	&Ярлыки КАК Ярлыки";
	
	ПараметрыЗапроса.Вставить("Ярлыки", ДокументОбъект.Ярлыки.Выгрузить(, "НомерСтроки, Склад, др_Ярлык"));
	
КонецПроцедуры

Процедура СформироватьЗапросКорректностьЗаполненияСкладов(ТекстЗапроса)
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовВПакете() + 
	"ВЫБРАТЬ
	|	Ярлыки.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Ярлыки КАК Ярлыки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.др_Ярлыки КАК СправочникДр_Ярлыки
	|		ПО Ярлыки.др_Ярлык = СправочникДр_Ярлыки.Ссылка
	|ГДЕ
	|	СправочникДр_Ярлыки.Тип = ЗНАЧЕНИЕ(Перечисление.др_ТипыЯрлыков.Платный)
	|	И Ярлыки.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)";
	
КонецПроцедуры

Процедура СформироватьЗапросДублиТоваров(ТекстЗапроса)
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовВПакете() + 
	"ВЫБРАТЬ
	|	МАКСИМУМ(Ярлыки.НомерСтроки) КАК НомерСтроки,
	|	ПРЕДСТАВЛЕНИЕ(Ярлыки.др_Ярлык) КАК ЯрлыкПредставление,
	|	Ярлыки.Склад КАК Склад,
	|	ПРЕДСТАВЛЕНИЕ(Ярлыки.Склад) КАК СкладПредставление
	|ИЗ
	|	Ярлыки КАК Ярлыки
	|
	|СГРУППИРОВАТЬ ПО
	|	Ярлыки.Склад,
	|	Ярлыки.др_Ярлык
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(*) > 1";
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыДляВыводаСообщенийОбОшибкахЗаполненияДокументов

Процедура СообщитьОбОшибкахКорректностьЗаполненияСкладов(Выборка, ДокументОбъект, Отказ)
	
	Пока Выборка.Следующий() Цикл
		
		ТекстОшибки = СтрШаблон("Не заполнена колонка ""Склад"" в строке %1 списка ""Ярлыки""",
			Выборка.НомерСтроки);
		
		ОбщегоНазначения.СообщитьПользователю(
			ТекстОшибки,
			ДокументОбъект,
			ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Ярлыки", Выборка.НомерСтроки, "Склад"),,
			Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СообщитьОбОшибкахДублиЯрлыков(Выборка, ДокументОбъект, Отказ)
	
	Пока Выборка.Следующий() Цикл
		
		
		Если ЗначениеЗаполнено(Выборка.Склад) Тогда
			ТекстОшибки = СтрШаблон("Ярлык ""%1"" со складом ""%2"" повторяется",
				Выборка.ЯрлыкПредставление,
				Выборка.СкладПредставление);
		Иначе
			ТекстОшибки = СтрШаблон("Ярлык ""%1"" повторяется",
				Выборка.ЯрлыкПредставление);
		КонецЕсли;
		
		ОбщегоНазначения.СообщитьПользователю(
		ТекстОшибки,
		ДокументОбъект,
		ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Ярлыки", Выборка.НомерСтроки, "Номенклатура"),
		,
		Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
```

</details>

7.6. Создать регистр накопления др_ЯрлыкиНаСкладах								

<details>

<summary>Структура метаданных регистра др_ЯрлыкиНаСкладах</summary>

### Структура метаданных регистра др_ЯрлыкиНаСкладах

```
//1729 Задача 7.6. 
др_ЯрлыкиНаСкладах
    Измерения
    - Ярлык
    - Склад
    Ресурсы
    - Количество

```

</details>


8. Создать документ др_РеализацияЯрлыков (реквизит Организация, таб часть Ярлыки (реквизиты таб части 
др_Ярлык, Склад, Количество, Подразделение)

8.1. Настроить интерактивно проверку заполнения для др_Ярлык и Склад

8.2. Настроить программно проверку заполнения для Склад (если Тип ярлыка бесплатный не проверять)

<details>

<summary>Просмотр кода</summary>

### Код формы

```

//1729 Задача 8.2. 

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриЧтенииСозданииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	ЗаполнитьДанныеФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаполнитьДанныеФормы();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если НеВыполнятьПроверкуОбъекта Тогда
		НепроверяемыеРеквизиты.Добавить("Объект");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте             
Процедура ЯрлыкиДр_ЯрлыкПриИзменении(Элемент)
	
	СтрокаЯрлыков = Элементы.Ярлыки.ТекущиеДанные;
	
	ЯрлыкДокумента = Новый Массив;
	ЯрлыкДокумента.Добавить(СтрокаЯрлыков.др_Ярлык);
	
	ТипыЯрлыка = ПолучитьТипыЯрлыка(ЯрлыкДокумента);
	СтрокаЯрлыков.др_ТипЯрлыка = ТипыЯрлыка[СтрокаЯрлыков.др_Ярлык];
	
КонецПроцедуры


&НаКлиенте
Процедура ЯрлыкиСкладПриИзменении(Элемент)
	СтрокаТоваров = Элементы.Ярлыки.ТекущиеДанные;
	СтрокаТоваров.Подразделение = ПодразделениеСклада(СтрокаТоваров.Склад);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДанныеФормы()
	ЯрлыкДокумента = ПолучитьЯрлыкДокумента();
	ТипыЯрлыка = ПолучитьТипыЯрлыка(ЯрлыкДокумента);
	
	Для Каждого СтрокаЯрлыков Из Объект.Ярлыки Цикл
		СтрокаЯрлыков.др_ТипЯрлыка = ТипыЯрлыка[СтрокаЯрлыков.др_Ярлык];
	КонецЦикла;

	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	др_ОбщегоНазначенияКлиентСервер.ДобавитьПростойЭлементУсловногоОформления(ЭтаФорма,
		"ЯрлыкиСклад", "Видимость", Ложь, 
		"Объект.Ярлыки.др_ТипЯрлыка", ВидСравненияКомпоновкиДанных.Равно, Перечисления.др_ТипыЯрлыков.БесПлатный);

	// На неравенство, чтобы не показывать при пустом ярлыке
	др_ОбщегоНазначенияКлиентСервер.ДобавитьПростойЭлементУсловногоОформления(ЭтаФорма,
		"ЯрлыкиПодразделение", "Видимость", Ложь, 
		"Объект.Ярлыки.др_ТипЯрлыка", ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.др_ТипыЯрлыков.БесПлатный);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьЯрлыкДокумента()
	
	ВозвращаемоеЗначение = Новый Массив;
	
	Для Каждого СтрокаЯрлыков Из Объект.Ярлыки Цикл
		ВозвращаемоеЗначение.Добавить(СтрокаЯрлыков.др_Ярлык);
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции 


&НаСервереБезКонтекста
Функция ПолучитьТипыЯрлыка(МассивЯрлыка)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивЯрлыка, "Тип");
КонецФункции

&НаСервереБезКонтекста
Функция ПодразделениеСклада(Склад)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Склад, "Подразделение");
КонецФункции

#КонецОбласти


```

</details>


8.3. Настроить Связи параметров выбора реквизита таб части др_Ярлык отбор по Организации)

8.4. Создать регистр накопления др_Продажи

<details>

<summary>Структура метаданных регистра др_Продажи</summary>

### Структура метаданных регистра др_Продажи

```
//1729 Задача 8.4. 
др_Продажи
    Измерения
    - Ярлык
    - Подразделение
    Ресурсы
    - Количество

```

</details>

9. Создать обработку др_ФормированиеРеализаций для автоматического создания др_РеализацияЯрлыков

9.1. Создать форму

<details>

<summary>Просмотр кода</summary>

### Код формы  Имя команды СоздатьДокументы

```
 	
//1729 Задача 9.1. 


#Область ОбработчикиСобытий

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	МассивНепроверяемыхРеквизитов.Добавить("ОсновнойРеквизитФормы");
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Остатки, "Организация",
		Организация, ВидСравненияКомпоновкиДанных.Равно, "ОтборОрганизация", ЗначениеЗаполнено(Организация));
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Остатки, "Подразделение",
		Подразделение, ВидСравненияКомпоновкиДанных.Равно, "ОтборПодразделение", ЗначениеЗаполнено(Подразделение));
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьДокументы(Команда)
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьДокументыНаСервере(Элементы.Остатки.ВыделенныеСтроки);
	
КонецПроцедуры

&НаСервере
Функция СоздатьДокументыНаСервере(Знач ДанныеСтрок)
	
	Результат = Новый Массив;
	
	// Проверяем возможность запуска:
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат Результат;
	КонецЕсли;
	
	// Готовим данные:
	ТаблицаОтборов = Обработки.др_ФормированиеРеализаций.ТаблицаОтборов();
	Для Каждого ДанныеСтроки Из ДанныеСтрок Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаОтборов.Добавить(), ДанныеСтроки);
	КонецЦикла;
	
	// Вызываем основной интерфейс:
	Результат = Обработки.др_ФормированиеРеализаций.СоздатьДокументы(ТаблицаОтборов);
	
	// Собираем информацию о результате:
	ПроведенныеДокументы = Новый Массив;
	Для Каждого ДокументОбъект Из Результат Цикл
		Если ДокументОбъект.Проведен Тогда
			ПроведенныеДокументы.Добавить(ДокументОбъект);
		КонецЕсли;
	КонецЦикла;

	Элементы.Остатки.Обновить(); // Перечитываем остатки

КонецФункции

#КонецОбласти

```

</details>

9.2. Создать Модуль менеджера

<details>

<summary>Модуль менеджера</summary>

### Код Создать Модуль менеджера

```
 	
//1729 Задача 9.2. Создать Модуль менеджера



#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ТаблицаОтборов() Экспорт
	
	ТаблицаОтборов = Новый ТаблицаЗначений;
	ТаблицаОтборов.Колонки.Добавить("Ярлык", 		Новый ОписаниеТипов("СправочникСсылка.др_Ярлыки"));
	ТаблицаОтборов.Колонки.Добавить("Склад",        Новый ОписаниеТипов("СправочникСсылка.Склады"));
	
	Возврат ТаблицаОтборов;
	
КонецФункции

Функция СоздатьДокументы(ТаблицаОтборов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаОтборов.Ярлык КАК Ярлык,
	|	ТаблицаОтборов.Склад КАК Склад
	|ПОМЕСТИТЬ ТаблицаОтборов
	|ИЗ
	|	&ТаблицаОтборов КАК ТаблицаОтборов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	др_ЯрлыкиНаСкладахОстатки.Ярлык.Организация КАК Организация,
	|	др_ЯрлыкиНаСкладахОстатки.Ярлык КАК др_Ярлык,
	|	др_ЯрлыкиНаСкладахОстатки.Склад КАК Склад,
	|	др_ЯрлыкиНаСкладахОстатки.КоличествоОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.др_ЯрлыкиНаСкладах.Остатки(
	|			,
	|			(Ярлык, Склад) В
	|				(ВЫБРАТЬ
	|					ТаблицаОтборов.Ярлык,
	|					ТаблицаОтборов.Склад
	|				ИЗ
	|					ТаблицаОтборов)) КАК др_ЯрлыкиНаСкладахОстатки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация";
	
	Запрос.УстановитьПараметр("ТаблицаОтборов", ТаблицаОтборов);
	ТаблицаОстатков = Запрос.Выполнить().Выгрузить();
	МассивТаблиц = ОбеспечениеСервер.РазбитьТаблицуПоЗначениюКлюча(ТаблицаОстатков, "Организация");
	
	Результат = Новый Массив;
	
	Для Каждого Элемент Из МассивТаблиц Цикл
		ДанныеЗаполнения = Элемент.Ключ;
		ДанныеЗаполнения.Вставить("Ярлыки", Элемент.Таблица); 
		
		Документ = ЗаполнитьИПровестиДокумент(ДанныеЗаполнения);
		Результат.Добавить(Документ);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет и проводит документ созданный обработкой.
//
Функция ЗаполнитьИПровестиДокумент(ДанныеЗаполнения)
	
	ДокументОбъект = Документы.др_РеализацияЯрлыков.СоздатьДокумент();
	ДанныеЗаполнения.Вставить("Дата", ТекущаяДатаСеанса());
	
	ДокументОбъект.Заполнить(ДанныеЗаполнения);
	
	ЗаписатьДокумент(ДокументОбъект);
	
	Возврат ДокументОбъект;
	
КонецФункции

Процедура ЗаписатьДокумент(ДокументОбъект)
	
	ОшибокНеОбнаружено = ДокументОбъект.ПроверитьЗаполнение();
	
	Если Не ОшибокНеОбнаружено Тогда
		
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		
	Иначе //нет ошибок, проводим документ
		
		Попытка
			
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			
		Исключение
			
			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
			ЗаписьЖурналаРегистрации("Создание реализаций", УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
```

</details>

9.3. Добавить код в модуль др_РеализацияЯрлыков

<details>

<summary>Просмотр кода</summary>

### Код модуля

```
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
```

<details>

<summary>Просмотр кода Область ОбработчикиСобытий</summary>

### Код модуля Процедура ОбработкаЗаполнения

```
#Область ОбработчикиСобытий


Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьДокументПоОтбору(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры



#КонецОбласти
```

</details>

<details>

<summary>Просмотр кода Область СлужебныеПроцедурыИФункции</summary>

### Код модуля Область СлужебныеПроцедурыИФункции


```
#Область СлужебныеПроцедурыИФункции


#Область ИнициализацияИЗаполнение

Процедура ЗаполнитьДокументПоОтбору(Знач ДанныеЗаполнения)
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	
	Если ДанныеЗаполнения.Свойство("Ярлыки") Тогда
		ЭтотОбъект.Ярлыки.Загрузить(ДанныеЗаполнения.Ярлыки);
		ЗаполнитьПодразделенияСкладов();
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПодразделенияСкладов()
	
	ПодразделенияСкладов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Ярлыки.ВыгрузитьКолонку("Склад"), "Подразделение");
	Для Каждого СтрокаЯрлыков Из Ярлыки Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаЯрлыков.Склад) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаЯрлыков.Подразделение = ПодразделенияСкладов[СтрокаЯрлыков.Склад];
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли

```
</details>

</details>



10. Синхронизация с бухгалтерией ДокументСсылка.др_ПриобретениеЯрлыков.

10.1. Изменить состав плана обмена

10.1.1. Добавить Справочник др_Ярлыки

10.1.2. Добавить документ др_ПриобретениеЯрлыков

10.2. Изменить Подписки на события

10.2.1. Справочник др_Ярлыки для СинхронизацияДанныхЧерезУниверсальныйФорматРегистрация

10.2.2. Документ др_ПриобретениеЯрлыков для	СинхронизацияДанныхЧерезУниверсальныйФорматРегистрацияДокумента

10.3. Добавить 	XDTO-пакет (YR3)

<details>

<summary>Структура пакета</summary>

### Структура пакета

```
//1729 Задача 10.3. 
http://v8.1c.ru/edi/edi_stnd/yr3/1.1 
    Директивы импорта
    - http://www.1c.ru/SSL/Exchange/Message
    - http://v8.1c.ru/edi/edi_stnd/EnterpriseData/1.15
    Типы значений
    - ДокументСсылка.др_ПриобретениеЯрлыков 	(Ref (http://www.1c.ru/SSL/Exchange/Message))
	- СправочникСсылка.др_Ярлыки 				(Ref (http://www.1c.ru/SSL/Exchange/Message))	
	- др_ТипыЯрлыков							(string (http://www.w3.org/2001/XMLSchema))	
		- Перечисление
			- Платный
			- Бесплатный
	Типы объектов
	- КлючевыеСвойствадр_ПриобретениеЯрлыков
		- Ссылка								(ДокументСсылка.др_ПриобретениеЯрлыков (http://v8.1c.ru/edi/edi_stnd/yr3/1.1))
		- Дата									(dateTime (http://www.w3.org/2001/XMLSchema))	
		- Номер									(ТипНомерДокумента (http://v8.1c.ru/edi/edi_stnd/EnterpriseData/1.15))
		- Организация							(КлючевыеСвойстваОрганизация (http://v8.1c.ru/edi/edi_stnd/EnterpriseData/1.15))
	- КлючевыеСвойствадр_Ярлыки	
		- Ссылка								(СправочникСсылка.др_Ярлыки (http://v8.1c.ru/edi/edi_stnd/yr3/1.1))
		- Наименование							(string (http://www.w3.org/2001/XMLSchema))
		- Организация							(КлючевыеСвойстваОрганизация (http://v8.1c.ru/edi/edi_stnd/EnterpriseData/1.15))	
		- Тип									(др_ТипыЯрлыков (http://v8.1c.ru/edi/edi_stnd/yr3/1.1))
	- Документ.др_ПриобретениеЯрлыков
		- КлючевыеСвойства						(КлючевыеСвойствадр_ПриобретениеЯрлыков (http://v8.1c.ru/edi/edi_stnd/yr3/1.1))
		- Ярлыки								(Документ.др_ПриобретениеЯрлыков.Ярлыки (http://v8.1c.ru/edi/edi_stnd/yr3/1.1))
	- Документ.др_ПриобретениеЯрлыков.Ярлыки
		- Строка								(Документ.др_ПриобретениеЯрлыков.Ярлыки.Строка (http://v8.1c.ru/edi/edi_stnd/yr3/1.1))
	- Документ.др_ПриобретениеЯрлыков.Ярлыки.Строка
		- др_Ярлык								(КлючевыеСвойствадр_Ярлыки)
		- Склад									(КлючевыеСвойстваСклад (http://v8.1c.ru/edi/edi_stnd/EnterpriseData/1.15))
		- Количество							(decimal (http://www.w3.org/2001/XMLSchema))

```

</details>


10.4. _

10.5. Изменить 	МенеджерОбменаЧерезУниверсальныйФормат	

Используется КД3

<details>

<summary>Процедура ЗаполнитьПравилаОбработкиДанных(НаправлениеОбмена, ПравилаОбработкиДанных)</summary>

### Измененный код ЗаполнитьПравилаОбработкиДанных

```
//1729 Задача 10.5. 
	Если НаправлениеОбмена = "Отправка" Тогда
...
	//1729++Выгрузить Ярлыки	
		ДобавитьПОД_Документ_др_ПриобретениеЯрлыков_Отправка(ПравилаОбработкиДанных);
		ДобавитьПОД_Справочник_др_Ярлыки_Отправка(ПравилаОбработкиДанных);
	//1729--Выгрузить Ярлыки	

...	


```

</details>


<details>

<summary>Обработчики Ярлыки</summary>

### Добавленный код 

```
//1729 Задача 10.5. 

...
//1729 ++Выгрузить Ярлыки	
#Область Документы_Ярлыки    
Процедура ДобавитьПОД_Документ_др_ПриобретениеЯрлыков_Отправка(ПравилаОбработкиДанных)
	
	ПравилоОбработки                         = ПравилаОбработкиДанных.Добавить();
	ПравилоОбработки.Имя                     = "Документ_др_ПриобретениеЯрлыков_Отправка";
	ПравилоОбработки.ОбъектВыборкиМетаданные = Метаданные.Документы.др_ПриобретениеЯрлыков;
	ПравилоОбработки.ОчисткаДанных           = Ложь;

	ПравилоОбработки.ИспользуемыеПКО.Добавить("ДокументСсылка_др_ПриобретениеЯрлыков_Отправка");
	
КонецПроцедуры

Процедура ДобавитьПКО_ДокументСсылка_др_ПриобретениеЯрлыков_Отправка(ПравилаКонвертации)
	
	ПравилоКонвертации = ОбменДаннымиXDTOСервер.ИнициализироватьПравилоКонвертацииОбъекта(ПравилаКонвертации);
	ПравилоКонвертации.ИмяПКО            = "ДокументСсылка_др_ПриобретениеЯрлыков_Отправка";
	ПравилоКонвертации.ОбъектДанных      = Метаданные.Документы.др_ПриобретениеЯрлыков;
	ПравилоКонвертации.ОбъектФормата     = "Документ.ПоступлениеТоваровУслуг";
	ПравилоКонвертации.ПриОтправкеДанных = "ПКО_ДокументСсылка_др_ПриобретениеЯрлыков_Отправка_ПриОтправкеДанных";
	СвойстваШапки = ПравилоКонвертации.Свойства;
	ДобавитьПКС(СвойстваШапки, "",            "Валюта", 1);
	ДобавитьПКС(СвойстваШапки, "",            "ВидОперации", 1);
	ДобавитьПКС(СвойстваШапки, "Дата",        "Дата");
	ДобавитьПКС(СвойстваШапки, "",            "Контрагент", 1);
	ДобавитьПКС(СвойстваШапки, "Номер",       "Номер");
	ДобавитьПКС(СвойстваШапки, "Организация", "Организация", , "Справочник_Организации_Отправка");
	
	СвойстваТЧ = ДобавитьПКТЧ(ПравилоКонвертации, "", "Товары");
	ДобавитьПКС(СвойстваТЧ, "", "ЕдиницаИзмерения", 1);
	ДобавитьПКС(СвойстваТЧ, "", "Количество", 1);
	ДобавитьПКС(СвойстваТЧ, "", "Номенклатура", 1, "Справочник_др_Ярлыки_Отправка");

	
КонецПроцедуры

#Область ОбработчикиКонвертации
#Область Справочники
Процедура ПОД_Справочник_др_Ярлыки_Отправка_ПриОбработке(ДанныеИБ, ИспользованиеПКО, КомпонентыОбмена)
	Если ДанныеИБ.Тип = Перечисления.др_ТипыЯрлыков.Бесплатный Тогда
		     ИспользованиеПКО.Справочник_др_Ярлыки_Отправка = ложь;
		КонецЕсли;
КонецПроцедуры
Процедура ПКО_Справочник_др_Ярлыки_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	ЕИ = Новый Структура("Код,Наименование","796","шт");
	ДанныеXDTO.Вставить("ЕдиницаИзмерения",ЕИ);
	ДанныеXDTO.Вставить("ТипНоменклатуры","Товар");  
	ГА = Новый Структура("Наименование","Товары");
	ДанныеXDTO.Вставить("ГруппаАналитическогоУчета",ГА);  
	
	//Группа
	Группа = Новый Структура("Наименование", "Товары");
	ДанныеXDTO.КлючевыеСвойства.Вставить("Группа", Группа);
КонецПроцедуры

#КонецОбласти
#Область Документы
Процедура ПКО_ДокументСсылка_др_ПриобретениеЯрлыков_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	//Выгрузить товары
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	др_ПриобретениеЯрлыковЯрлыки.др_Ярлык КАК Номенклатура,
	|	др_ПриобретениеЯрлыковЯрлыки.Количество КАК Количество
	|ИЗ
	|	Документ.др_ПриобретениеЯрлыков.Ярлыки КАК др_ПриобретениеЯрлыковЯрлыки
	|ГДЕ
	|	др_ПриобретениеЯрлыковЯрлыки.Ссылка = &Ссылка
	|	И др_ПриобретениеЯрлыковЯрлыки.др_Ярлык.Тип = &Тип";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.Ссылка);
	Запрос.УстановитьПараметр("Тип", Перечисления.др_ТипыЯрлыков.Платный);
	
	Товары = Запрос.Выполнить().Выгрузить();                  
	ЕИ = Новый Структура("Код,Наименование","796","шт");
	Товары.Колонки.Добавить("ЕдиницаИзмерения", Новый ОписаниеТипов("Структура"));
	Товары.ЗаполнитьЗначения(ЕИ , "ЕдиницаИзмерения");
	
	
	ДанныеXDTO.Вставить("Товары", Товары);
	
	//выгрузить обязательные поля формата
	ДанныеXDTO.КлючевыеСвойства.Вставить("ДанныеВходящегоДокумента", "");
	ДанныеXDTO.Вставить("ДатаВходящегоДокумента", дата(1,1,1)); 
	ДанныеXDTO.Вставить("НаименованиеВходящегоДокумента", "");
	ДанныеXDTO.Вставить("НомерВходящегоДокумента", "");
	
	ДанныеXDTO.Вставить("ВидОперации", "ПокупкаУПоставщика");                                                                                      
	
	Валюта = Новый Структура("Код,Наименование", "643", "Рубль");
	ДанныеXDTO.Вставить("Валюта", Валюта);                                                                                      
	
	ДанныеXDTO.Вставить("ВидДоговора", "СПоставщиком");                                                                                      
	
	Контрагент = Новый Структура("Наименование,ЮридическоеФизическоеЛицо", "Служебный","ЮридическоеЛицо");
	ДанныеXDTO.КлючевыеСвойства.Вставить("Контрагент", Контрагент);
	
	Договор = Новый Структура("Наименование,ВидДоговора,Организация,Контрагент", "ОсновнойСлужебный","СПоставщиком",ДанныеXDTO.КлючевыеСвойства.Организация,Контрагент);
	ДанныеВзаиморасчетов = Новый Структура("Договор,ВалютаВзаиморасчетов,КурсВзаиморасчетов,КратностьВзаиморасчетов,РасчетыВУсловныхЕдиницах,СчетУчетаРасчетовСКонтрагентом,СчетУчетаРасчетовПоАвансам",
	Договор , Валюта, 		       1.00,                   1,                     ложь,                    "",                            "" );
	ДанныеXDTO.Вставить("ДанныеВзаиморасчетов", ДанныеВзаиморасчетов);
КонецПроцедуры

#КонецОбласти

#КонецОбласти


#КонецОбласти



#Область Справочники
Процедура ДобавитьПОД_Справочник_др_Ярлыки_Отправка(ПравилаОбработкиДанных)
	
	ПравилоОбработки                         = ПравилаОбработкиДанных.Добавить();
	ПравилоОбработки.Имя                     = "Справочник_др_Ярлыки_Отправка";
	ПравилоОбработки.ОбъектВыборкиМетаданные = Метаданные.Справочники.др_Ярлыки;
	ПравилоОбработки.ПриОбработке            = "ПОД_Справочник_др_Ярлыки_Отправка_ПриОбработке";
	ПравилоОбработки.ОчисткаДанных           = Ложь;

	ПравилоОбработки.ИспользуемыеПКО.Добавить("Справочник_др_Ярлыки_Отправка");
	
КонецПроцедуры

Процедура ДобавитьПКО_Справочник_др_Ярлыки_Отправка(ПравилаКонвертации)
	
	ПравилоКонвертации = ОбменДаннымиXDTOСервер.ИнициализироватьПравилоКонвертацииОбъекта(ПравилаКонвертации);
	ПравилоКонвертации.ИмяПКО            = "Справочник_др_Ярлыки_Отправка";
	ПравилоКонвертации.ОбъектДанных      = Метаданные.Справочники.др_Ярлыки;

	ПравилоКонвертации.ОбъектФормата     = "Справочник.Номенклатура";
	ПравилоКонвертации.ПриОтправкеДанных = "ПКО_Справочник_др_Ярлыки_Отправка_ПриОтправкеДанных";
	СвойстваШапки = ПравилоКонвертации.Свойства;
	ДобавитьПКС(СвойстваШапки, "",             "Группа", 1);
	ДобавитьПКС(СвойстваШапки, "",             "ГруппаАналитическогоУчета", 1);
	ДобавитьПКС(СвойстваШапки, "",             "ЕдиницаИзмерения", 1);
	ДобавитьПКС(СвойстваШапки, "Код",          "КодВПрограмме");
	ДобавитьПКС(СвойстваШапки, "Наименование", "Наименование");
	ДобавитьПКС(СвойстваШапки, "",             "ТипНоменклатуры", 1);

	
КонецПроцедуры
//1729--Выгрузить Ярлыки	


#КонецОбласти




#КонецОбласти

...	


```

</details>

11. Сделать  "типовое" проведение др_РеализацияЯрлыков в ERP

11.1.  Процедура ПровестиДокумент(Документ, Отказ, ДопПараметры) общего модуля ПроведениеДокументов

<details>

<summary>Просмотр кода ПровестиДокумент</summary>

### Просмотр кода

```
 	
//1729 Задача 11.1. 
Процедура ПровестиДокумент(Документ, Отказ, ДопПараметры)
	
	УстановитьПривилегированныйРежим(Истина); 
 	//++11.1. Свойства мы определяли в модуле объекта документа в процедуре ПередЗаписью
	Свойства = СвойстваДокумента(Документ);
	МенеджерДокумента = Документы[Документ.Метаданные().Имя];
	ПроведениеДокументовЛокализация.ПереназначитьМодульПолученияДанныхДокумента(МенеджерДокумента);
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.Сторно") Тогда
		// Список механизмов получим из сторнируемого документа
		МенеджерСторнируемогоДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Документ.СторнируемыйДокумент);
		МеханизмыДокумента    = УчетныеМеханизмыДокумента(МенеджерСторнируемогоДокумента);
	Иначе
	//++11.1. Учетные механизмы документы определены в модуле менеджера в функции ЗарегистрироватьУчетныеМеханизмы		
		МеханизмыДокумента    = УчетныеМеханизмыДокумента(МенеджерДокумента);
	КонецЕсли;
	  //++11.1. В учетных механизмах конфигурации мы добавили новый механизм "УчетЯрлыков"
	МеханизмыКонфигурации = УчетныеМеханизмыКонфигурации();
	//++11.1. Получение полного списка регистров из всех учетных механизмов документа
	РегистрыКРегистрации = Новый Массив;
	ДвижимыеРегистры     = Новый Структура;
	Механизмы            = Новый СписокЗначений;
	Для каждого Механизм Из МеханизмыДокумента Цикл
		МодульМеханизма = ОбщегоНазначения.ОбщийМодуль(МеханизмыКонфигурации[Механизм]);
		
		//11.1. см. ПроведениеДокументов.ПараметрыУчетногоМеханизма
		ПараметрыМеханизма = МодульМеханизма.ПараметрыДляПроведенияДокумента(Документ, Свойства); // см. ПроведениеДокументов.ПараметрыУчетногоМеханизма
		ПараметрыМеханизма.Вставить("МодульМеханизма", МодульМеханизма);
		
		Если Свойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(РегистрыКРегистрации, ПараметрыМеханизма.ПодчиненныеРегистры);
			
			Для каждого Регистр Из ПараметрыМеханизма.ПодчиненныеРегистры Цикл
				ДвижимыеРегистры.Вставить(Регистр.Имя);
			КонецЦикла;
			
			Для каждого Регистр Из ПараметрыМеханизма.НезависимыеРегистры Цикл
				ДвижимыеРегистры.Вставить(Регистр.Имя);
			КонецЦикла;
		КонецЕсли;
		
		Механизмы.Добавить(ПараметрыМеханизма, Механизм);
	КонецЦикла;
	Механизмы.СортироватьПоПредставлению();
	
	ЗаписываемыеРегистры = ПодготовитьНаборыЗаписейКРегистрацииДвижений(Документ, Свойства, РегистрыКРегистрации);
	
	Если Свойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ТаблицыДляДвижений = ТаблицыДляДвижений(Документ, МенеджерДокумента, ДвижимыеРегистры, ДопПараметры);
		ОтразитьДвиженияПодчиненныхРегистров(Механизмы, Документ, ТаблицыДляДвижений, Отказ);
	КонецЕсли;
	
	МенеджерВременныхТаблиц = МенеджерВременныхТаблицКонтроля(Документ);
	УстановитьДопСвойстваКонтрольныхРегистров(Механизмы, Документ, МенеджерВременныхТаблиц);
	
	Если Свойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ЗаписатьДвиженияНезависимыхРегистров(Механизмы, Документ, ТаблицыДляДвижений, МенеджерВременныхТаблиц, Отказ);
	КонецЕсли;
	
	ОбработатьДокументПередЗаписьюДвижений(Механизмы, Документ, МенеджерВременныхТаблиц, Отказ);

	Документ.Движения.Записать();
	
	ОбработатьДокументПослеЗаписиДвижений(Механизмы, Документ, МенеджерВременныхТаблиц, Отказ);
	ПроверитьДатуЗапретаПоТаблицеИзменений(Механизмы, Документ, МенеджерВременныхТаблиц, Отказ);
	ВыполнитьКонтрольРезультатовПроведения(Механизмы, Документ, МенеджерВременныхТаблиц, Отказ);
	
	Если Не Отказ Тогда
		СформироватьЗаданияНаОтложенныеДвижения(Документ, МенеджерВременныхТаблиц);
		ОтметитьВыполнениеОбработкиОбновленияПодчиненныхРегистров(ЗаписываемыеРегистры, Документ);
	КонецЕсли;
	
	МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры

```

</details>

11.2.  Функция УчетныеМеханизмыКонфигурации() общего модуля ПроведениеДокументов

<details>

<summary>Просмотр кода УчетныеМеханизмыКонфигурации</summary>

### Просмотр кода

```

// Описывает учетные механизмы используемые в конфигурации.
//
// Возвращаемое значение:
// 	Структура Из КлючИЗначение - Где:
// 			* Ключ - Строка - Имя механизма
// 			* Значение - Строка - Имя модуля менеджера
Функция УчетныеМеханизмыКонфигурации() Экспорт
	
	МеханизмыКонфигурации = Новый Структура;

  ... 
  
	//++ 1729 н11.2. см. 11.3.-->
    // Ключ - Имя механизма, Значение - модуль реализующий интерфейс проведения 
    МеханизмыКонфигурации.Вставить("др_УчетЯрлыков", "др_УчетЯрлыков");
    //-- 1729 <--
	
	ПроведениеДокументовЛокализация.ДополнитьУчетныеМеханизмыКонфигурации(МеханизмыКонфигурации);
	
	Возврат МеханизмыКонфигурации;
	
КонецФункции

```

</details>


11.3.  Создать ОбщийМодуль др_УчетЯрлыков

<details>

<summary>Просмотр кода др_УчетЯрлыков</summary>

### Просмотр кода

```
 // Формирует параметры для проведения документа по регистрам учетного механизма через общий механизм проведения.
//
// Параметры:
//  Документ - ДокументОбъект - записываемый документ
//  Свойства - ФиксированнаяСтруктура - свойства документа (См. ПроведениеДокументов.СвойстваДокумента).
//
// Возвращаемое значение:
//  Структура - параметры учетного механизма (См. ПроведениеДокументов.ПараметрыУчетногоМеханизма()).
//
Функция ПараметрыДляПроведенияДокумента(Документ, Свойства) Экспорт
	
	Параметры = ПроведениеДокументов.ПараметрыУчетногоМеханизма();
	
	// Проведение
	Если Свойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.др_ЯрлыкиНаСкладах);
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.др_Продажи);
		
	КонецЕсли;
	
	// Контроль
	Если Свойства.РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
		
		Параметры.КонтрольныеРегистрыИзменений.Добавить(Метаданные.РегистрыНакопления.др_ЯрлыкиНаСкладах);

	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

// Процедура формирования движений по подчиненным регистрам
//
// Параметры:
//	ТаблицыДляДвижений - Структура - таблицы данных документа
//	Движения - КоллекцияДвижений - коллекция наборов записей движений документа
//	Отказ - Булево - признак отказа от проведения документа.
//
Процедура ОтразитьДвижения(ТаблицыДляДвижений, Движения, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения, "др_ЯрлыкиНаСкладах");
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения, "др_Продажи");
	
КонецПроцедуры

// Формирует тексты запросов для контроля изменений записанных движений регистров.
//
// Параметры:
//  Запрос - Запрос - запрос, хранящий параметры используемые в списке запросов
//  ТекстыЗапроса - СписокЗначений - список текстов запросов и их имен.
//  Документ - ДокументОбъект - записываемый документ.
//
Процедура ИнициализироватьДанныеКонтроляИзменений(Запрос, ТекстыЗапроса, Документ) Экспорт
	
	Если ПроведениеДокументов.ЕстьЗаписиВТаблице(Документ, "ДвиженияДр_ЯрлыкиНаСкладахИзменение") Тогда
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	ТаблицаОстатков.Склад КАК Склад,
			|	ТаблицаОстатков.Ярлык КАК Ярлык,
			|	ТаблицаОстатков.КоличествоОстаток КАК Количество
			|ИЗ
			|	РегистрНакопления.др_ЯрлыкиНаСкладах.Остатки(
			|			,
			|			(Ярлык, Склад) В
			|				(ВЫБРАТЬ
			|					Таблица.Ярлык КАК Ярлык,
			|					Таблица.Склад КАК Склад
			|				ИЗ
			|					ДвиженияДр_ЯрлыкиНаСкладахИзменение КАК Таблица)) КАК ТаблицаОстатков
			|ГДЕ
			|	ТаблицаОстатков.КоличествоОстаток < 0";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ОшибкиОстаткиДр_Ярлыков");
		
	КонецЕсли;
	
КонецПроцедуры

// Выводит сообщения пользователю при наличии ошибок контроля изменений записанных движений регистров.
//
// Параметры:
//  РезультатыКонтроля - Структура - таблицы с результатами контроля изменений
//  Документ - ДокументОбъект - записываемый документ
//  Отказ - Булево - признак отказа от проведения документа.
//
Процедура СообщитьОРезультатахКонтроляИзменений(РезультатыКонтроля, Документ, Отказ) Экспорт
	
	Если ПроведениеДокументов.ЕстьЗаписиВТаблице(Документ, "ДвиженияДр_ЯрлыкиНаСкладахИзменение") Тогда
		
		ШаблонСообщенияЯрлык = НСтр("ru = 'На складе: %1  недостаточно ярлыков: %2 в количестве %3'");
		
		Для каждого СтрокаОшибки Из РезультатыКонтроля.ОшибкиОстаткиДр_Ярлыков Цикл
			
			ПредставлениеЯрлык = СокрЛП(СтрокаОшибки.Ярлык);
			ПредставлениеСклад	 = СокрЛП(СтрокаОшибки.Склад);
			
			Если ЗначениеЗаполнено(СтрокаОшибки.Количество) Тогда
				ТекстСообщения = СтрШаблон(ШаблонСообщенияЯрлык , ПредставлениеСклад, ПредставлениеЯрлык, -СтрокаОшибки.Количество);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Документ,,, Отказ);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстыЗапросовКонтрольДатыЗапретаПоТаблицеИзменений(Запрос) Экспорт

	СоответствиеТекстовЗапросов = Новый Соответствие();
	Возврат СоответствиеТекстовЗапросов;
	
КонецФункции


```

</details>


11.4.  Модуль объекта документа др_РеализацияЯрлыков

<details>

<summary>Просмотр кода др_РеализацияЯрлыков</summary>

### Просмотр кода

```

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры



...

```

</details>

11.5.  Модуль менеджера документа др_РеализацияЯрлыков

<details>

<summary>Просмотр кода Модуль менеджера документа др_РеализацияЯрлыков</summary>

### Просмотр кода

```

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("др_УчетЯрлыков");
	//МеханизмыДокумента.Добавить("РеестрДокументов");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса

		ТекстЗапросаТаблицаДр_ЯрлыкиНаСкладах(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДр_Продажи(Запрос, ТекстыЗапроса, Регистры);
		////ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
		
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Номер КАК Номер,
	//|	ДанныеДокумента.Склад КАК Склад,
	|	ДанныеДокумента.Организация КАК Организация
	//|	ДанныеДокумента.Подразделение КАК Подразделение,
	//|	ДанныеДокумента.Ответственный КАК Ответственный,
	//|	ДанныеДокумента.Комментарий КАК Комментарий,
	//|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	//|	ДанныеДокумента.Партнер КАК Партнер
	|ИЗ
	|	Документ.др_РеализацияЯрлыков КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("ПометкаУдаления",       Реквизиты.ПометкаУдаления);
	Запрос.УстановитьПараметр("Проведен",              Реквизиты.Проведен);
	Запрос.УстановитьПараметр("Период",                Реквизиты.Период);
	Запрос.УстановитьПараметр("Номер",                 Реквизиты.Номер);
	Запрос.УстановитьПараметр("Организация",           Реквизиты.Организация);
	//Запрос.УстановитьПараметр("Склад",		           Реквизиты.Склад);
	//Запрос.УстановитьПараметр("Подразделение",         Реквизиты.Подразделение);
	//Запрос.УстановитьПараметр("Ответственный",         Реквизиты.Ответственный);
	//Запрос.УстановитьПараметр("Комментарий",           Реквизиты.Комментарий);
	//Запрос.УстановитьПараметр("ХозяйственнаяОперация", Реквизиты.ХозяйственнаяОперация);
	//Запрос.УстановитьПараметр("Партнер", 			   Реквизиты.Партнер);
	
	Запрос.УстановитьПараметр(
		"ИдентификаторМетаданных",
		ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Тип("ДокументСсылка.др_РеализацияЯрлыков")));
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаДр_ЯрлыкиНаСкладах(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "др_ЯрлыкиНаСкладах";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	                               
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаЯрлыки.НомерСтроки КАК НомерСтроки,
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаЯрлыки.др_Ярлык КАК Ярлык,
	|	ТаблицаЯрлыки.Склад КАК Склад,
	|	ТаблицаЯрлыки.Количество КАК Количество
	|ИЗ
	|	Документ.др_РеализацияЯрлыков.Ярлыки КАК ТаблицаЯрлыки
	|ГДЕ
	|	ТаблицаЯрлыки.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДр_Продажи(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "Др_Продажи";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	&Период КАК Период,
	|	ТаблицаТовары.др_Ярлык КАК др_Ярлык,
	|	ТаблицаТовары.Склад КАК Склад,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.Подразделение КАК Подразделение
	|ИЗ
	|	Документ.др_РеализацияЯрлыков.Ярлыки КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции


```

</details>

11.6.  Модуль набора записей др_ЯрлыкиНаСкладах

<details>

<summary>Просмотр кода Модуль набора записей др_ЯрлыкиНаСкладах</summary>

### Просмотр кода

```

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	БлокироватьДляИзменения = Истина;
	
	// Текущее состояние набора помещается во временную таблицу "Движениядр_ЯрлыкиНаСкладахПередЗаписью",
	// чтобы при записи получить изменение нового набора относительно текущего.

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.СвойстваДокумента.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Ярлык КАК Ярлык,
	|	Таблица.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Таблица.Количество
	|		ИНАЧЕ -Таблица.Количество
	|	КОНЕЦ КАК КоличествоПередЗаписью
	|ПОМЕСТИТЬ ДвиженияДр_ЯрлыкиНаСкладахПередЗаписью
	|ИЗ
	|	РегистрНакопления.Др_ЯрлыкиНаСкладах КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый";
	результат = Запрос.Выполнить();
	тз=результат.Выгрузить(); 
	ДополнительныеСвойства.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.Ярлык КАК Ярлык,
	|	ТаблицаИзменений.Склад КАК Склад,
	|	СУММА(ТаблицаИзменений.Количество) КАК Количество
	|ПОМЕСТИТЬ ДвиженияДр_ЯрлыкиНаСкладахИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Ярлык КАК Ярлык,
	|		Таблица.Склад КАК Склад,
	|		Таблица.КоличествоПередЗаписью КАК Количество
	|	ИЗ
	|		ДвиженияДр_ЯрлыкиНаСкладахПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Ярлык,
	|		Таблица.Склад,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.Количество
	|			ИНАЧЕ Таблица.Количество
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.др_ЯрлыкиНаСкладах КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Ярлык,
	|	ТаблицаИзменений.Склад
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.Количество) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДвиженияДр_ЯрлыкиНаСкладахПередЗаписью";
	
	МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
	РезультатЗапроса = МассивРезультатовЗапроса[0]; // РезультатЗапроса
	Выборка = РезультатЗапроса.Выбрать();
	ЕстьИзменения = Выборка.Следующий() И Выборка.Количество > 0;
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"ДвиженияДр_ЯрлыкиНаСкладахИзменение", ЕстьИзменения);
	
КонецПроцедуры



```

</details>


12. Добавить в документ ЗаказКлиента реквизит др_допНазначение (СправочникСсылка.Назначения).
Изменить проведение документа ЗаказКлиента.

12.1. Добавить в документ ЗаказКлиента реквизит др_допНазначение (СправочникСсылка.Назначения).

12.2. Изменить Процедуру ОтразитьРезерв(Запрос, ТекстыЗапроса, Регистры) модуль менеджера документа ЗаказКлиента


<details>

<summary>Просмотр кода</summary>

### Просмотр кода

```

Процедура ОтразитьРезерв(Запрос, ТекстыЗапроса, Регистры)
	
	ТекстЗапросаДанныхДокумента = 
	"ВЫБРАТЬ
	|	ТоварыДокумента.Ссылка				КАК Ссылка,
	|	ДанныеШапки.Дата					КАК Период,
	|	ТоварыДокумента.Ссылка				КАК Заказ,
	|	НЕОПРЕДЕЛЕНО						КАК Накладная,
	|	ЛОЖЬ								КАК Исправление,
	|	НЕОПРЕДЕЛЕНО						КАК ИсправляемыйДокумент,
	|	ДанныеШапки.Партнер					КАК Получатель,
	|	ТоварыДокумента.Склад				КАК Склад,
	|	ТоварыДокумента.Номенклатура		КАК Номенклатура,
	|	ТоварыДокумента.Характеристика		КАК Характеристика,
	//++ НАЧАЛО ДОРАБОТКИ 1729 Задача 12.2.-->
	// Типовой код:
	//|	ВЫБОР
	//|		КОГДА ТоварыДокумента.Обособленно
	//|			ТОГДА ДанныеШапки.Назначение
	//|		ИНАЧЕ &НазначениеПоУмолчанию
	//|	КОНЕЦ								КАК Назначение,
	// Добавленный код:
	|	ВЫБОР КОГДА ТоварыДокумента.Обособленно ТОГДА
	|				
	|			ВЫБОР
	|				КОГДА &др_допНазначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|					ТОГДА &др_допНазначение
	|				ИНАЧЕ 
	|					ДанныеШапки.Назначение
	|			КОНЕЦ 
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
    |
	|	КОНЕЦ     КАК Назначение,
	|
	//-- КОНЕЦ ДОРАБОТКИ н1<--		
	
	|	ТоварыДокумента.Серия				КАК Серия,
	|	ТоварыДокумента.СтатусУказанияСерий	КАК СтатусУказанияСерий,
	|	ТоварыДокумента.Количество			КАК Количество,
	|	ЛОЖЬ								КАК СверхЗаказа,
	|	ТоварыДокумента.Отменено			КАК Отменено,
	|	ЛОЖЬ								КАК ЭтоНакладная,
	|	ИСТИНА								КАК ОтгрузкаПоЗаказу
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК ТоварыДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ДанныеШапки
	|		ПО ТоварыДокумента.Ссылка = ДанныеШапки.Ссылка
	|
	|ГДЕ
	|	ТоварыДокумента.Ссылка В(&Ссылка)
	|	И Не ДанныеШапки.ЭтоЗаказКакСчет
	|	И ДанныеШапки.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиентуРеглУчет)
	|	И (ТоварыДокумента.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.СоСклада)
	|		ИЛИ ТоварыДокумента.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить)
	|			И ДанныеШапки.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовКлиентов.НеСогласован))
	|	И ТоварыДокумента.Количество <> 0";
	
	ТекстЗапросаДанныхДокумента = СтрЗаменить(ТекстЗапросаДанныхДокумента,
												"&НазначениеПоУмолчанию",
												"ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)");
	
	СкладыСервер.ОтразитьРезерв(Запрос, ТекстыЗапроса, Регистры, ТекстЗапросаДанныхДокумента);
	
КонецПроцедуры

```

</details>


12.3. Изменить Процедуру ЗапланироватьОтгрузкуТоваров(Запрос, ТекстыЗапроса, Регистры) модуль менеджера документа ЗаказКлиента


<details>

<summary>Просмотр кода</summary>

### Просмотр кода

```
Процедура ЗапланироватьОтгрузкуТоваров(Запрос, ТекстыЗапроса, Регистры)

	ТекстЗапросаДанныхДокумента = 
	"ВЫБРАТЬ
	|	ТоварыДокумента.Ссылка				КАК Ссылка,
	|	ТоварыДокумента.ДатаОтгрузки		КАК Период,
	|	ТоварыДокумента.Ссылка				КАК Заказ,
	|	НЕОПРЕДЕЛЕНО						КАК Накладная,
	|	ЛОЖЬ								КАК Исправление,
	|	НЕОПРЕДЕЛЕНО						КАК ИсправляемыйДокумент,
	|	ДанныеШапки.Партнер					КАК Получатель,
	|	ТоварыДокумента.Склад				КАК Склад,
	|	ТоварыДокумента.Номенклатура		КАК Номенклатура,
	|	ТоварыДокумента.Характеристика		КАК Характеристика,
	//++ НАЧАЛО ДОРАБОТКИ 1729 Задача 12.3-->
	// Типовой код:
	//|	ВЫБОР
	//|		КОГДА ТоварыДокумента.Обособленно
	//|			ТОГДА ДанныеШапки.Назначение
	//|		ИНАЧЕ &НазначениеПоУмолчанию
	//|	КОНЕЦ								КАК Назначение,
	// Добавленный код:
	|	ВЫБОР КОГДА ТоварыДокумента.Обособленно ТОГДА
	|				
	|			ВЫБОР
	|				КОГДА &др_допНазначение <> &НазначениеПоУмолчанию
	|					ТОГДА &др_допНазначение
	|				ИНАЧЕ 
	|					ДанныеШапки.Назначение
	|			КОНЕЦ 
	|		ИНАЧЕ &НазначениеПоУмолчанию
    |
	|	КОНЕЦ     КАК Назначение,
	|
	//-- КОНЕЦ ДОРАБОТКИ 1729 Задача 12.3<--		
	
	|	ВЫБОР
	|		КОГДА ТоварыДокумента.Обособленно
	|			ТОГДА ДанныеШапки.Назначение
	|		ИНАЧЕ &НазначениеПоУмолчанию
	|	КОНЕЦ								КАК Назначение,
	|	ТоварыДокумента.Серия				КАК Серия,
	|	ТоварыДокумента.СтатусУказанияСерий	КАК СтатусУказанияСерий,
	|	ТоварыДокумента.Количество			КАК Количество,
	|	ЛОЖЬ								КАК СверхЗаказа,
	|	ТоварыДокумента.Отменено			КАК Отменено,
	|	ЛОЖЬ								КАК ЭтоНакладная,
	|	ИСТИНА								КАК ОтгрузкаПоЗаказу
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК ТоварыДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ДанныеШапки
	|		ПО ТоварыДокумента.Ссылка = ДанныеШапки.Ссылка
	|
	|ГДЕ
	|	ТоварыДокумента.Ссылка В(&Ссылка)
	|	И Не ДанныеШапки.ЭтоЗаказКакСчет
	|	И ДанныеШапки.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовКлиентов.НеСогласован)
	|	И ДанныеШапки.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиентуРеглУчет)
	|	И ТоварыДокумента.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить)
	|	И ТоварыДокумента.Количество <> 0";
	
	ТекстЗапросаДанныхДокумента = СтрЗаменить(ТекстЗапросаДанныхДокумента,
												"&НазначениеПоУмолчанию",
												"ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)");
	
	СкладыСервер.ЗапланироватьОтгрузкуТоваров(Запрос, ТекстыЗапроса, Регистры, ТекстЗапросаДанныхДокумента);
	
КонецПроцедуры

```

</details>


12.4. Изменить Процедуру ОтразитьРаспределениеЗапасовДвижения(Запрос, ТекстыЗапроса, Регистры) модуль менеджера документа ЗаказКлиента


<details>

<summary>Просмотр кода</summary>

### Просмотр кода

```
Процедура ОтразитьРаспределениеЗапасовДвижения(Запрос, ТекстыЗапроса, Регистры)
	
	ТекстЗапросаТабЧасть =
		"ВЫБРАТЬ
		|	ТабЧасть.Ссылка                                     КАК Ссылка,
		|	ТабЧасть.Ссылка.Дата                                КАК Период,
		|	ТабЧасть.Номенклатура                               КАК Номенклатура,
		|	ТабЧасть.Характеристика                             КАК Характеристика,
		|	
		|	ВЫБОР КОГДА ТабЧасть.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа) ТОГДА
		|			ТабЧасть.Подразделение
		|		ИНАЧЕ
		|			ТабЧасть.Склад
		|		КОНЕЦ КАК Склад,
		//++ НАЧАЛО ДОРАБОТКИ 1729 Задача 12.4-->
		// Типовой код:
		//|	ВЫБОР КОГДА ТабЧасть.Обособленно ТОГДА
		//|				
		//|				ТабЧасть.Ссылка.Назначение
		//|				
		//|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		//|		КОНЕЦ КАК Назначение,
		// Добавленный код:
		|	ВЫБОР КОГДА ТабЧасть.Обособленно ТОГДА
		|				
		|			ВЫБОР
		|				КОГДА &др_допНазначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|					ТОГДА &др_допНазначение
		|				ИНАЧЕ 
		|					ТабЧасть.Ссылка.Назначение
		|			КОНЕЦ 
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
        |
		|	КОНЕЦ     КАК Назначение,
		|
		//-- КОНЕЦ ДОРАБОТКИ <--		
		|	
		|	ТабЧасть.Количество     КАК Количество,
		|	ТабЧасть.Ссылка         КАК ЗапланированныйРасходРаспределенногоЗапаса,
		|	ИСТИНА                  КАК КонтрольСвободногоОстатка,
		|	ЛОЖЬ                    КАК ИгнорироватьРезервыПриКонтролеОстатков
		|ИЗ
		|	Документ.ЗаказКлиента.Товары КАК ТабЧасть
		|ГДЕ
		|	НЕ ТабЧасть.Отменено
		|		И ТабЧасть.Ссылка.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовКлиентов.НеСогласован)
		|		И ТабЧасть.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить)";
	
	РаспределениеЗапасовДвижения.РасходЗапаса(Запрос, ТекстыЗапроса, Регистры, ТекстЗапросаТабЧасть);
	
	ТекстЗапросаТабЧасть =
		"ВЫБРАТЬ
		|	ТабЧасть.Ссылка                                     КАК Ссылка,
		|	ТабЧасть.Ссылка.Дата                                КАК Период,
		|	ТабЧасть.Номенклатура                               КАК Номенклатура,
		|	ТабЧасть.Характеристика                             КАК Характеристика,
		|	
		|	ВЫБОР КОГДА ТабЧасть.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа) ТОГДА
		|			ТабЧасть.Подразделение
		|		ИНАЧЕ
		|			ТабЧасть.Склад
		|		КОНЕЦ КАК Склад,
		|	
		//++ НАЧАЛО ДОРАБОТКИ 1729 Задача 12.4-->
		//|	ВЫБОР КОГДА ТабЧасть.Обособленно ТОГДА
		//|				
		//|				ТабЧасть.Ссылка.Назначение
		//|				
		//|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		//|		КОНЕЦ КАК Назначение,
		// Добавленный код:
		|	ВЫБОР КОГДА ТабЧасть.Обособленно ТОГДА
		|				
		|			ВЫБОР
		|				КОГДА &др_допНазначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|					ТОГДА &др_допНазначение
		|				ИНАЧЕ 
		|					ТабЧасть.Ссылка.Назначение
		|			КОНЕЦ 
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
        |
		|	КОНЕЦ     КАК Назначение,
		|
		//-- КОНЕЦ ДОРАБОТКИ <--		
		|	
		|	ТабЧасть.Количество                                 КАК Количество,
		|	ТабЧасть.ВариантОбеспечения                         КАК ВариантОбеспечения,
		|	ТабЧасть.Ссылка                                     КАК Заказ,
		|	
		|	ВЫБОР КОГДА ТабЧасть.Ссылка.НеОтгружатьЧастями ТОГДА
		|				ТабЧасть.Ссылка.ДатаОтгрузки
		|			ИНАЧЕ
		|				ТабЧасть.ДатаОтгрузки
		|		КОНЕЦ КАК ЖелаемаяДатаОтгрузки,
		|	ЛОЖЬ КАК ПоГрафику,
		|	НЕОПРЕДЕЛЕНО КАК РаспоряжениеВГрафике,
		|	0 КАК КоличествоВГрафике
		|ИЗ
		|	Документ.ЗаказКлиента.Товары КАК ТабЧасть
		|ГДЕ
		|	НЕ ТабЧасть.Отменено
		|		И ТабЧасть.Ссылка.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовКлиентов.НеСогласован)
		|		И ТабЧасть.ВариантОбеспечения <> ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить)";
	
	РаспределениеЗапасовДвижения.ЗапланироватьРасходЗапаса(
		Запрос,
		ТекстыЗапроса,
		Регистры,
		ТекстЗапросаТабЧасть);
	
	ТекстЗапросаТабЧасть =
		"ВЫБРАТЬ
		|	ТабЧасть.Ссылка                                     КАК Ссылка,
		|	ТабЧасть.Ссылка.Дата                                КАК Период,
		|	ТабЧасть.Номенклатура                               КАК Номенклатура,
		|	ТабЧасть.Характеристика                             КАК Характеристика,
		|	
		|	ВЫБОР КОГДА ТабЧасть.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа) ТОГДА
		|			ТабЧасть.Подразделение
		|		ИНАЧЕ
		|			ТабЧасть.Склад
		|		КОНЕЦ КАК Склад,
		//++ НАЧАЛО ДОРАБОТКИ 1729 Задача 12.4-->
		//|	ВЫБОР КОГДА ТабЧасть.Обособленно ТОГДА
		//|				
		//|				ТабЧасть.Ссылка.Назначение
		//|				
		//|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		//|		КОНЕЦ КАК Назначение,
		// Добавленный код:
		|	ВЫБОР КОГДА ТабЧасть.Обособленно ТОГДА
		|				
		|			ВЫБОР
		|				КОГДА &др_допНазначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|					ТОГДА &др_допНазначение
		|				ИНАЧЕ 
		|					ТабЧасть.Ссылка.Назначение
		|			КОНЕЦ 
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
        |
		|	КОНЕЦ     КАК Назначение,
		|
		//-- КОНЕЦ ДОРАБОТКИ н1<--		
		|	
		|	ТабЧасть.Количество                                 КАК Количество,
		|	
		|	ВЫБОР
		|		КОГДА ТабЧасть.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа) И ТабЧасть.Обособленно
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.КОбеспечению)
		|		КОГДА ТабЧасть.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа) И НЕ ТабЧасть.Обособленно
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.НеТребуется)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.СоСклада)
		|	КОНЕЦ КАК ВариантОбеспечения,
		|	
		|	ТабЧасть.Ссылка                                     КАК Заказ,
		|	
		|	ВЫБОР КОГДА ТабЧасть.Ссылка.НеОтгружатьЧастями ТОГДА
		|				ТабЧасть.Ссылка.ДатаОтгрузки
		|			ИНАЧЕ
		|				ТабЧасть.ДатаОтгрузки
		|		КОНЕЦ КАК ЖелаемаяДатаОтгрузки,
		|	ЛОЖЬ КАК ПоГрафику,
		|	НЕОПРЕДЕЛЕНО КАК РаспоряжениеВГрафике,
		|	0 КАК КоличествоВГрафике
		|ИЗ
		|	Документ.ЗаказКлиента.Товары КАК ТабЧасть
		|ГДЕ
		|	НЕ ТабЧасть.Отменено
		|		И ТабЧасть.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовКлиентов.НеСогласован)
		|		И ТабЧасть.ВариантОбеспечения В(
		|			ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить),
		|			ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.СоСклада))";
	
	РаспределениеЗапасовДвижения.ЗапланироватьРасходЗапаса(
		Запрос,
		ТекстыЗапроса,
		Регистры,
		ТекстЗапросаТабЧасть);
	
КонецПроцедуры

```

</details>


12.5. Изменить Процедуру ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка) модуль менеджера документа ЗаказКлиента


<details>

<summary>Просмотр кода</summary>

### Просмотр кода

```
Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеШапки.Дата                      КАК Период,
	|	ДанныеШапки.Партнер                   КАК Партнер,
	|	ДанныеШапки.Контрагент                КАК Контрагент,
	|	ДанныеШапки.Организация               КАК Организация,
	|	ДанныеШапки.Валюта                    КАК Валюта,
	|	ДанныеШапки.Склад                     КАК Склад,
	|	ДанныеШапки.Статус                    КАК Статус,
	|	ДанныеШапки.Ссылка                    КАК ЗаказКлиента,
	|	ДанныеШапки.Приоритет                 КАК Приоритет,
	|	ДанныеШапки.ЦенаВключаетНДС           КАК ЦенаВключаетНДС,
	|	ДанныеШапки.ХозяйственнаяОперация     КАК ХозяйственнаяОперация,
	|	ДанныеШапки.Договор                   КАК Договор,
	|	ДанныеШапки.Назначение                КАК Назначение,
	|	ДанныеШапки.Подразделение             КАК Подразделение,
	//++ НАЧАЛО ДОРАБОТКИ 1729 Задача 12.5 -->
	|	ДанныеШапки.др_допНазначение          КАК др_допНазначение,
	//-- КОНЕЦ ДОРАБОТКИ <--
	|		
	|	
	|	ЕСТЬNULL(ДанныеШапки.Договор.ДопустимаяСуммаЗадолженности, 0) КАК ДопустимаяСуммаЗадолженности,
	|	ДанныеШапки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеШапки.Номер КАК Номер,
	|	ДанныеШапки.Комментарий КАК Комментарий,
	|	ДанныеШапки.СуммаДокумента КАК СуммаДокумента,
	|	ДанныеШапки.Проведен КАК Проведен,
	|	ДанныеШапки.ПометкаУдаления КАК ПометкаУдаления,
	|	ДанныеШапки.НомерПоДаннымКлиента КАК НомерПоДаннымКлиента,
	|	ДанныеШапки.ДатаПоДаннымКлиента КАК ДатаПоДаннымКлиента,
	|	ДанныеШапки.Автор КАК Автор,
	|	ДанныеШапки.Менеджер КАК Менеджер,
	|	ДанныеШапки.ЭтоЗаказКакСчет КАК ЭтоЗаказКакСчет,
	|	ДанныеШапки.КартаЛояльности.Владелец.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	ЕСТЬNULL(ДанныеШапки.Договор.КомиссионныеПродажи25, ЛОЖЬ) КАК КомиссионныеПродажи25
	|
	|ИЗ
	|	Документ.ЗаказКлиента КАК ДанныеШапки
	|ГДЕ
	|	ДанныеШапки.Ссылка = &Ссылка";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период",          	 Реквизиты.Период);
	Запрос.УстановитьПараметр("ЗаказКлиента",    	 Реквизиты.ЗаказКлиента);
	Запрос.УстановитьПараметр("Период",          	 Реквизиты.Период);
	Запрос.УстановитьПараметр("Склад",           	 Реквизиты.Склад);
	Запрос.УстановитьПараметр("Партнер",           	 Реквизиты.Партнер);
	Запрос.УстановитьПараметр("Валюта",          	 Реквизиты.Валюта);
	Запрос.УстановитьПараметр("Статус",          	 Реквизиты.Статус);
	Запрос.УстановитьПараметр("Приоритет",           Реквизиты.Приоритет);
	Запрос.УстановитьПараметр("Организация",     	 Реквизиты.Организация);
	Запрос.УстановитьПараметр("ЦенаВключаетНДС", 	 Реквизиты.ЦенаВключаетНДС);
	Запрос.УстановитьПараметр("Назначение", 		 Реквизиты.Назначение);
	Запрос.УстановитьПараметр("Подразделение", 		 Реквизиты.Подразделение);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", Реквизиты.ХозяйственнаяОперация);
	Запрос.УстановитьПараметр("Договор",			 Реквизиты.Договор);
	Запрос.УстановитьПараметр("ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента", 
		ПолучитьФункциональнуюОпцию("ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента"));
	Запрос.УстановитьПараметр("ИспользоватьРасширенныеВозможностиЗаказаКлиента", Не Реквизиты.ЭтоЗаказКакСчет);
	Запрос.УстановитьПараметр("ДопустимаяСуммаЗадолженности", Реквизиты.ДопустимаяСуммаЗадолженности);
	Запрос.УстановитьПараметр("Контрагент",           Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("НаправлениеДеятельности", Реквизиты.НаправлениеДеятельности);
	Запрос.УстановитьПараметр("Номер",                Реквизиты.Номер);
	Запрос.УстановитьПараметр("Менеджер",             Реквизиты.Менеджер);
	Запрос.УстановитьПараметр("Автор",                Реквизиты.Автор);
	Запрос.УстановитьПараметр("Комментарий",          Реквизиты.Комментарий);
	Запрос.УстановитьПараметр("Проведен",             Реквизиты.Проведен);
	Запрос.УстановитьПараметр("ПометкаУдаления",      Реквизиты.ПометкаУдаления);
	Запрос.УстановитьПараметр("ДатаПоДаннымКлиента",  Реквизиты.ДатаПоДаннымКлиента);
	Запрос.УстановитьПараметр("НомерПоДаннымКлиента", Реквизиты.НомерПоДаннымКлиента);
	Запрос.УстановитьПараметр("СуммаДокумента",       Реквизиты.СуммаДокумента);
	Запрос.УстановитьПараметр("ИдентификаторМетаданных",     ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(ДокументСсылка)));
	Запрос.УстановитьПараметр("ХозОперацииПередачиТоваров",  ПродажиСервер.ОперацииПередачиТоваров());
	Запрос.УстановитьПараметр("БонуснаяПрограммаЛояльности", Реквизиты.БонуснаяПрограммаЛояльности);
	Запрос.УстановитьПараметр("ТекущаяДата",  ТекущаяУниверсальнаяДата());
	Запрос.УстановитьПараметр("КомиссионныеПродажи25",Реквизиты.КомиссионныеПродажи25);

	//++ НАЧАЛО ДОРАБОТКИ 1729 Задача 12.5 -->
	Запрос.УстановитьПараметр("др_допНазначение",        Реквизиты.др_допНазначение);
	//-- КОНЕЦ ДОРАБОТКИ <--	
	
	ИнформацияПоДоговору = Неопределено;
	Если ЗначениеЗаполнено(Реквизиты.Договор) Тогда
		ШаблонСтроки = "ru = 'По договору ""%1""';
						|en = 'Under the ""%1"" contract'"; // @НСтр
		ИнформацияПоДоговору = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСтроки, Реквизиты.Договор);
	КонецЕсли;
	Запрос.УстановитьПараметр("ИнформацияПоДоговору", ИнформацияПоДоговору);

	
КонецПроцедуры


```

</details>

13. Добавление своих отборов в обработку по формированию потребностей (ОбеспечениеПотребностей)

13.1. Изменить  макет МакетКомпоновкиДляСерверныхОтборов

<details>

<summary>Просмотр кода</summary>

### Просмотр кода

```
 	
//1729 Задача 13.1. Изменить  макет МакетКомпоновкиДляСерверныхОтборов

ВЫБРАТЬ
	Склады.Ссылка                                            КАК Склад,
	ВЫРАЗИТЬ(NULL КАК Справочник.Склады)                     КАК СкладОтгрузкиЗаказа,
	ВЫРАЗИТЬ(NULL КАК Справочник.СтруктураПредприятия)       КАК ПодразделениеПолучатель,
	ВЫРАЗИТЬ(NULL КАК Справочник.Номенклатура)               КАК Номенклатура,
	СпрХарактеристика.Ссылка                                 КАК Характеристика,
	СпрНазначение.Ссылка                                     КАК Назначение,
	СпрНаправленияДеятельности.Ссылка                        КАК НаправлениеДеятельности,
	СпрПодразделение.Ссылка                                  КАК Подразделение,
	СпрСпособыОбеспечения.Ссылка                             КАК СпособОбеспечения,
	СпрСегментНоменклатуры.Ссылка                            КАК СегментНоменклатуры,
	ВЫРАЗИТЬ(NULL КАК Перечисление.ТипыОбеспечения)          КАК ТипОбеспечения,
	ВЫРАЗИТЬ(NULL КАК ЧИСЛО)                                 КАК КодСтроки,
	ВЫРАЗИТЬ(NULL КАК ЧИСЛО)                                 КАК РаспределеноНаСкладе,
	ВЫРАЗИТЬ(NULL КАК ЧИСЛО)                                 КАК РаспределеноИзЗаказов,
	ВЫРАЗИТЬ(NULL КАК ЧИСЛО)                                 КАК НеОбеспечено,
	ВЫРАЗИТЬ(NULL КАК ДАТА)                                  КАК ДатаОтгрузки,
	ВЫРАЗИТЬ(NULL КАК ДАТА)                                  КАК ДатаОтгрузкиЗаказа,
	СпрНазначение.Заказ                                      КАК Заказ,
	//1729 Задача 13
    ВЫБОР
    	КОГДА ДопСведенияЗаказ.Значение = ИСТИНА
     	 ТОГДА ИСТИНА
    		ИНАЧЕ ЛОЖЬ
  	КОНЕЦ 													 КАК ХорошийЗаказ,
    //-1729
	СпрСпособыОбеспечения.ИсточникОбеспеченияПотребностей    КАК ИсточникОбеспечения,

	&ИспользуетсяОтборПоСегментуНоменклатуры                 КАК Параметр1,
	&ИспользоватьХарактеристикиНоменклатуры                  КАК Параметр2,
	&ИспользуетсяОтборПоЗаказам                              КАК Параметр3
	ИЗ
		// Поля отбора зависимые от функциональных опций.
		Справочник.ХарактеристикиНоменклатуры     КАК СпрХарактеристика,
		Справочник.Склады                         КАК Склады,
		Справочник.НаправленияДеятельности        КАК СпрНаправленияДеятельности,
		Справочник.Назначения                     КАК СпрНазначение,
		
	  	//1729 Задача 13
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДопСведенияЗаказ
	    ПО СпрНазначение.Заказ = ДопСведенияЗаказ.Объект
	      И (ВЫБОР
		        КОГДА ТИПЗНАЧЕНИЯ(СпрНазначение.Заказ) = ТИП(Документ.ЗаказНаВнутреннееПотребление)
	          ТОГДА ДопСведенияЗаказ.Свойство.Имя = "ХорошийЗаказ"
	        ИНАЧЕ ЛОЖЬ
	      КОНЕЦ),
     //КонецВставки
		
		
		Справочник.СтруктураПредприятия           КАК СпрПодразделение,
		Справочник.СегментыНоменклатуры           КАК СпрСегментНоменклатуры,
		Справочник.СпособыОбеспеченияПотребностей КАК СпрСпособыОбеспечения

{ВЫБРАТЬ
	Склад.*                 КАК Склад,
	СкладОтгрузкиЗаказа.*   КАК СкладОтгрузкиЗаказа,
	Номенклатура.*          КАК Номенклатура,
	Характеристика.*        КАК Характеристика,
	Назначение.*            КАК Назначение,
	НаправлениеДеятельности.* КАК НаправлениеДеятельности,
	Подразделение.*         КАК Подразделение,
	ПодразделениеПолучатель.* КАК ПодразделениеПолучатель,
	СпособОбеспечения.*     КАК СпособОбеспечения,
	СегментНоменклатуры.*   КАК СегментНоменклатуры,
	ТипОбеспечения          КАК ТипОбеспечения,
	ИсточникОбеспечения.*   КАК ИсточникОбеспечения,

	КодСтроки               КАК КодСтроки,
	Заказ.*                 КАК Заказ,    
	//1729 Задача 13
  	ХорошийЗаказ 			КАК ХорошийЗаказ,
  	//-1729
	ДатаОтгрузки            КАК ДатаОтгрузки,
	ДатаОтгрузкиЗаказа      КАК ДатаОтгрузкиЗаказа,
	РаспределеноНаСкладе    КАК РаспределеноНаСкладе,
	РаспределеноИзЗаказов   КАК РаспределеноИзЗаказов,
	НеОбеспечено            КАК НеОбеспечено}
{ГДЕ
	Склады.Ссылка.*                                                КАК Склад,
	ВЫРАЗИТЬ(NULL КАК Справочник.СтруктураПредприятия)             КАК ПодразделениеПолучатель,
	ВЫРАЗИТЬ(NULL КАК Справочник.Номенклатура).*                   КАК Номенклатура,
	СпрХарактеристика.Ссылка                                       КАК Характеристика,
	СпрНазначение.Ссылка.*                                         КАК Назначение,
	СпрНаправленияДеятельности.Ссылка.*                            КАК НаправлениеДеятельности,
	СпрПодразделение.Ссылка                                        КАК Подразделение,
	СпрСпособыОбеспечения.Ссылка.*                                 КАК СпособОбеспечения,
	СпрСегментНоменклатуры.Ссылка                                  КАК СегментНоменклатуры,
	ВЫРАЗИТЬ(NULL КАК Перечисление.ТипыОбеспечения)                КАК ТипОбеспечения,
	СпрНазначение.Заказ.*                                          КАК Заказ,
	//1729 Задача 13
	(ВЫБОР
			КОГДА ДопСведенияЗаказ.Значение = ИСТИНА
				ТОГДА ИСТИНА
			ИНАЧЕ ЛОЖЬ
		КОНЕЦ) 													   КАК ХорошийЗаказ,	
	//-1729
	СпрСпособыОбеспечения.ИсточникОбеспеченияПотребностей.*        КАК ИсточникОбеспечения

}

```

</details>

13.2. Изменить функцию менеджера Функция ВременнаяТаблицаОтборПоЗаказам()

<details>

<summary>Просмотр кода</summary>

### Просмотр кода

```
 	
//1729 Задача 13.2. 

Функция ВременнаяТаблицаОтборПоЗаказам()
	
	МассивТекстов = Новый Массив;
	
	МассивТекстов.Добавить(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РеестрДокументов.Ссылка КАК Заказ
		|ПОМЕСТИТЬ ОтборПоЗаказам
		|ИЗ РегистрСведений.РеестрДокументов КАК РеестрДокументов
  		//1729 Задача 13.2.
 		|  ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДопСведенияЗаказ
    	|  ПО РеестрДокументов.Ссылка = ДопСведенияЗаказ.Объект
    	|    И ВЫБОР 
    	|    КОГДА ТипЗначения(РеестрДокументов.Ссылка) = Тип(Документ.ЗаказНаВнутреннееПотребление) 
    	|      ТОГДА ДопСведенияЗаказ.Свойство.Имя = ""ХорошийЗаказ""
    	|    ИНАЧЕ ЛОЖЬ КОНЕЦ
    	//-1729		
		|ГДЕ
		|	&ИспользуетсяОтборПоЗаказам
		|{ГДЕ
		|	РеестрДокументов.Ссылка.* КАК Заказ,
  		//1729 Задача 13.2.
	    |  ВЫБОР КОГДА ДопСведенияЗаказ.Значение = ИСТИНА ТОГДА ИСТИНА ИНАЧЕ ЛОЖЬ КОНЕЦ КАК ХорошийЗаказ,
    	//-1729		
		|	РеестрДокументов.НаправлениеДеятельности.* КАК НаправлениеДеятельности}");
	
	//++ НЕ УТКА
	Если ПравоДоступа("Чтение", Метаданные.Документы.ЭтапПроизводства2_2) Тогда
		МассивТекстов.Добавить(
			"ВЫБРАТЬ
			|	ЭтапПроизводства2_2.Ссылка
			|ИЗ
			|	Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
			|ГДЕ
			|	&ИспользуетсяОтборПоЗаказам
			|{ГДЕ
			|	ЭтапПроизводства2_2.Распоряжение.* КАК Заказ,
			|	ЭтапПроизводства2_2.НаправлениеДеятельности.* КАК НаправлениеДеятельности}
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ЭтапПроизводства2_2.Ссылка
			|ИЗ
			|	Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
			|ГДЕ
			|	&ИспользуетсяОтборПоЗаказам
			|	И ЭтапПроизводства2_2.ПроизводствоНаСтороне
			|{ГДЕ
			|	ЭтапПроизводства2_2.ЗаказПереработчику.* КАК Заказ,
			|	ЭтапПроизводства2_2.НаправлениеДеятельности.* КАК НаправлениеДеятельности}");
	КонецЕсли;
	
	Если ПравоДоступа("Чтение", Метаданные.РегистрыСведений.СтруктураЗаказа) Тогда
		МассивТекстов.Добавить(
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	СтруктураЗаказа.ЗаказНаПроизводство
			|ИЗ
			|	РегистрСведений.СтруктураЗаказа КАК СтруктураЗаказа
			|ГДЕ
			|	&ИспользуетсяОтборПоЗаказам
			|{ГДЕ
			|	СтруктураЗаказа.ЗаказНаПроизводство.* КАК Заказ,
			|	СтруктураЗаказа.ЗаказНаПроизводство.НаправлениеДеятельности.* КАК НаправлениеДеятельности}");
	КонецЕсли;
	//-- НЕ УТКА
	
	Возврат СтрСоединить(МассивТекстов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());

КонецФункции



```

</details>

13.3. Изменить функцию менеджера Функция ВременнаяТаблицаТоваровДляПоддержанияЗапаса()

<details>

<summary>Просмотр кода</summary>

### Просмотр кода

```
 	
//1729 Задача 13.3. Изменить функцию менеджера Функция ВременнаяТаблицаТоваровДляПоддержанияЗапаса()
Функция ВременнаяТаблицаТоваровДляПоддержанияЗапаса()

	ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НоменклатураСегмента.Номенклатура    КАК Номенклатура,
		|	НоменклатураСегмента.Характеристика  КАК Характеристика
		|ПОМЕСТИТЬ ОтборПоСегментам
		|ИЗ
		|	РегистрСведений.НоменклатураСегмента КАК НоменклатураСегмента
		|		{ГДЕ
		|			НоменклатураСегмента.Номенклатура.*    КАК Номенклатура,
		|			НоменклатураСегмента.Характеристика.*  КАК Характеристика,
		|			НоменклатураСегмента.Сегмент.*         КАК СегментНоменклатуры}
		|ГДЕ
		|	&ИспользуетсяОтборПоСегментуНоменклатуры
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|" + Справочники.ФорматыМагазинов.ТекстЗапросаВтФорматыСкладов(Ложь)
		+ РегистрыСведений.ТоварныеОграничения.ВременнаяТаблицаТоварыПоддерживаемогоЗапаса()
		+ "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Т.Номенклатура                       КАК Номенклатура,
		|	Т.Характеристика                     КАК Характеристика,
		|	Т.Склад                              КАК Склад,
		|
		|	СпрСпособ.Ссылка                     КАК СпособОбеспечения
		|
		|ПОМЕСТИТЬ ВтТовары
		|ИЗ
		|	ВтТоварыПоддерживаемогоЗапаса КАК Т
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОбеспеченияПотребностей КАК СпрСпособ
		|		ПО &ПодстановкаОсновногоСпособаОбеспечения
		|
		//отборы компоновщика
		|{ГДЕ
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)              КАК Назначение,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)    КАК ПодразделениеПолучатель,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
		//1729 Задача 13.2
		|	ЛОЖЬ 													  КАК ХорошийЗаказ
		//-1729
		|	ВЫРАЗИТЬ(NULL КАК Справочник.Назначения).Заказ.*          КАК Заказ}
		|
		|{ГДЕ
		|	(ЕСТЬNULL(СпрСпособ.Ссылка,
		|		ЗНАЧЕНИЕ(Справочник.СпособыОбеспеченияПотребностей.ПустаяСсылка))).*           КАК СпособОбеспечения,
		|	ЕСТЬNULL(СпрСпособ.ТипОбеспечения, ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Покупка)) КАК ТипОбеспечения,
		|	ВЫРАЗИТЬ(
		|		ВЫБОР
		|			КОГДА СпрСпособ.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) ТОГДА
		|				СпрСпособ.Подразделение
		|			ИНАЧЕ
		|				ЕСТЬNULL(Т.Склад.Подразделение,
		|					ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
		|		КОНЕЦ КАК Справочник.СтруктураПредприятия).*                                   КАК Подразделение,
		|	(ЕСТЬNULL(СпрСпособ.ИсточникОбеспеченияПотребностей, НЕОПРЕДЕЛЕНО)).*              КАК ИсточникОбеспечения}
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад,
		|	СпособОбеспечения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВтФорматыСкладов;
		|
		|////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	Т.Номенклатура                       КАК Номенклатура,
		|	Т.Характеристика                     КАК Характеристика,
		|	Т.Склад                              КАК Склад,
		|	Т.СпособОбеспечения                  КАК СпособОбеспечения
		|{ВЫБРАТЬ
		|	Номенклатура.*      КАК Номенклатура,
		|	Характеристика.*    КАК Характеристика,
		|	Склад.*             КАК Склад,
		|	СпособОбеспечения.* КАК СпособОбеспечения}
		|ИЗ
		|	ВтТовары КАК Т";

	ТекстЗапроса = РегистрыСведений.СхемыОбеспечения.ПодставитьСоединениеДляПолученияСпособаОбеспечения(
		ТекстЗапроса, "ПодстановкаОсновногоСпособаОбеспечения");
	
	Возврат ТекстЗапроса;

КонецФункции



```

</details>

13.4. Изменить функцию менеджера Функция ПолучитьЗапросСОтборамиКомпоновкиДанных()

<details>

<summary>Просмотр кода</summary>

### Код формы

```
 	
//1729 Задача 13.4. 

Функция ПолучитьЗапросСОтборамиКомпоновкиДанных(АдресСхемыКомпоновкиДанных, КомпоновщикНастроек, ИмяНабораДанных)
	
	СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
	
	СегментыСервер.ВключитьОтборПоСегментуНоменклатурыВСКД(КомпоновщикНастроек);
	
	ОтборКомпоновки = КомпоновщикНастроек.ПолучитьНастройки().Отбор;
	ИспользуетсяОтборПоЗаказам = НайтиОтборРекурсивно(ОтборКомпоновки.Элементы, Новый ПолеКомпоновкиДанных("Заказ"))
	//1729 Задача 13.4
		ИЛИ НайтиОтборРекурсивно(ОтборКомпоновки.Элементы, Новый ПолеКомпоновкиДанных("ХорошийЗаказ"))
	//1729
		Или НайтиОтборРекурсивно(ОтборКомпоновки.Элементы, Новый ПолеКомпоновкиДанных("НаправлениеДеятельности"));
	
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ИспользуетсяОтборПоЗаказам", Истина, ИспользуетсяОтборПоЗаказам);
	
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных,
		КомпоновщикНастроек.ПолучитьНастройки(),,,, Ложь);

	Запрос = Новый Запрос(МакетКомпоновкиДанных.НаборыДанных[ИмяНабораДанных].Запрос);

	Для каждого ПараметрКомпоновки Из МакетКомпоновкиДанных.ЗначенияПараметров Цикл

		Запрос.УстановитьПараметр(ПараметрКомпоновки.Имя, ПараметрКомпоновки.Значение);

	КонецЦикла;
	ИспользоватьХарактеристикиНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры");
	Запрос.УстановитьПараметр("ИспользоватьХарактеристикиНоменклатуры", ИспользоватьХарактеристикиНоменклатуры);
	Возврат Запрос;

КонецФункции



```

</details>

14. Настройка открытия форм при начале работы системы в 1С:ERP 2.5
Настройка открытия нетиповой ВНЕШНЕЙ обработки при начале работы системы.

14.1. Создаем новую обработку в дереве конфигурации. 
Назовем ее др_НачальнаяОбработка. Добавляем в нее новую форму “Форма”. 

14.2. Создаем новую роль. Назовем ее др_ИспользованиеНачальнойОбработки. 
Синоним – Использование начальной обработки. 
Даем права на использование и просмотр обработке из п14.1.

14.3. Добавляем в перечисление ФормыОткрываемыеПриНачалеРаботыСистемы 
 значение - др_НачальнаяОбработка.
 
14.4. В общем модуле ОткрытиеФормПриНачалеРаботыСистемыКлиентСерверПереопределяемый 
ищем функцию НастройкиФормы(). 

<details>

<summary>Просмотр кода</summary>

### Просмотр кода

```
Функция НастройкиФормы(Форма) Экспорт
 	
//1729 Задача 14.4. 
...

	ИначеЕсли Форма = ПредопределенноеЗначение("Перечисление.ФормыОткрываемыеПриНачалеРаботыСистемы.др_НачальнаяОбработка") Тогда
		
		СтруктураПараметров.Вставить("ИмяЗапускаемойФормы", "Обработка.др_НачальнаяОбработка.Форма.Форма");
		СтруктураПараметров.Вставить("Роль", 				"др_ИспользованиеНачальнойОбработки");
		СтруктураПараметров.Вставить("ПараметрЗапуска", 	"rash_UseProcessing");          
	
//1729- 
	
...
	
КонецПроцедуры


```

</details>

14.5. Обновляем базу.

14.6. Заходим в режим предприятия. Для роли из п14.2 создаем профиль и группу доступа.
Создаем профиль с ролью из п14.2.
Создаем группу доступа по этому профилю. Добавляем пользователей.

14.7. После создания профиля и группы доступа по п14.6 появится настройка открытия нашей обработки. 
Настройку можно увидеть в карточке пользователя по этой гиперссылке. 
Данные настроек хранятся в регистре сведений НастройкиОткрытияФормПриНачалеРаботыСистемы.

14.8. Заходим в настройку, ставим галочку “открывать по умолчанию”.

15. Настройка открытия нетиповой ВНЕШНЕЙ обработки при начале работы системы

15.1. В форму “Форма” обработки др_НачальнаяОбработка добавим реквизит
ИмяОбработкиСлужебное. Тип - строка неограниченной длины.

15.2. В модуль формы добавим следующий код

<details>

<summary>Просмотр кода</summary>

### Просмотр кода

```
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЭтотОбъект.Закрыть();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ИмяОбработки = РеквизитФормыВЗначение("Объект").Метаданные().Имя;
	ПостФикс = "ВО";
	Запрос = Новый Запрос;
	Запрос.Текст =
	
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|             ДопОтчетыИОбработки.Ссылка КАК Ссылка
	|ИЗ
	|             Справочник.ДополнительныеОтчетыИОбработки КАК ДопОтчетыИОбработки
	|ГДЕ
	|             ДопОтчетыИОбработки.ИмяОбъекта = &ИмяОбработки
	|             И ДопОтчетыИОбработки.Публикация =
	|               ЗНАЧЕНИЕ(Перечисление.ВариантыПубликацииДополнительныхОтчетовИОбработок.Используется)";
	
	Запрос.УстановитьПараметр("ИмяОбработки", СтрШаблон("%1_%2", ИмяОбработки, Постфикс));
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		
		Отказ = Истина;
		
	Иначе
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ИмяОбработкиСлужебное =
		ДополнительныеОтчетыИОбработкиВызовСервера.ПодключитьВнешнююОбработку(Выборка.Ссылка);
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если Не Отказ Тогда
		ОткрытьНовуюФорму();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьНовуюФорму()
	НаименованиеФормыЗапускаемойОбработки = СтрШаблон("ВнешняяОбработка.%1.Форма.Форма", ИмяОбработкиСлужебное); 
	ОткрытьФорму(НаименованиеФормыЗапускаемойОбработки);
КонецПроцедуры

//1

#КонецОбласти

```

</details>



16. Дополнительные реквизиты в сериях номенклатуры в 1С:ERP 2.5

16.1. Создаем новый ВидНоменклатуры  для серийного учета (_Продукция серийного учета)

16.1.1. Доп реквизит Создаем реквизит "Номер ярлыка" с типом значения число (10,0) (НомерЯрлыка_bca1b17d26e243e8ad04fd9038f569a6)

16.1.2. Доп реквизит Создаем реквизит  "Вес НЕТТО"  с типом значения число (15,3)


16.2. Допустим, работа с сериями готовой продукции осуществляется только из документа производство без заказа, 
через кнопку "Указать серии". Если нам что-то нужно передать в обработку регистрации серий,
то это лучше делать в процедуре формы документа "ОткрытьПодборСерий"

<details>

<summary>Просмотр кода</summary>

### Просмотр кода

```

...

		ПараметрыФормыУказанияСерий = ПараметрыФормыУказанияСерий(ИмяТЧ, ТекущиеДанныеИдентификатор);

//1729
        
        ПараметрыФормыУказанияСерий.Вставить("др_Партия",Объект.Ссылка);

//1729-
	
...


```

</details>



16.3. В обработке "ПодборСерийВДокументы" добавляем реквизит "др_партия"
для формы "РегистрацияИПодборСерийПоОднойСтрокеТоваров" на закладке реквизиты. 
Он послужит для хранения переданного значения. 
Аналогично добавляем реквизит мо_МаксНомерКатушки, 
он будет хранить последний из имеющихся номеров.
Проинициализируем реквизиты в процедуре "ПриСозданииНаСервере":

<details>

<summary>Просмотр кода</summary>

### Просмотр кода

```

...

//1729	
	Если Параметры.Свойство("др_партия") Тогда
		др_партия=Параметры.др_партия;
		др_МаксНомерКатушки=ПолучитьПоследнийНомерКатушки();      

	КонецЕсли;
//1729-	
КонецПроцедуры
	
	//1729
&НаСервере
Функция ПолучитьПоследнийНомерКатушки()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СерииНоменклатурыДополнительныеРеквизиты.Значение КАК Значение
		|ИЗ
		|	Справочник.СерииНоменклатуры.ДополнительныеРеквизиты КАК СерииНоменклатурыДополнительныеРеквизиты
		|ГДЕ
		|	СерииНоменклатурыДополнительныеРеквизиты.Свойство.Имя = &Имя
		|
		|УПОРЯДОЧИТЬ ПО
		|	Значение УБЫВ";
	
	Запрос.УстановитьПараметр("Имя", "НомерЯрлыка_bca1b17d26e243e8ad04fd9038f569a6");
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	МаксНомерКатушки = 0;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		МаксНомерКатушки = ВыборкаДетальныеЗаписи.Значение;
	КонецЦикла;
	Возврат МаксНомерКатушки ;
КонецФункции
//1729-	

	
	
...


```

</details>



 
16.4. При создании новой серии в ТЧ, нам нужно будет заполнить наш реквизит др_МаксНомерКатушки.
 Модифицируем процедуру "СерииПриНачалеРедактирования"

<details>

<summary>Просмотр кода</summary>

### Просмотр кода

```
//1729
	Если НоваяСтрока Тогда
		ПолеНомерКатушки=ПолучитьПолеДополнительногоРеквизитаНаФорме("НомерЯрлыка_bca1b17d26e243e8ad04fd9038f569a6","Объект_Серии");
		Если ЗначениеЗаполнено(ПолеНомерКатушки) Тогда
		     УстановитьЗначениеДополнительногоРеквизитаНаФорме(ПолеНомерКатушки,др_МаксНомерКатушки+1);
			 др_МаксНомерКатушки=др_МаксНомерКатушки+1;
		КонецЕсли;
	КонецЕсли;	
//1729-	
КонецПроцедуры
//1729
&НаСервере
Функция ПолучитьПолеДополнительногоРеквизитаНаФорме(ИмяРеквизита, ПрефиксТЧ="")
	СписокСвойств = ЭтаФорма[ПрефиксТЧ+"Свойства_ОписаниеДополнительныхРеквизитов"];
	Свойство=ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя",ИмяРеквизита);
	Если ЗначениеЗаполнено(Свойство) Тогда
		НайденныеСтроки =СписокСвойств.НайтиСтроки(Новый Структура("Свойство",Свойство));
		Если НайденныеСтроки.Количество()>0 Тогда
			Возврат НайденныеСтроки[0].ИмяРеквизитаЗначение;
		КонецЕсли;
	КонецЕсли;
	Возврат "";
КонецФункции

&НаКлиенте
Процедура УстановитьЗначениеДополнительногоРеквизитаНаФорме(ПолеДополнительногоРеквизитаНаФорме,ЗначениеРеквизита)
	Если НЕ ПолеДополнительногоРеквизитаНаФорме = Неопределено Тогда 
		Элементы.Серии.ТекущиеДанные[ПолеДополнительногоРеквизитаНаФорме]= ЗначениеРеквизита;
	КонецЕсли;
КонецПроцедуры
//1729-	


```

</details>
